[toc]

本章要点

- 简介
- 移位和循环移位指令
- 移位和循环移位的应用
- 乘法和除法指令
- 扩展加法和减法指令
- ASCII和非压缩十进制算术指令
- 压缩十进制算术指令

# 7.1 简介

每种汇编语言都有进行操作数移位的指令，移位和循环移位指令在控制硬件设备、加密数据以及实现高速的图形操作时特别有用。本章讲述如何进行移位和循环移位操作以及如何使用移位操作进行高效的乘法和除法运算。
接下来，我们将探究IA-32指令集中的乘法和除法指令，Intel根据运算是有符号的还是无符号的对乘法和除法指令进行分类。本章还将讲述如何把C++中的数学表达式翻译成汇编语言。编译器把复合表达式分解翻译成一系列机器指令，模拟编译器有助于理解其工作机制，同时也有助于更好地对汇编语言代码进行手工优化。通过本章的学习，读者对操作符优先级以及寄存器优化(在机器层）的机制将有更深入的理解。
你是否思考过计算机是如何加减多字整数的吗？本章中讲述的ADC(带进位加）指令和SB(带进位减）指令使得处理任意大小的整数更容易一些。在本章的最后，将讲述Intel的一些专门处理压缩十进制整数及整数字符串的算术指令。

# 7.2 移位和循环移位指令

和第6章介绍的位操作指令一样，移位指令也是最具汇编语言特征的指令。移位(shifting)的含义是在操作数内向左或向右移动数据位，Intel提供了多种移位指令(参见表7.1),表中所有的移位指令都影响溢出标志和进位标志。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.4yn3egi0tio0.webp)



![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.kxgo5fc5hhs.webp)

## 7.2.1 逻辑移位和算术移位

对于一个数字来说有两种最基本的移位操作。第一种为逻辑移位(logical shift)，即以0填充最后移出的位。在下图中，一个字节逻辑右移一位，请注意位7被赋了值0。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.2xdu1n54v6w0.webp)

## 7.2.2 SHL 指令

SHL(shiftleft)指令对目的操作数执行逻辑左移操作，最低位以0填充，移出的最高位送入进位标志(CF),原来进位标志中的值将丢失。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.3c7b93j6qxk0.webp)

SHL指令的第一个操作数是目的操作数，第二个操作数是移位的位数：

```assembly
SHL 目的操作数,移位位数
```

SHL指令允许使用下面的操作数类型：

```assembly
SHL 	reg,imm8
SHL 	mem,imm8
SHL 	reg,CL
SHL 	mem,CL
```

Intel8086/8088处理器要求imm8必须等于1,从80286及以上的处理器开始，imm8可以是0~255之间的整数。在任何Intel处理器上，都可以使用CL存放移位位数。这里列出的格式也适用于==SHR,SAL,SAR,ROR,ROL,RCR和RCL==指令。
例子：在下面的指令中，BL被左移一位。最高位被复制到进位标志中，最低位被清零。

```assembly
mov b1,8Fh		;BL = 10001111b
shl b1,1		;CF,BL = 1,00011110b
```

### 多次移位

一个值多次左移（移位位数大于1)时，进位标志中的值是最后移出最高有效位(MSB)的数据位。在下面的例子中，进位标志的值是位6(0)而非位7的值：

```assembly
mov a1,10000000b
shl al,2			;CF=0
```

右移也遵循同样的规则。

### 快速乘法

使用SHL指令可以进行2的次寡的高速乘法操作。任何操作数左移n位就相当于乘以$2^n$，例如，5左移1位就得到乘积5×2:

```assembly
mov 	dl,5
shl 	d1,1
```

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.ej229tufeu8.webp)

如果十进制数10左移2位，得到的结果和10×22是一样的：

```assembly
mov 	d1,10
shl 	dl,2		;(10*4)=40
```

## 7.2.3 SHR 指令

SHR指令(shift right)对目的操作数执行逻辑右移操作，移出的数据位以0代替，最低位被复制到进位标志中，原来的进位标志值丢失。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.457br139e0i0.webp)

SHR指令的格式与SHL相同。在下例中，AL中的最低位被复制到进位标志中，AL中的最高位被清0:

```assembly
mov al,0D0h		;AL=11010000b
shr al,1		;AL=01101000b,CF=0
```

### 多次移位

一个值多次右移（移位位数大于1)时，进位标志中的值是最后移出位0的数据位：

```assembly
mov al,00000010b
shr al,2			;AL=00000000b,CF=1
```

### 快速除法

任何无符号操作数逻辑右移n位就相当于该操作数除以$2^n$。例如32除以$2^1$的结果是16:

```assembly
mov 	dl,32
shr 	d1,1
```

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.35j5pw7q1m20.webp)

在下面的例子中，64被23除：

```assembly
mov al,01000000b		;AL=64
shr al,3				;除以8,AL=00001000b
```

(有符号数的除法要使用SAR指令移位，因为SAR指令保留符号位。）

## 7.2.4 SAL 和 SAR 指令

SAL(shift arithmetic left)指令与SHL指令等价。SAR(shift arithmetic right)指令对目的操作数执行算术右移操作：

![image-20230301223859430](../../../../Library/Application%20Support/typora-user-images/image-20230301223859430.png)

SAR,SHR指令的格式与SHL,SHR指令的格式相同，移位动作可以根据第二个操作数中的计数进行重复：

```assembly
SAR 目的操作数,移位位数
```

下面的例子显示了SAR复制符号位的情况，AL中的值在右移操作的前后都是负数：

```assembly
mov al,0F0h			;AL=11110000b(-16)
sar al,1			;AL=11111000b(-8)CF=0
```

### 有符号数的除法

使用SAR指令可以对有符号数进行除以2的次幕的快速操作。下例中-128
被$2^3$除，商是-16:

```assembly
mov d1,-128		;DL=10000000b
sar d1,3		;DL=11110000b
```

### 符号扩展AX至EAX

假设AX中存放着一个有符号整数，我们想把AX符号扩展至EAX,使用移位指令，可以按如下方法操作：首先左移EAX16位，然后再算术右移EAX16位：

```assembly
mov ax,-128		;EAX=???FF80h
shl eax,16		;EAX=FF800000h
sar eax,16		;EAX=FFFFFF80h
```

## 7.2.5 ROL指令

ROL(rotate left)指令在向左移动一位后，把最高位同时复制到进位标志和最低位中。其指令格式与SHL指令相同：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.6uy4ucndjf80.webp)

循环移位和移位的不同之处在于前者并不丢失任何数据位。从一端移走的数据位会出现在另一端。在下面的例子中，高位被同时复制到进位标志和位0中：

```assembly
mov al,40h		;AL=01000000b
rol al,1		;AL=10000000b,CF=0
rol al,1		;AL=00000001b,CF=1
rol al,1		;AL=00000010b,CF=0
```

### 多次循环左移

循环左移的移位位数大于1时，进位标志的值是最后移出最高有效位的数据位：

```assembly
mov al,00100000b
rol al,3		;CF=1,AL=00000001b
```

### 交换位组

可使用ROL指令交换一个字节的高半部分（位4~7)和低半部分（位0~3)。例如，26h无论是向左或向右左移4位都将得到62h:

```assembly
mov al,26h
rol al,4		;AL=62h
```

如果一个多字节整数循环移动4个数据位位，其效果相当于一个十六进制数位循环左移或右移1位。例如下例中，6A4Bh重复循环左移4位，最后得到了其原始值：

```assembly
mov ax,6A4Bh
rol ax,4		;AX=A4B6h
rol ax,4		;AX=4B6Ah
rol ax,4		;AX=B6A4h
rol ax,4		;AX=6A4Bh
```

## 7.2.6 ROR指令

ROR(rotate right)指令在向右移动一位后，把最低位同时复制到进位标志位和最高位中指令的格式与SHL指令相同。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.4e1ns21xoww0.webp)

在下面的例子中，注意最低位被同时复制到进位标志和结果的最高位中：

```assembly
mov al,01h		;AL=00000001b
ror al,1		;AL=10000000b,CF=1
ror al,1		;AL=01000000b,CF=0
```

### 多次循环右移

循环右移的移位位数大于1时，进位标志的值是最后移出最低有效位（位0)的数据位：

```assembly
mov al,00000100b
ror al,3		;AL=10000000b,CF=1
```

## 7.2.7 RCL和RCR指令

RCL(rotate carry left)指令在每位左移一位后，把进位标志复制到最低有效位中，把最高有效位复制到进位标志中。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.4o4p3q82j1k0.webp)

如果把进位标志想像成一个填加到数字末尾的额外位的话，RCL指令就变成了一条简单的循环左移指令。在下例中，CLC指令清除了进位标志，第一条RCL指令把BL的最高位送进位标志，然后把其他位左移一位，第二条RCL指令把进位标志移入最低位，并把其他所有位左移：

```assembly
clc			;CF=0
mov bl,88h	;CF,BL=0 10001000b
rcl bl,1	;CF,BL=1 00010000b
rcl bl,1	;CF,BL=0 00100001b
```

### 从进位标志中恢复一个位

RCL指令可以恢复以前送人进位标志中的数据位。下面的例子把testval的最低位送入进位标志以进行检查，如果testval最低位是1,则发生跳转；如果最低位是0,RCL指令把数字恢复成原值：

```assembly
.data
testval BYTE 01101010b
.code
shr testval,1		;LSB移位送进位标志
jc exit				;进位标志置位则退出
rcl testval,1		;否则恢复数字
```

### RCR指令

RCR(Rotate Carry Right)指令在每位向右移动一位后，把进位标志复制到最高有效位中并把最低有效位复制到进位标志中。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.77dniuelfrc0.webp)

与RCL指令的情况一样，先把上图想像成一个9位的值，其中进位标志是右边的最低有效位，这样更有助于理解。
在下面的例子中，STC指令首先设置进位标志，然后使用RCR指令把进位标志送MSB(最高有效位)，并把LSB(最低有效位)送入进位标志中：

```assembly
stc			; CF = 1
mov ah,10h	;AH,CF=000100001
rcr ah,1	;AH,CF=100010000
```

## 7.2.8 符号溢出

在使用移位和循环移位指令对一个有符号整数移位一位后得到的值如果超出了该操作数能表示的有符号数的范围，那么溢出标志就会置位。换句话说，在进行移位操作后数字的符号位变反时，溢出标志置位。在下例中，在循环左移时一个正数(+127)变成了负数(-2):

```assembly
mov a1,+127		;AL=01111111b
rol al,1		;0F=1,AL=11111110b
```

类似地，-128右移一位时，溢出标志也会置位，因为AL中的结果（+64)的符号是正的：

```assembly
mov al,-128		;AL=10000000b
shr al,1		;0F=1,AL=01000000b
```

如果移位或循环移位的移位位数大于1,溢出标志的值无定义。

## 7.2.9 SHLD/SHRD 指令

> 目的操作数与源操作数按位左右相互移动，目的操作数不变

SHLD和SHRD指令是从Intel386处理器开始引入的。==SHLD(shift left double)指令把目的操作数左移指定的位数，左移空出来的位用源操作数的高位来填充。指令对源操作数没有任何影响，但是符号标志、零标志、辅助进位标志、奇偶标志和进位标志都受影响。==

```assembly
SHLD 目的操作数,源操作数,移位位数
```

SHRD(shift right double)指令把目的操作数向右移动指定的位数，空出来的位由源操作数的低位来填充。

```assembly
SHRD 目的操作数,源操作数,移位位数
```

下面的指令格式适用于SHLD指令和SHRD指令。其中的目的操作数可以是寄存器或内存操作数，但是源操作数必须是寄存器。移位位数既可以是CL寄存器也可以是8位的立即数：

```assembly
SHLD  reg16,reg16,CL/imm8
SHLD  mem16,reg16,CL/imm8
SHLD  reg32,reg32,CL/imm8
SHLD  mem32,reg32,CL/imm8
```

例子1:下面的语句把wval左移4位并且把AX的高4位插入到wval的低4位中：

```assembly
.data
wval WORD 9BA6h
.code
mov ax,0AC36h
shid wval,ax,4	;wval=BA6Ah
```


数据位的移动如下图所示：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.2pvndl6147k0.webp)

例子2:在下面的例子中，AX右移4位，AX的高4位由DX的低4位填充：

```assembly
mov ax,234Bh
mov dx,7654h
shrd ax,dx,4	;AX=4234h
```

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.2a3ew15x6fbw.webp)

在成组的数据位需要左移或右移以重定位屏幕上的图像的时候，SHLD和SHRD指令可用于控制这些位映射图像。SHLD和SHRD指令的另外一个潜在的应用是数据加密，在数据加密算法中通常包含了数据位的移动。这两条指令还可以用来进行长整数的乘法和除法运算。

# 7.3 移位和循环移位的应用

## 7.3.1 多双字移位

要对扩展精度整数（长整数）进行移位操作，可把它划分为字节数组、字数组或双字数组，然后再对该数组进行移位操作。在内存中存储数字时通常采取的方式是最低字节在最低的地址位置上（小尾顺序）。下面的步骤以一个双字数组为例，说明了如何把这样的一个数组右移一位：

```assembly
ArraySize=3
.data
array DWORD ArraySize DUP(?)
```

1. 把ESI的值设置为array的偏移。
2. 把最高位置[ESI+8]处的双字右移一位，最低位复制到进位标志中。
3. 把[ESI+4]处的值右移一位，最高位自动以进位标志的值填充，最低位复制到进位标志中。
4. 把[ESI+0]处的双字右移一位，其最高位自动以进位标志值填充，其最低位复制到进位标志中。

下图显示了数组的内容及使用ESI间接引用的表示：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.43c9t2a2qlu0.webp)

实现程序MultiShf.asm的代码如下，程序中使用的是RCR指令，也可以用SHRD指令：

```assembly
.data
ArraySize = 3
array DWORD ArraySize DUP (9999999h) ; 1001 1001...
.code
	mov esi,0
	shr array [esi+8],1 	;高双字
	rcr array [esi+4],1		;中间的双字，包含进位
	rcr array [esi],1		;低双字，包含进位
```

程序的输出如下，分别列出了对数字移位之前和移位之后的数值，以二进制格式显示：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.6c81sqtmmj40.webp)

## 7.3.2 二进制乘法

IA-32的二进制乘法指令(MUL和IMUL)相对于其他机器指令来说是比较缓慢的。汇编语言程序员通常会寻找更好的进行二进制乘法的方法，有时候移位操作的优越性是显而易见的。我们已经知道，在乘数是2的次幕的情况下，用SHL指令进行无符号数的乘法是相当高效的。无符号整数左移n位就相当于乘以了$2^n$。任何乘数都可以表示成2的次幂的和。例如，为了计算EAX乘以36,就可以把36分解成(32+4),然后应用乘法分配率进行运算：

```
EAX * 36 	= EAX * (32 + 4)
			= (EAX * 32) + (EAX * 4)
```

下图描述了123×36的乘法过程，积为4428:

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.6r2igakul600.webp)

乘数36的位2和位5是1,这些恰好是例子中的移位次数。下面的代码使用32位寄存器实现该乘法运算：

```assembly
.code
mov eax,123
mov ebx,eax
shl eax,5		;乘以2^5
shl ebx,2		;乘以2^2
add eax,ebx		;积相加
```

本章的练习要求读者把这个例子一般化，编写一个过程使用移位和加法把任意两个32位整数相乘。

## 7.3.3 显示二进制数的数据位

一类常见的编程任务是要求把二进制整数转换成ASCII二进制字符串以进行显示。SHL指令这时就很有用了，因为SHL指令在每次操作数左移的时候，都会把最高位复制到进位标志中，下面的BinToAsc过程是一个简单实现。

```assembly
BinToAsc PROC
; 32位二进制整数转换为ASCII二进制
; Converts 32-bit binary integer to ASCII binary
; Receives : EAX = binary integer , ESI points to buffer
; Returns : buffer filled with ASCII binary digits
	push 	ecx					;保护寄存器
	push 	esi					
	mov 	ecx,32				;EAX中数据位的数目，循环32次
L1:	
	shl 	eax,1				;左移高位至进位标志中,CF
	mov 	BYTE PTR [esi],'0' 	;选择0作为默认数字
	jnc 	L2					;如果无进位，跳转到L2
	mov 	BYTE PTR [esi],'1'  ;否则1送缓冲区
L2: 
	inc 	esi					;下一个缓冲区位置
	loop 	L1					;继续循环，另外一位左移
	pop		esi
	pop 	ecx
	ret
BinToAsc ENDP
```



## 7.3.4 分离MS-DOS文件的各个日期域

有时一个字节或字包含多个域，这时就需要从中提取一系列位（称为位串，bit strings)。例如在实地址模式下，MS-DOS的功能57h在DX中返回文件的日期戳记（日期戳记表明了文件最后一次修改的时间）,其中位0~4代表0~31之间的天数，位5~8代表月份，位9~15存放的是年份。假设文件的最后修改时间是在1999年3月10日，那么DX中的文件日期戳如下（年份数是相对于1980年的）:

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.1bezl39m3v6o.webp)

要提取一个域，把要提取的位移位到寄存器的最低部分，然后清除不相干的位。下面的代码复制DL的值并屏蔽不属于该域的其他位以便提取出天数：

```assembly
mov al,dl		;DL的副本
and al,00011111b;清除位5~7
mov day,al		;保存在变量day中
```

为了提取月份，可以把位5~8移到AL的最低部分然后屏蔽掉其他位，也就是将AL右移，直到月份数在最低的4个位之内为止，之后把AL保存在一个变量中：

```assembly
mov ax,dx		;DX的副本
shr ax,5		;右移5位
and al,00001111b;清除位4~7
mov month,al	;保存在变量month中
```

年份数（位9~15)完全处于DH寄存器中，我们把DH复制到AL中并右移一位：

```assembly
mov al, dh	;DH的副本
shr al,1	;右移一位
mov ah,0	;AH清零
add ax,1980	;年份是相对于1980年的
mov year,ax	;保存的变量year中
```

# 7.4 乘法和除法指令

MUL和IMUL指令分别进行有符号整数和无符号整数的乘法操作。DIV指令进行无符号整数的除法操作，IDIV进行有符号整数的除法操作。

## 7.4.1 MUL 指令

MUL(无符号乘法）指令有三种格式：

1. 第一种将8位的操作数与AL相乘；
2. 第二种将16位的操作数与AX相乘；
3. 第三种将32位的操作数与EAX相乘。

乘数和被乘数大小必须相同，乘积的尺寸是乘数/被乘数大小的两倍。三种格式都既接受寄存器操作数，也接受内存操作数，但是不接受立即数操作数。

```
MUL r/m8
MUL r/m16
MUL r/m32
```

指令中唯一的一个操作数是乘数。表7.2根据乘数大小的不同列出了被乘数和乘积，==由于目的操作数（乘积）是乘数/被乘数大小的两倍，因此不会发生溢出==。如果积的高半部分不为0,就设置进位和溢出标志。由于进位标志通常用于无符号算术运算，因此我们主要关注该标志。例如当AX与16位操作数相乘的时候，积存储在DX:AX中。如果DX不为0,则进位标志置位。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.3ewlf4vhc1o0.webp)

在执行完MUL指令后要检查进位标志的一个好理由：有时我们需要知道乘积的高半部分是否可被安全地忽略。

### MUL 指令的例子

下面的语句把AL和BL相乘，积在AX中，进位标志清零(CF=0),因为AH(乘积的高半部分)等于0:

```assembly
mov al,5h
mov bl,10h
mul bl		;AX=50h,CF=0
```

下面的语句将16位数2000h和100h相乘，CF=1,因为乘积的高半部分DX等于0:

```assembly
.data
val1	WORD	2000h
val2	WORD	0100h
.code
mov 	ax,vall		;AX=2000h
mul 	val2		;DX:AX=00200000h,CF=1
```

下面的语句将32位数12345h和1000h相乘得到一个64位的积，由EDX=0知CF=0:

```assembly
mov eax,12345h
mov ebx,1000h
mul ebx			;EDX:EAX=0000000012345000h,CF=0
```

## 7.4.2 IMUL 指令

IMUL(有符号乘法）指令执行有符号整数的乘法运算，保留了乘积的符号位。IMUL指令在IA-32指令集中有三种格式：单操作数、双操作数和三操作数。在单操作数格式中，乘数和被乘数尺寸大小相同，乘积的大小是乘数/被乘数大小的两倍(8086/8088处理器只支持这种格式）。

### 单操作数格式

单操作数格式把乘积存储在累加器(AX,DX:AX,EDX:EAX)中：

```assembly
IMUL 	r/m8	;AX=AL*r/m byte
IMUL 	r/m16	;DX:AX=AX*r/m word
IMUL 	r/m32	;EDX:EAX=EAX*r/m doubleword
```

和MUL指令一样，IMUL指令的单操作数格式中乘积的尺寸大小使得溢出不可能发生。如果乘积的高半部分不是低半部分的符号扩展，进位标志和溢出标志置位，可使用该特点确定乘积的高半部分是否可以忽略。

### 双操作数格式

==双操作数格式中乘积存储在第一个操作数中，第一个操作数必须是寄存器，第二个操作数可以是寄存器、内存操作数或立即数==，下面是16位操作数的格式：

```assembly
IMUL r16,r/m16
IMUL r16,imm8
IMUL r16,imm16
```

下面是32位操作数的格式，乘数必须是一个32位的寄存器、32位的内存操作数或立即数（8位或32位）:

```assembly
IMUL r32,r/m32
IMUL r32,imm8
IMUL r32,imm32
```

==双操作数格式会F。==使用双操作数格式时，务必在执行完IMUL操作后检查这些标志的值。

### 三操作数格式：

==三操作数格式把乘积存储在第一个操作数中，一个16位的寄存器可被一个8位或16位的立即数乘：==

```assembly
IMUL r16,r/m16,imm8
IMUL r16,r/m16,imm16
```

一个32位的寄存器可被一个8位或32位的立即数乘：

```assembly
IMUL r32,r/m32,imm
MUL  r32,r/m32,imm32
```

==如果有效位丢失，则溢出标志和进位标志置位==。使用三操作数格式时，务必在执行完IMUL操作后检查这些标志的值。

### 无符号乘法

==双操作数和三操作数格式的IMUL指令也可用于进行无符号乘法。不过这样做有一个缺陷：进位标志和溢出标志不能用来指示乘积的高半部分是否为0。==

### IMUL指令的例子

下面的指令执行8位有符号数乘法（48×4),得到的积+192存放在AX中，AH不是AL的符号扩展，因此溢出标志置位：

```assembly
mov 	al,48
mov 	bl,4
imul 	bl		;AX=00COh,OF=1
```

下面的指令执行8位有符号数乘法(-4×4),得到的积-16存放在AX中，AH是AL的符号扩展，因此溢出标志清零：

```assembly
mov 	al,-4
mov 	bl,4
imul 	bl		;AX=FFFOh,OF=0
```

下面的指令执行16位有符号乘法（48×4),积+192存放在DX:AX中。DX是AX的符号扩展，因此没有发生符号溢出：

```assembly
mov 	ax,48
mov 	bx,4
imul 	bx		;DX:AX=000000coh,OF=0
```

下面的指令执行32位有符号数乘法[4823424×(-423)],得到的积-2040308352存放在EDX:EAX中。EDX是EAX的符号扩展，因此溢出标志清零：

```assembly
mov eax,+4823424
mov ebx,-423
imul ebx		;EDX:EAX=FFFFFFFF86635D80h,OF=0
```

下面的指令解释了双操作数格式IMUL指令的用法：

```assembly
.data
word1  SWORD  4
dword1 SDWORD 4
.code
	mov ax,-16		;AX=-16
	mov bx,2		;BX=2
	imul bx,ax		;BX=-32
	imul bx,2		;BX=-64
	imul bx,word1	;BX=-256
	mov  eax,-16	;EAX=-16
	mov  ebx,2		;EBX=2
	imul ebx,eax	;EBX=-32
	imul ebx,2		;EBX=-64
	imul ebx,dword1 ;EBX=-256
```

下面的双操作数IMUL指令发生了符号溢出，因为-64000无法容纳于16位的目的操作数中：

```assembly
mov  ax,-32000
imul ax,2		;0F=1
```

下面的指令解释了三操作数格式，包含了一个发生符号溢出的例子：

```assembly
.data
word1  SWORD  4
dword1 SDWORD 4
.code
imul bx,word1,-16				;BX=-64
imul ebx, dword1,-16			;EBX=-64
imul ebx, dword1,-2000000000	;0F=1
```

## 7.4.3 乘法操作的基准（性能）测试

既然已经学习了进行乘法运算的两种方法：通过移位指令以及通过标准的MUL和IMUL指令，那么比较一下两种方法的性能就很有趣了。下面的过程使用两种方法分别进行同样的EAX乘以36的运算：

```assembly
mult_by_shifting PROC
;EAX乘以36,使用SHL指令，重复LOOP_COUNT次
	mov 	ecx,LOOP_COUNT
L1:
	push	eax		;保存原始的EAX
	mov 	ebx,eax
	shl 	eax,5
	add 	eax,ebx
	pop		eax		;恢复EAX
	loop 	L1
	ret
mult_by_shifting ENDP
mu1t_by_MUL PROC
;EAX乘以36,使用MUL指令，重复LOOP_COUNT次
	mov 	ecx,LOOP_COUNT
L1:
	push	eax		;保存原始的EAX
	mov	 	ebx,36
	mul 	ebx		;恢复EAX
	pop 	eax
mult_by_MUL ENDP
```

我们调用mult_by_shifting多次并记录执行时间：

```assembly
.data
LOOP_COUNT=OFFFFFFFFH
.data
	intval DWORD 5
	startTime DWORD ?
.code
	call GetMseconds		;获取起始时间
	mov  startTime,eax
	mov  eax,intval			;现在进行乘法
	call mult_by_shifting
	call GetMseconds		;获取停止时间
	sub  eax,startTime
	call WriteDec			;显示用掉的时间
```

假设我们以同样的方式调用mult_by_MUL,在一台4GHz的奔腾4机器上，结果很明显：调用SHL过程的程序用掉了6.078s,而使用MUL指令用去了20.718s。换句话说，使用MUL指令导致计算慢了241%(参见程序CompareMult.asm)!

## 7.4.4 DIV指令

DIV(无符号除法）指令执行8位、16位和32位无符号整数的除法运算。指令中唯一的一个寄存器或内存操作数是除数，DIV的指令格式是：

```assembly
DIV r/m8
DIV r/m16
DIV r/m32
```

下表显示了被除数、除数、商及余数之间的关系。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.39if9hxsumu0.webp)

### DIV指令的例子

下面的指令执行8位无符号数的除法（83h/2),商是41h,余数是1:

```assembly
mov ax,0083h	;被除数
mov bl,2		;除数
div bl			;AL=41h,AH=01h
```

下面的指令执行16位无符号数除法(8003h/100h)，商是80h,余数是3。==DX中存放的是被除数的高位，因此在执行DIV指令之前DX必须首先清零==：

```assembly
mov dx,0		;清除被除数的高位
mov ax,8003h	;被除数的低位
mov cx,100h		;除数
div cx			;AX=0080h,DX=0003h
```

下面的指令执行32位无符号数除法，指令使用内存操作数作为除数：

```assembly
divisor DWORD 000001001
.code
mov edx,DWORD PTR dividend+4	;高双字
mov eax,DWORD PTR dividend		;低双字
div divisor		;EAX=08003000h,EDX=00000020h
```

## 7.4.5 有符号整数除法

有符号除法和无符号除法几乎是完全相同的，==唯一的不同在于：在进行除法操作之前，隐含的被除数必须进行符号扩展。==下面首先介绍符号扩展指令，然后再介绍有符号除法指令IDIV。

### 符号扩展指令(CBW,CWD,CDQ)

==有符号除法指令中的被除数在进行除法操作之前通常要进行符号扩展==（符号扩展在4.1.5节介绍过了)。Intel提供了三条符号扩展指令：CBW,CWD和CDQ。CBW指令（字节符号扩展至字）扩展AL的符号位至AH中，保留了数字的符号。在下面的例子中(AL中的).9Bh和（AX中的）FF9Bh都等于-101:

```assembly
.data
byteVal SBYTE -101	;9Bh
.code
	mov al,byteVa1	;AL=9Bh
	cbw				;AX=FF9Bh
;CWD(字符号扩展至双字）指令扩展AX的符号位至DX中
.data
	wordVal SWORD -101	;FF9Bh
.code
	mov ax,wordVal		;AX=FF9Bh
	cwd					;DX:AX=FFFFFF9Bh
```

CDQ(双字符号扩展至8字节）指令扩展EAX的符号位至EDX中：

```assembly
.data
dwordVal SDWORD -101		;FFFFFF9Bh
.code
	mov eax,dwordVal
	cdq				;EDX:EAX=FFFFFFFFFFFFF9Bh
```

### IDIV指令

IDIV(有符号数除法）指令进行有符号整数的除法运算，使用的操作数格式与DIV令相同。在进行8位除法之前，被除数(AX)必须进行符号扩展，余数的符号和被除数总是相同。

### 例子1

下面-48除以5的例子中，在IDIV指令执行之后，AL中的商是-9,AH中的余数是-3:

```assembly
.data
byteVa1 SBYTE -48
.code
	mov al,byteval		;被除数
	cbw					;扩展AL至AF
	mov b1,5			;除数
	idiv bl				;AL=-9,AH=-3
```

### 例子2

16位除法要求AX符号扩展至DX,在下面的例子中，-5000被256除：

```assembly
wordVal SWORD -5000
.code
	mov ax,wordVal		;被除数的低半部分
	cwd					;扩展AX至DX
	mov bx,+256			;除数
	idiv b				;商AX=-19余数DX=-136
```

### 例子3

32位除法要求EAX符号扩展至EDX。下面的例子中+50000被256除

```assembly
.data
dwordVal SDWORD +50000
.code
	mov eax,dwordVal	;被除数的低半部分
	cdq					;扩展EAX至EDX
	mov ebx,-256		;除数
	idiv ebx			;商EAX=-195余数EDX=+80
```

在执行DIV和IDIV指令后所有的算术状态标志都是不确定的。

### 除法溢出

在除法操作产生的商太大，目的操作数无法容纳的时候，就会导致除法溢出，这会导致CPU触发一个中断，当前程序将被终止。例如下面的指令在MS-Windows下执行时会导致除法溢出，因为商（100h)无法存放于AL寄存器中：

```assembly
mov ax,1000h
mov bl,10h
div bl			;AL不能容纳100h
```

图7.1显示了在MS-Windows下执行该代码的时候出现的错误对话框。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.3xh5ym93lty0.webp)

在执行试图除以0的指令时也会产生类似的对话框窗口：

```assembly
mov ax,dividend
mov bl,0
div bl
```

可以使用32位的除法来减小发生除法溢出的可能性，如：

```assembly
mov eax,1000h
cdg
mov ebx,10h
div ebx			; EAX = 00000100h
```

防止被0除是相当容易的，在进行除法操作之前首先检查除数是否为0:

```assembly
mov ax,dividend
mov bl,divisor
cmp b1,0			;检查除数
je  NoDivideZero	;是否为0?为0则显示错误
div bl				;不为0:继续
...
No DivideZero;显示"Attempt to divide by zero")
```

## 7.4.6 算术表达式的实现

4.2.5节讲述了如何使用加法和减法指令实现算术表达式，现在可以把乘法和除法指令也包括进来了。乍看起来，实现算术表达式似乎是应该留给编译器作者的任务，但亲历学习将使读者受益良多。通过算术表达式的实现，读者能够了解到编译器是如何优化代码的；通过检查乘法操作之后乘积的大小，读者可以实现比一般的编译器中实现得更好的错误检查代码。在两个32位操作数相乘的时候，大多数高级语言编译器都简单地忽略乘积的高32位，但是在汇编语言中，可以使用进位标志和溢出标志检查乘积是否能够容纳于32位操作数中，这些标志的使用已经在7.4.1节和7.4.2节介绍过了。
要查看C++编译器生成的代码，有两种简单的方法：第一种方法是在调试C++程序时打开反汇编窗口；第二种方法是生成汇编语言列表文件，例如，在MicrosoftVisualC++中，使用/FA命令行选项可生成汇编语言列表文件。

### 例子1

以汇编语言实现下面的C++语句（使用32位无符号整数）:

```c
var4 = (var1 + var2) * var3;
```

这个问题非常直接，因为可以按照从左到右的顺序（加法，然后是乘法）进行处理，下面的代码中，在第二条指令之后，EAX中包含var1和var2的和。在第三条指令中，EAX乘以var3.积存放在EAX中：

```assembly
	mov eax,var1
	add eax,var2
	mul var3		;EAX=EAX*var3
	jc  tooBig		;无符号溢出？
	mov var4,eax
	jmp next
tooBig:			;显示错误消息
```

如果乘积大于32位，JC指令可以跳转到处理该错误的标号处。

### 例子2

以汇编语言实现下面的C++语句（使用32位无符号整数）:

```c
var4 = (var1 * 5) / (var2 - 3);
```

在本例中，圆括号内有两个子表达式，左边的子表达式可以赋给EDX:EAX,这样就没有必要检查溢出了，右边的表达式赋给EBX,最后使用除法完成整个表达式：

```assembly
mov eax,var1		;左边
mov ebx,5
mul ebx				;EDX:EAX=乘积
mov ebx,var2		;右边
sub ebx,3
div ebx				;最后的除法
mov var4,eax
```

### 例子3

以汇编语言实现下面的C++语句（使用32位有符号整数）:

```c
var4 = (var1 * -5) / (-var2 % var3);
```

这个表达式的实现与前面的例子相比需要一点技巧，我们可以从右边的表达式开始，并把右表达式的值存储在EBX中。因为操作数是有符号的，因此需要把被除数符号扩展到EDX(这是很重要的）,然后使用IDIV指令：

```assembly
mov  eax,var2	;开始右边子表达式
neg  eax
cdq				;符号扩展被除数
idiv var3		;EDX=余数
mov  ebx,edx	;EBX=右边子表达式的值
;接下来计算左边子表达式的积，并把积存储在EDX:EAX中：
mov  eax,-5		;开始左边子表达式
imul var1		;EDX:EAX=左边子表达式的值
;最后计算左边的子表达式（EDX:EAX)除以右边的子表达式（EBX):
idiv ebx		;最后的除法
mov  var4,eax	;商
```

# 7.5 扩展加法和减法

扩展精度的加法和减法是指任意尺寸大小数字的加法和减法。假如要求你写一个C++程序，把两个1024位的整数相加，解决方案可不是那么简单！但在汇编语言中，ADC(带进位加）指令和SBB(带进位减）指令非常适合于解决此类的问题。

## 7.5.1 ADC指令

ADC(Add With Carry)指令把源操作数、目的操作数以及进位标志相加。指令格式与MOV指令是一样的：

```assembly
ADC reg,reg
ADC mem,reg
ADC reg,mem
ADC mem,imm
ADC reg,imm
```

例如，下面的指令把两个8位整数相加（FFh+FFh),16位的结果01FEh被存放在DL:AL中：

```assembly
mov d1,0
mov al,0FFh
add al,0FFh		;AL=FE
adc d1,0		;DL=01
```

与之类似，下面的指令把两个32位整数相加（FFFFFFFH+FFFFFFFh),在EDX:EAX中存放64位的和00000001FFFFFFFEh:

```assembly
mov edx,0
mov eax,0FFFFFFFFh
add eax,0FFFFFFFFh
adc edx,0
```

## 7.5.2 扩展加法的例子

下面的Extended_Add过程把两个任意同样尺寸的整数相加，程序使用一个循环把每对双字相加，保存进位标志，并使进位标志参与后面双字的加法运算：

```assembly
Extended_Add PROC
;Calculates the sum of two extended integers stored
;as arrays of doublewords
;Receives: ESI and EDI point to the two integers,
;EBX points to a variable that will hold the sum , and
;ECX indicates the number of doublewords to be added .
;input operands
	pushad
	clc					; clear the Carry flag
L1: 
	mov		eax,[esi]		; get the first integer
	adc 	eax,[edi]		; add the second integer
	pushfd				; save the Carry flag
	mov 	[ebx],eax		; store partial sum
	add 	esi,4
	add 	edi,4
	add 	ebx,4
	popfd				; restore the Carry flag
	loop L1				; repeat the loop
	mov 	dword ptr [ebx],0; clear high dword of sum
	adc 	dword ptr [ebx],0; add any leftover carry
	popad
Extended_Add ENDP
```

下面摘自ExtAdd.asm的程序片段调用了Extended_Add过程，例子中传递的是两个64位整数。注意我们特别为和分配了额外的双字以保存可能出现的进位值：

```assembly
.data
op1 QWORD 0A2B2A40674981234h
op2 QWORD 08010870000234502h
sum DWORD 3 dup(OFFFFFFFFh)	;=0000000122C32B0674BB5736
.code
main PROC
	mov 	esi, OFFSET op1		; first operand
	mov 	edi, OFFSET op2		; second operand
	mov 	ebx, OFFSET sum		; sum operand
	mov 	ecx,2				; number of doublewords
	call 	Extended_Add		;Display the sum.
	mov 	eax,sum+8			; display high-order dword
	call 	WriteHex
	mov 	eax,sum+4			; display middle dword
	call 	WriteHex
	mov 	eax,sum				; display low-order dword
	call 	WriteHex
	call 	Crlf
	exit
main ENDP
```

下面是程序产生的输出，加法确实产生了进位：

```c
0000000122C32B0674BB5736
```

## 7.5.3 SBB 指令

SBB(subtract with borrow)指令从目的操作数中减去源操作数和进位标志的值。指令允许的操作数与ADC指令是相同的。
下面的例子代码执行64位的减法，把EDX:EAX设置为0000000100000000h并从中减去1程序首先减去低32位值，进位标志将被设置，然后再减去高32位和进位标志值：

```assembly
mov		edx,1	;高半部分
mov 	eax,0	;低半部分
sub 	eax,1	;减去1
sbb		edx,0	;减去1的高半部分（0)
```

EDX:EAX中的64位差值是00000000FFFFFFFFH。

# 7.6 ASCII和未压缩十进制算术指令

到现在为止，本书讲述的整数算术指令都是处理二进制数值的，尽管CPU是以二进制数方式进行运算的，但是也能处理ASCII十进制数串的算术运算。后者可以方便地由用户输入并在控制台窗口中显示，无须转换成二进制数值。假设程序需要用户输入两个数字并把它们相加，下面是一个输出样例，其中用户输入了3402和1256:

```assembly
Enter first number : 3402
Enter second number : 1256
The sum is :
4658
```

在计算和显示数字的时候有两种选择：

1. 把两个操作数转换成二进制数值并相加，然后把和从二进制数转换成ASCII码数字串的格式后再显示。
2. 连续地把每对ASCII码数字直接相加（2+6,0+5,4+2,3+1),这样和就是ASCII数字串，可以直接在屏幕上显示。

第二种选择要求在每对ASCII数字相加后使用特殊指令来调整其和，指令集中有4条指令可以处理这一类的ASCII加法、减法、乘法和除法：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.6jkx1w81vcw0.webp)

### ASCII码十进制数和未压缩的十进制数

未压缩的十进制数的高4位总是0,而在ASCII码十进制数中高4位总是0011b。在任何情况下，整数类型按每字节存储1个数字。下面的例子显示了3402是如何使用这两种格式存储的：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.1e8i6bvqeq9s.webp)

虽然ASCII算术指令比二进制算术指令执行得要慢，但它有如下优点：

- 在进行算术运算之前无须进行字符串到二进制数值的转换。
- 可以使用假想的小数点进行实数运算，可避免使用浮点数时因近似产生的错误。

ASCII加法和减法指令中操作数可以是ASCII格式或未压缩的十进制数格式，但只有未压缩的十进制数字才能用于乘法和除法。

## 7.6.1 AAA指令

AAA(ASCII adjust after addition)指令调整ADD或ADC指令的结果。假设AX存放着两个ASCII(未压缩）十进制数字相加的和，AAA指令把AX中的和转换成两个未压缩的十进制数字。对于未压缩的十进制数字，可容易地通过和30h进行“或”运算把它转换成对应的ASCII码
下面的例子显示了如何使用AAA指令把ASCII数字8和2正确相加，在执行加法之前必须把AH清零，否则会影响AAA指令的结果，最后一条指令把AH和AL转换成ASCII数字：

```assembly
mov ah,0
mov al,'8'		;AX=0038h
add al,'2'		;AX=006Ah
aaa				;AX=0100h(结果的ASCII码调整）
or  ax,3030h	;AX=3130h='10·(转换成ASCII码）
```

### 使用AAA指令的多字节加法

下面看一下两个使用隐含小数点的ASCII十进制数值相加的过程，实现比读者想像中的可能稍微复杂一点，这是因为每个数字进行加法后产生的进位必须向相邻的高位传播，在下面的伪码中，acc指的是累加寄存器：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.6mjulnhskpw0.webp)

进位数字必须转换成ASCII码，在把进位数字和第一个操作数相加的时候，必须使用AAA调整结果，下面是程序清单：

```assembly
TITLE ASCII Addition	(ASCII_add.asm)
; Perform ASCII arithmetic on strings having
; an implied fixed decimal point.
INCLUDE Irvine32.inc
DECIMAL_OFFSET = 5
; offset from right of string
.data
	decimal_one BYTE "100123456789765"	;1001234567.89765
	decimal_two BYTE"900402076502015"	;9004020765.02015
	sum BYTE (SIZEOF decimal_one+1) DUP (0),0
.code
main PROC
; Start at the last digit position.
	mov esi , SIZEOF decimal _ one - 1
	mov edi , SIZEOF decimal _ one
	mov ecx, SIZEOF decimal_one
	mov bh,0			; set carry value to zero
L1: 
	mov ah,0			; clear AH before addition
	mov a1,decimal_one[esi]	; get the first digit
	add al,bh			; add the previous carry
	aaa					; adjust the sum (AH = carry)
	mov bh,ah			; save the carry in carry1
	or bh,30h			; convert it to ASCII
	add al,decimal_two[esi]	; add the second digit
	aaa					; adjust the sum (AH = carry)
	or bh,ah			; OR the carry with carry1
	or bh , 30h			; convert it to ASCII
	or al,30h			; convert AL back to ASCII
	mov sum[edi],a1		; save it in the sum
	dec esi				; back up one digit
	dec edi
	1oop L1
	mov sum[edi],bh		; save last carry digit
; Display the sum as a string .
	mov edx, OFFSET sum
	call WriteString
	call Crlf
	exit
	main ENDP
END main
```

下面是程序的输出，和没有小数点（是隐含的）:

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.73ppmbe033g0.webp)

## 7.6.2 AAS 指令

AAS指令（ASCII adjust after subtraction)在SUB或SBB指令从一个未压缩（ASCII)十进制数中减去另外一个未压缩（ASCII)十进制数并把结果存储在AL中，之后对AL中的结果进行调整使其与ASCII数字表示相一致。仅当减法产生负数结果时调整才是必需的。例如下面的指令从ASCII8中减去ASCII9:

```assembly
.data
vall BYTE	'8'
val2 BYTE 	'9'
.code
	mov ah,0
	mov al,val1	;AX=0038h
	sub al,val2	;AX=00FFh
	aas			;AX=FF09h
	pushf		;保存进位标志
	or al,30h	;AX=FF39h
	popf		;恢复进位标志
```

在SUB指令执行之后，AX等于00FFh,AAS指令把AL转换成09h并从AH中减去1,把AH设置成FFh并相应设置进位标志。

## 7.6.3 AAM 指令

AAM(ASCII adjust after multiplication)指令调整MUL指令的结果，把乘积的二进制数值转换成未压缩十进制数，MUL指令必须使用未压缩的十进制数。在下面的例子中，把5和6相乘并调整AX中的结果，得到AX=0300h,也就是30的未压缩十进制数表示：

```assembly
.data
AscVal BYTE 05h,06h
.code
mov bl,ascVal		;第一个操作数
mov al,ascVal+1		;第二个操作数
mul bl				;AX=001Eh
aam					;AX=0300h
```

## 7.6.4 AAD指令

AAD(ASCII adjust before division)指令在除法操作之前调整AX中的未压缩十进制数被除数，以执行DIV指令。下面的例子把未压缩的0307h转换成二进制数，然后再除以5。执行DIV指令后，商07h存放在AL中，余数02h存放在AH中：

```assembly
.data
quotient 	BYTE ?
remainder 	BYTE ?
.code
	mov ax,0307h		;被除数
	aad					;AX=0025h
	mov b1,5			;除数
	div bl				;AX=0207h
	mov quotient,al
	mov remainder,ah
```

# 7.7 压缩十进制算术指令

在压缩的十进制整数中，每字节存储两个十进制数字，每个十进制数字用4个数据位来表示，如果压缩十进制数字的个数是偶数，最高位以0填充，压缩的十进制数大小可变：

```assembly
bcd1 QWORD 2345673928737285h 	;2,345,673,928.737,285(十进制数）
bcd2 DWORD 12345678h			;12,345,678(十进制数）
bcd3 DWORD 08723654h			;8,723,654(十进制数）
bcd4 WORD 9345h					;9,345(十进制数）
bcd5 WORD 0237h					;237(十进制数）
bcd6 BYTE 34h					;34(十进制数）
```

压缩的十进制整数至少有以下两方面的优点：

- 数字几乎可以有任意数目的有效位，这使得执行高精度运算成为可能。
- 把压缩的十进制数转换成ASCII(或相反)是相当简单的。

有两条指令DAA(加法后进行十进制数调整）和DAS(减法后进行十进制数调整）可用于调整压缩十进制数字加法和减法的运算结果。遗憾的是，对于乘法和除法不存在这样的指令，此时，数字必须首先解压缩再进行乘法或除法，然后重新进行压缩。

## 7.7.1 DAA指令

DAA(decimal adjust after addition)指令把ADD和ADC指令执行后AL中的二进制数和转换成压缩的十进制数格式。例如下面的指令把压缩的十进制数35和48相加，执行DAA指令后，二进制数和(7Dh)被调整成83h,恰好是压缩的十进制数35和48之和的压缩十进制数格式：

```assembly
mov al,35h
add al,48h		;AL=7Dh
daa				;AL=83h(调整结果）
```

DAA指令的内部逻辑在IA-32指令集参考手册中有详尽的文档。
例子：下面的程序把两个16位的压缩十进制整数相加，和存储在压缩的双字中。由于是加法，因此要求和要比操作数多至少一个数据位：

```assembly
TITLE Packed Decimal Example	(AddPacked.asm)
;Demonstrate packed decimal addition.
INCLUDE Irvine32.inc
.data
	packed_1 	WORD 4536h
	packed_2 	WORD 7207h
	sum 		DWORD ?
.code
main PROC
; Initialize sum and index .
	mov sum,0		
	mov esi,0
	mov al,BYTE PTR packed_1[esi]	
; Add low bytes .
	add al,BYTE PTR packed_2[esi]
	daa
	mov BYTE PTR sum[esi],al
; Add high bytes , include carry .
	inc esi
	mov al,BYTE PTR packed_1[esi]
	adc al,BYTE PTR packed_2[esi]
	daa
	mov BYTE PTR sum[esi],al
; Add final carry , if any .
	inc esi
	mov al,0
	adc a7,0
	mov BYTE PTR sum [esi],al
; Display the sum in hexadecimal .
	mov eax,sum
	call WriteHex
	call Crlf
	exit
	main ENDP
END main
```

无须多说，很显然程序中包含了很多重复代码，应该使用循环替换。本章的一个练习就要求读者写一个过程，对两个任意大小的十进制整数进行加法操作。

## 7.7.2 DAS指令

DAS(decimal adjust after subtraction)指令将SUB或SBB指令执行后AL中的二进制结果转换成压缩的十进制数格式。例如下面的指令将压缩的十进制数字85和48相减并调整结果：

```assembly
mov bl,48h
mov al,85h
sub al,bl		;AL=3Dh
das				;AL=37h(调整结果）
```

DAS指令的内部逻辑在IA-32指令集参考手册中有详尽的文档。

# 7.8 本章小结

和前一章的位操作指令一样，移位指令也是最具汇编语言特征的指令。移位意味着把数据位左移或把数据位右移。
SHL指令（逻辑左移）把目的操作数内的每位左移，以0填充最低位。SHL指令的一个最重要的应用就是用于实现与2的次寡的快速乘法，把任何操作数左移n位就相当于乘以了$2^n$。SHR指令(逻辑右移）把操作数的每位右移，并以0填充最高位，把任何操作数右移n位就相当于除以了$2^n$
SAL(算术左移)和SAR(算术右移)指令是为有符号数的移位特别设计的。
ROL(循环左移)指令把每位左移并把最高位同时复制到进位标志和最低位中。ROR(循环右移)指令把每位右移并把最低位同时复制到最高位和进位标志中。
RCL(带进位循环左移)指令把操作数的每位左移并把最高位复制到进位标志中，原进位标志复制到结果的低位。RCR(带进位循环右移)指令把每位右移，并把最低位复制到进位标志中，原进位标志值复制到结果的最高位。
SHLD(双精度左移)和SHRD(双精度右移)指令在IA-32系列处理器上才能使用，对于大整数的移位是非常有效的。
MUL指令将8位、16位或32位操作数与AL,AX或EAX相乘。IMUL指令执行有符号整数的乘法，它有三种格式：单操作数格式、双操作数格式和三操作数格式。
DIV指令对无符号操作数进行8位、16位或32位除法操作。IDIV指令进行有符号整数的除法操作，使用的操作数格式与DIV指令是相同的。
CBW(字节扩展到字)指令将AL寄存器的符号位扩展到AH寄存器中。CDQ指令(将双字扩展到8字节)指令将EAX中的符号位扩展到EDX寄存器中。CWD(字扩展到双字)指令将AX中的符号位扩展到DX寄存器中。

扩展加法和减法是指加减任意大小的整数。ADC和SBB指令可用于实现扩展的加法和减法。ADC(带进位加）指令将源操作数、进位标志值和目的操作数相加。SBB(带进位减)指令从目的操作数中减去源操作数和进位标志的值。
ASCII 十进制数每字节存储一个数字，编码形式是ASCII数字。AAA(加法后进行ASCII调整)指令把ADD或ADC指令产生的二进制数结果转换成ASCII十进制数格式。AAS(减法后进行ASCII调整)指令把SUB或SBB指令产生的二进制数结果转换成ASCII十进制数格式。
未压缩的十进制数以二进制数值的形式每字节存储一个十进制数字。AAM(乘法后进行ASCII调整)指令把MUL指令产生的二进制积转换成未压缩的十进制数格式。AAD(除法前进行ASCII调整)指令在执行DIV指令之前把未压缩的十进制数转换成二进制数值。
压缩的十进制数每个字节存储两个十进制数字。DAA(加法后进行十进制调整)指令把ADD或ADC指令产生的二进制数结果转换成压缩的十进制数格式。DAS(减法后进行十进制数调整)指令把SUB或SBB指令产生的二进制数结果转换成压缩的十进制数格式。