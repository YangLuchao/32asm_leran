[toc]

本章要点
简介
布尔和比较指令
条件跳转
条件循环指令
条件结构
应用：有限状态机
决策伪指令

# 6.1 简介

==允许进行决策的程序设计语言使用一种称为条件分支的技术能够在运行时改变控制流程。高级语言中的IF语句、SWITCH语句或条件循环语句都有内建的特定的分支逻辑==。汇编语言也提供了决策逻辑所需的所有工具。通过本章，读者将看到高级的条件分支语句是如何翻译成底层的实现代码的。
处理硬件设备的程序必须能够操控数字中的单个数据位，应该能够测试、清除和设置单个的数据位。数据加密和压缩也依赖于位操作，本章讲述如何在汇编语言中进行这类操作。
本章试图解答如下的一些基本问题：

- 如何使用第1章中介绍的布尔运算符（AND,OR和NOT)?
- 在汇编语言中如何写一条IF语句？
- 编译器是如何将嵌套的IF语句翻译成机器语言的？
- 如何设置和清除二进制数字中的单个位？
- 如何对数据进行简单的二进制加密？
- 在布尔表达式中的有符号数和无符号数有什么区别？
- 什么是有限状态机？
- GOTO语句真是有害的吗！?

本章遵循一种自底向上的方法，首先讲述程序逻辑背后的二进制基础知识，接下来讲述CPU如何使用CMP指令和处理器状态标志来比较操作数，最后将所有这些进行综合，讲述如何使用汇编语言实现高级语言的逻辑结构。

# 6.2布尔和比较指令

下面开始在二进制层次学习如何进行条件处理，使用布尔代数中的4种基本操作`AND,OR,XOR和NOT`，这类操作在计算机硬件和软件设计中都有广泛的应用。
IA-32指令集中包含`AND,OR,XOR,NOT,TEST和BTop`指令，直接实现了字节、字和双字的布尔运算(见表6.1)。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.6o6j8tgpmcw0.webp)

## 6.2.1 CPU的状态标志

布尔指令影响零标志、进位标志、符号标志、溢出标志和奇偶标志。下面简单回顾一下这些标志的含义：

- ==零标志操作的结果等于0时置位==。
- ==进位标志在指令执行产生的结果==（视为无符号整数）对目的操作数而言太大（或太小）而无法容纳时的置位。
- ==符号标志是目的操作数最高位的一份副本==，如果目的操作数为负数则设置该标志，如果是正数则清零（0是正数）
- ==符号标志在指令产生的有符号结果无效时置位==。
- ==在指令目的操作数的低字节中，为1的数据位的数量是偶数时设置奇偶标志==。

## 6.2.2 AND指令

AND指令在每对操作数的对应数据位之间执行布尔（位）`与`操作，并把结果存放在目的操作数中：

```assembly
AND 目的操作数,源操作数
```

下面的操作数格式都是允许的：

```assembly
AND reg,reg
AND reg,mem
AND reg,imm
AND mem,reg
AND mem,imm
```

两个操作数可以是8,16或32位的，但是它们的尺寸必须相同。对两个操作数中每对匹配的数据位，遵循下面的操作规则：如果两个数据位都是1,那么结果为1,否则结果是0。下面的真值表来自于第1章，位x和位y是输入，第3列显示的是表达式x$\wedge$y的值：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.4fk400w9jci0.webp)

AND指令常用来清除选定的位并同时保留其他位。在下例中，数据的高4位被清零而低4位z保持不变：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.1g60qj0pxpq8.webp)

该操作由下面的指令完成：

```assembly
mov al,00111011b
and al,00001111b
```

在这个例子中，低4位可能包含着重要的信息，而我们并不关心高4位。可以把该技术想像是位提取（bit extraction),因为低4位从AL中被“拖”了出来。

### 影响的标志：

AND指令总是清除溢出标志和进位标志，它依据目的操作数的值修改符号标志、零标志和奇偶标志。

### 将字符转换为大写形式

AND指令提供了一种把小写字母转换成大写字母的简单方法。如果我们比较一下大写字母A和小写字母a,很明显它们只有位5是不同的：

```assembly
01100001 = 61h ('a')
01000001 = 41h ('A')
```

其他的大小写字母之间也有同样的关系。如果将任何字符与二进制值11011111进行`与`操作，那么除了位5被清除之外，其他所有位都保持不变。在下例中，数组中的所有字符都被转换成大写字母：

```assembly
.data
array BYTE 50 DUP(?)
.code
	mov ecx,LENGTHOF array
	mov esi,OFFSET array
L1:
	and	BYTE PTR [esi],11011111b		;清除位5
	inc esi
	loop L1
```

## 6.2.3 OR指令

OR指令在每对操作数的对应数据位之间执行布尔`或`操作，并把结果存放在目的操作数中：

```
OR 目的操作数,源操作数
```

OR指令的操作数格式和AND指令相同：

```assembly
OR reg,reg
OR reg,mem
OR reg,imm
OR mem,reg
OR mem,imm
```

两个操作数可以是8,16或32位的，但它们的尺寸必须相同。对于两个操作数的每对匹配数据位，至少有一个输入为1时结果才为1。下面的真值表（来自于第1章）描述了布尔表达式
x$\vee$y的值：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.1st7wjtus4tc.webp)

OR指令常用来设置特定的位并保留其他位。下图中，3Bh和0Fh进行“或”运算，低4位全部变成了1而高4位保持不变：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.6ul4xwxnauk0.webp)

OR指令可以把0~9之间的整数转换成对应的ASCII码数字字符，方法是把位4和位5设置为1。例如，AL=05h,可以将其同30h进行“或”运算来把它转换成数字5的ASCII码（35h):

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.6ygnsd2grt40.webp)

完成该功能的汇编语言指令如下：

```assembly
mov dl,5		;二进制值
or  dl,30h		;转换到ASCII码
```

### 影响的标志：

OR指令总是清除溢出标志和进位标志。它根据目的操作数的值修改符号标志、零标志、奇偶标志。可以把一个数字同自身进行“或”运算以获取关于其值的某些信息，如：

```assembly
or al,al
```

零标志和符号标志的值与AL中的内容的关系如下：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.61g04aoaufo0.webp)

## 6.2.4 XOR 指令

XOR指令在每对操作数的对应数据位之间执行布尔“异或”操作，并把结果存放在目的操作数中：

```assembly
XOR 目的操作数,源操作数
```

XOR指令同OR以及AND指令的操作数格式相同，两个操作数可以是8,16或32位的。对于两个操作数中的每对匹配数据位，遵循以下的操作规则：如果两个位相同（都为0或为1),结果为0;否则结果为1。下面的真值表描述了布尔表达式x$\oplus$y的值：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.6lkayo5ny780.webp)

所有同0进行异或操作的位保持不变，而同1进行异或运算的位都变反了。XOR操作的一个特性是：某个操作数与同样的操作数执行两次异或运算后，其值保持不变。下面的真值表说明了当x和y进行两次异或操作后又变成了x原始值：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.7juga9912k80.webp)

在6.3.4节中将发现XOR的这种“可逆”特性使其成为了进行简单的对称加密的理想工具

### 影响的标志：

XOR指令总是清除溢出标志和进位标志，它依据目的操作数的值修改符号标志
零标志和奇偶标志。

### 检查奇偶标志：

奇偶标志位表明了位操作或算术运算结果的最低字节包含为1的数据位的个数是奇数还是偶数。当为1的数据位的个数为偶数时设置奇偶标志，为奇数时清除该标志。检验数字中为1的数据位个数的奇偶性的方法之一是将数字同全0字节进行异或运算，无须改变其值：

```assembly
mov al,10110101b		;5位=奇校验
xor al,0				;PO,奇偶标志被清除
mov al,11001100b		;4位=偶校验
xor al,0				;PE,奇偶标志置位
```

(调试器通常使用PE来表示奇偶标志置位，使用PO来表示奇偶标志被清零。）

### 16位的奇偶值：

可通过对16位寄存器的高字节和低字节进行异或运算检查其奇偶性：

```assembly
mov ax, 64C1h		;0110 0100 1100 0001
xor ah,al			;PE,奇偶标志置位
```

可以把16位寄存器的高字节和低字节数据位分别想像成两个8位集合的成员，两个集合中的交集部分（为1的数据位）通过XOR运算将被清零，剩余位形成一个联合，这个联合的奇偶性和整个16位整数的奇偶性是一样的。
32位值的奇偶性又该如何计算呢？如果以B0到B3来分别标识4个字节，那么整个32位值的奇偶性可通过下式计算：$B_0\quad XOR \quad B_1 \quad XOR \quad B_2 \quad XOR \quad B_3$。

## 6.2.5 NOT 指令

NOT指令对一个操作数的所有数据位取反，得到的结果称为该操作数的反码（one's complement)。该指令允许下面的操作数类型

```assembly
NOT reg
NOT mem
```

例如，FOh的反码是OFh:

```assembly
mov al,11110000
not al				;AL=00001111b
```

### 影响的标志：

NOT指令不影响任何状态标志。

## 6.2.6 TEST 指令

TEST指令在每对操作数的对应数据位之间执行隐含的`与`操作，并相应设置标志位。TEST指令和AND指令之间的唯一区别在于TEST并不修改目的操作数。TEST指令的操作数格式与AND相同，该指令在测试操作数某些位是否被设置时特别有用。
例子：测试多个位。TEST指令可同时检查几个位。假设我们想要知道AL中位0和位3是否被同时设置了，就可以使用以下的指令（例子中的值00001001称为位掩码）:

```assembly
test al,00001001b ;测试位0和位3
```

从以下的数据实例中，可以推断出仅当所有的测试位都清零时零标志才置位：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.740u667f0m40.webp)

### 影响的标志：

TEST指令总是清除溢出标志和进位标志。它修改符号标志、零标志、奇偶标志的方式与AND指令相同。

## 6.2.7 CMP指令

CMP指令在源操作数和目的操作数之间执行隐含的减法操作，两个操作数都不会被修改：

```assembly
CMP 目的操作数，源操作数
```

CMP指令与AND指令的操作数格式相同。

### 影响的标志：

CMP指令根据对目的操作数进行假想的减法操作后目的操作数的值修改溢出标志、符号标志、零标志、进位标志、辅助进位标志和奇偶标志的值。在比较两个无符号操作数的时候，零标志和进位标志反映了两个操作数之间的关系：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.407xask0l2e0.webp)

如果比较的两个操作数是有符号的，则符号标志、零标志、溢出标志与两个操作数之间的关系如下表所示：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.5jt0blo5hvk0.webp)

CMP指令是创建条件逻辑结构的有效工具。IF语句在汇编语言中等价于在CMP指令后跟一条条件跳转指令。
例子：下面我们来看三个代码片断，以弄清CMP指令是如何影响标志的。在AX中装入5
并和10比较的时候，由于5减去10需要借位，因此CF标志位置位：

```assembly
mov ax,5
cmp ax,10	; ZF = 0 and CF = 1
```

比较1000和1000会设置零标志位，因为从源数中减去目的数得到的结果是0:

```assembly
mov ax,1000
mov cx,1000	; ZF = 1 and CF = 0
cmp cx,ax
```

比较105和0会清除零标志和进位标志，因为105大于0:

```assembly
mov si,105
cmp si,0	; ZF = 0 and CF = 0
```

## 6.2.8 设置和清除单个CPU状态标志

如何设置和清除零标志、符号标志、进位标志和溢出标志？有很多种方法，但大多数方法都需要修改目的操作数。要设置零标志，可以将操作数同0进行TEST或AND操作；要想清除零标志，可以将操作数同1进行OR操作：

```assembly
test 	a1,0			;设置零标志
and 	al,0			;设置零标志
or 		al,1			;清除零标志
```

TEST指令不会修改操作数，不过AND操作修改操作数。要设置符号标志，将操作数的最高位同1进行OR操作；要清除该标志，将操作数的高位同0进行AND操作：

```assembly
or 	al,80h		;设置符号标志
and al,7Fh		;清除符号标志
```

使用STC指令可以设置进位标志，使用CLC指令可以清除进位标志：

```assembly
stc		;设置进位标志
clc		;清除进位标志
```

要设置溢出标志，把两个将得到负数和的正数字节相加；要清除溢出标志，将操作数同0进行OR操作：

```assembly
mov al,7Fh		;AL=+127
inc al			;AL=80h(-128),OF=1
or  eax,0		;清除溢出标志
```

# 6.3 条件跳转

## 6.3.1 条件结构

在IA-32指令集中没有高级的逻辑结构，但无论多么复杂的结构，都可以使用比较和跳转指令的组合来实现。执行条件语句时包括两个步骤：首先，使用CMP,AND,SUB之类的指令修改CPU标志；其次，使用条件跳转指令测试标志值并导致向新地址的分支转移。下面来看几个例子。
例子1:用CMP指令比较AL和0,如果CMP指令设置了零标志，那么JZ(为0则跳转）
指令就跳转到标号L1处

```assembly
	cmp al,0
	jz 	L1		;如果ZF=1则跳转
L1:
```

例子2:用AND指令对DL寄存器执行位与操作，这会影响零标志，如果零标志清零则JNZ(不为0则跳转）指令就会引起跳转：

```assembly
	and d1,10110000b
	jnz	L2	;如果ZF=0则跳转
L2:
```

## 6.3.2条件跳转（Jcond)指令

条件跳转指令在标志条件为真时分支转移到新的目的标号处，如果条件标志为假，那么立即执行紧跟在条件跳转指令之后的指令。格式如下：

```assembly
Jcond 目标地址
```

格式中的cond指的是一个标志条件，用来表示一个或多个标志的状态。例如：

```assembly
jc	如果进位（进位标志设置）则跳转
jnc	如果无进位（进位标志清除）则跳转
jz	如果为零（零标志设置）则跳转
jnz	如果非零（零标志清除）则跳转
```

我们已经知道算术指令、比较指令和布尔指令几乎总是会设置标志位，条件跳转指令检查标志位的状态并根据标志位的状态决定是否跳转。

### 限制：

MASM在默认情况下要求跳转的目的地址在当前的过程之内（在第5章中讲述JMP
指令时曾提到过）,==为了突破这种限制，可以声明一个全局标号（标号后面跟`::`):==

```assembly
	jc MyLabel
MyLabel::
```

通常，应尽量避免跳转到当前的过程之外，否则调试程序时会比较困难。
在Intel386之前，跳转的目标地址被限制在距跳转指令后的第一条指令+128~-127个字节的范围之内。IA-32处理器可跳转到当前段内的任何地址

### 使用CMP指令：

假设我们想在AX等于5时跳转到位置L1处。在下面的例子中，假设AX等于5,CMP指令设置了零标志，由于零标志置位了，执行JE指令就会发生跳转：

```assembly
cmp ax,5
je 	L1		;相等则跳转
```

如果AX不等于5,CMP就会清除零标志，执行JE指令就不会发生跳转。在下面的例子中，
由于AX小于6而发生了跳转：

```assembly
mov ax,5
cmp ax,6
j1	L1		;如果小于则跳转
```

下面的例子中，由于AX大于4而发生了跳转：

```assembly
mov ax,5
cmp ax,4
jg L1		;大于则跳转
```

## 6.3.3 条件跳转指令的类型

IA-32指令集中跳转指令的数目惊人地多，支持根据有符号、无符号整数的比较以及对CPU状态标志的检查进行跳转的一系列指令，条件跳转指令可分成下面的4类：

- ==基于特定的标志值的。==
- ==根据两个操作数是否相等，或根据(E)CX的值的。==
- ==基于无符号操作数的比较结果的。==
- ==基于有符号操作数的比较结果的。==

表6.2列出了基于特定CPU标志值：零标志、进位标志、溢出标志、奇偶标志和符号标志的跳转指令。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.28jol4z2wy80.webp)

### 基于恒等性比较的跳转指令

表6.3列出了基于两个操作数是否相等或CX,ECX值是否为零的跳转指令。符号leftOp(左操作数）和rightOp(右操作数）是指CMP指令的左（目的）和右（源）操作数：

```assembly
CMP leftop,rightop
```

操作数名反映了代数学中关系运算符中操作数的顺序，例如在表达式X<Y中，X称为左操作数（leftOp),Y称为右操作数（rightOp)。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.6su0sbmfugg0.webp)

JE指令和JZ指令是等价的，JNE指令和JNZ指令是等价的。下面是一些例子：

```assembly
;例子1:
mov edx,0A523h
cmp edx,0A523h
jne L5			;不发生跳转
je L1			;发生跳转
;例子2:
mov bx,1234h
sub bx,1234h
jne L5			;不发生跳转
je L1			;发生跳转
;例子3:
mov CX,0FFFFh
inc cx
jcxz L2			;发生跳转
;例子4:
xor ecx,ecx
jecxz L2		;发生跳转
```

### 基于无符号数比较的跳转指令

基于无符号整数比较结果的跳转指令如表6.4所示。在比较无符号值的时候，这种类型的跳转指令非常有用，例如7FFFh和8000h作为16位的无符号整数进行比较时，前者的值是小于后者的（作为有符号整数时，前者的值大于后者）。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.hffg6jycahs.webp)

### 基于有符号数比较的跳转指令

表6.5列出了基于有符号数比较的跳转指令。例如，1字节的有符号值80h(-128d)是小于7Fh(+127d)的。下面例子说了在比较80h和7Fh时JA和JG指令的不同之处：

```assembly
mov al,7Fh		;7Fh或+127
cmp al,80h		;80h或-128
ja IsAbove		;不跳转，因为7F不大于80h
jg IsGreater	;跳转，因为+127大于-128
```

在上面的例子里，JA指令并不跳转，因为无符号数7Fh比无符号数80h要小。相反，JG指令执行跳转，因为+127(7Fh)大于-128(80h)。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.3zksalhuuqi0.webp)

下面来看几个基于有符号比较的跳转指令的例子。

```assembly
;例子1:
mov edx,-1
cmp edx,0
jnl L5		;不跳转
jnle L5		;不跳转
j1 L1		;跳转
;例子2:
mov bx,+34
cmp bx,-35
jng L5		;不跳转
jnge L5		;不跳转
jge L1		;跳转
;例子3:
mov ecx,0
cmp ecx,0
jg L5		;不跳转
jnl L1		;跳转
;例子4:
mov ecx,0
cmp ecx,0
j1 L5		;不跳转
jng L1		;跳转
```

### 条件跳转指令的跳转范围

在16位实地址模式下，条件跳转使用单个有符号字节（称为相对偏移地址）定位跳转的目标地址，目标地址被限定在距当前地址`-128~+127`个字节的范围之内。这里当前地址是指当前指令之后的下一条指令的地址，因为CPU总是在执行当前的指令之前增加指令指针的值以指向下一条要执行的指令的地址。LOOP,LOOPZ和LOPPNZ指令（参见6.4节）的目标地址也有同样的范围限制
下面的例子列出了在16位实地址模式下编译时汇编器为JZ指令生成的机器码字节。偏移0000处的JZ指令的编码是7403,操作码是74,相对偏移是03。（NOP是空操作指令。）紧跟JZ之后的指令的地址是0002,因此CPU把0002和偏移03相加，得到偏移0005(也就是标号L2的偏移地址）:

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.6q9red1q6f40.webp)

### 16位模式下的长跳转

16位模式程序中的跳转目标地址如果超出了一个有符号字节偏移所允许的范围，MASM就会给出一个`relative jump out of range`的错误。假如指令的平均长度是3个字节，循环中可以放大约40条指令。要绕过这种限制，避免错误信息，可以先跳转到一条无条件跳转指令处（跳转范围是16位的有符号字）,再通过无条件跳转指令跳转到真正的目的地址处：

```assembly
	jz l2
	Jmp l3
L2: jmp farTarget
L3:
```

### 32位模式下的跳转

在32位模式下，如果跳转的目标地址距当前地址超出了1个字节的范围，MASM就会为跳转指令生成32位的有符号相对偏移地址。在下例中，标号L1距当前的地址189(BDh)个字节，因此跳转指令的地址域是32位的：

```assembly
00000000 0F 84 000000BD jz L1
```

使用32位偏移地址的跳转指令的操作码为两个字节长，在上面的例子中，操作码是0Fh和84h

## 6.3.4 条件跳转的应用

### 测试状态位

AND,OR,CMP,NOT和TEST指令后面常跟能够改变程序流程的条件跳转指令。条跳转指令通常要测试CPU状态标志位的值。例如，假设8位的内存操作数status中存放着同计算机相连的外设的状态信息，下面的指令在位5置位时跳转到某标号处，表示机器处于脱机状态：

```assembly
mov 	al,status
test 	al,00100000b	;测试位5
jnz 	Equipoffline
```

下面的语句在位0,位1,位4中的任何一位置位时跳转到另外一个标号处：

```assembly
mov 	al,status
test 	al,00010011b	;测试位0,1,4
jnz 	InputDataByte
```

如果想在位2,位3和位7全部置位时跳转某标号处，需要使用AND和CMP两条指令：

```assembly
mov al,status
and al,10001100b		;保留位2,3,7
cmp al,10001100b		;所有位都置位了？
je ResetMachine			;是：则跳转到标号处
```

### 取两个整数中的较大值

下面的指令比较AX和BX中的无符号整数并把其中的较大者送DX寄存器：

```assembly
	mov dx,ax		;假设AX较大
	cmp ax,bx		;假设AX>=BX,则
	jae L1			;跳转至L1
	mov dx,bx		;否则将BX的值移至DX
L1:				;DX包含较大的值
```

### 取三个整数中的最小值

下面的指令比较V1,V2和V3三个无符号变量的值，并把其中的最小者送AX寄存器：

```assembly
.data
V1 WORD ?
V2 WORD ?
V3 WORD ?
.code
	mov ax,V1		;假设V1是最小的
	cmp ax,V2		;若ax<=V2,则

	jbe L1			;跳转至L1
	mov ax, V2		;否则将V2的值移至ax
L1:
	cmp ax,V3		;若ax<=V3,则
	jbe L2			;跳转至L2
	mov ax, V3		;否则将V3的值移至ax
L2:
```



### 应用：数组的顺序查找

程序设计中的一个常见任务是在数组中查找符合特定标准的值，在发现第一个匹配项时，可以显示其值或返回指向数据所在位置的指针。下面我们看看这在整数数组中实现起来是多么容易。ArryScan.asm程序查找16位整数数组中的第一个非零值，如果找到一个匹配项就显示该值，否则显示一条该值无法找到的信息：

```assembly
TITLE Scanning an Array	(ArryScan.asm)
;扫描数组查找第一个非零值
INCLUDE Irvine32.inc
.data
intArray SWORD 0,0,0,0,1,20,35,-12,66,4,0
;intArray SWORD 1,0,0,0
;可选的测试数据
;intArray SWORD 0,0,0,0
;可选的测试数据
;intArray SWORD 0,0,0,1
;可选的测试数据
noneMsg BYTE "A non-zero value was not found",0
;注意程序中现在已经注释掉了的测试数据是可选的，你可以去掉注释，以便使用不同的数据来测试该程序。
.code
main PROC 
	mov ebx,OFFSET intArray		;指向数组
	mov ecx,LENGTHOF intArray	;循环计数器
L1: 
	cmp WORD PTR [ebx],0		;值和零比较
	add ebx,2					;指向下二个值
	loop L1						;继续循环
	jmp notFound				;一个都没找到
found:							;显示这个值
	mov sx eax,WORD PTR[ebx]
	call WriteInt
	jmp quit
notFound:						;显示信息“not found"
	mov edx,OFFSETnoneMsg
	call WriteString
quit:
	call Crlf
	exit
main endp
END main
```

### 应用：字符串加密

在6.2.4节讲述了XOR指令的一个有趣的特性：如果整数X同Y进行异或得到的结果再次同Y进行异或，那么其运算结果为X:
$$((X \otimes Y ) \otimes Y ) = X$$

XOR的这种“可逆”的特性为进行数据加密提供了便利：把用户输入的消息（称为明文）中的所有字符都同另外一个字符串（称为密钥）中的字符进行异或操作，可以把它转换成不可识别的字符串（称为密文）。密文可以存储或转移到远方的其他地点，未经授权的人无法阅读。授权用户可以使用密钥解密密文得到原始的明文。

### 例子程序：

下面要看的程序使用的是对称加密方法，也就是说加密和解密使用同样的密钥，程序的流程如下。

- 用户输入明文。
- 程序重复使用单个字符对明文加密以生成密文，并在屏幕上显示。
- 程序解密密文，得到原始的明文并在屏幕上显示。

下面是程序的输出示例：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.4glx60kvc9i0.webp)

程序清单：下面是程序的完整清单：

```assembly
TITLE Encryption Program (Encrypt.asm)
INCLUDE Irvine32.inc
KEY = 239			;1~255之间的任意值
BUFMAX = 128		;缓冲区的最大值
.data
sPrompt 	BYTE "Enter the plain text: ",0
sEncrypt 	BYTE "Cipher text: ",0
sDecrypt 	BYTE "Decrypted :	",0
buffer 		BYTE BUFMAX+1 DUP(0)
bufSize 	DWORD ?
.code
main PROC
	call 	InputTheString		;输入明文
	call 	TranslateBuffer		;加密缓冲区
	mov 	edx,OFFSET sEncrypt	;显示加密的消息
	call 	DisplayMessage
	call 	TranslateBuffer		;解密缓冲区
	mov 	edx,OFFSET sDecrypt;显示解密消息
	call 	DisplayMessage
	exit
main ENDP
InputTheString PROC
; Prompts user for a plaintext string. Saves the string
; and its length .
; Receives : nothing
; Returns : nothing
	pushad
	mov 	edx,OFFSET sPrompt;显示提示信息
	call 	WriteString
	mov 	ecx,BUFMAX			;最多字符数目
	mov 	edx,OFFSET buffer	;指向缓冲区
	call 	ReadString			;输入字符串
	mov 	bufsize,eax			;保存其长度
	call 	Crlf
	popad
	ret
InputTheString ENDP
DisplayMessage PROC
;Displays the encrypted or decrypted message.
; Receives : EDX points to the message
; Returns : nothing
	pushad
	call 	WriteString
	mov 	edx,OFFSET buffer	;显示缓冲区的内容
	call 	WriteString
	call 	Crlf
	call 	Crlf
	popad
	ret
DisplayMessage ENDP
TranslateBuffer PROC
; Translates the string by exclusive-ORing each
; byte with the encryption key byte.
; Receives : nothing
; Returns : nothing
	pushad
	mov 	ecx,bufsize		;循环计数器
	mov 	esi,0			;缓冲区的索引0
L1:
	xor 	buffer[esi],KEY	;转换一个字节
	inc		esi				;指向下一个字节
	loop L1
	popad
	ret
TranslateBuffer ENDP
END main
```

## 6.3.5位测试指令（可选）

BT,BTC,BTR和BTS指令统称为位测试（bit testing)指令，这些指令很重要，因为它们可以在单条原子指令内可执行多个步骤。位测试指令对多线程程序非常有用，对多线程程序而言，在不冒被其他线程中断的危险的情况下对重要标志位（称为信号量）进行测试、清除、设置或求反是非常重要的。本书网站上有一个描述简单的多线程情况的例子。

### BT指令

BT(位测试，bit test)指令选择第一个操作数的位n并把它复制到进位标志中：

```assembly
BT bitBase,n
```

第一个操作数称为位基（bitBase),它不会被指令所修改。BT指令允许以下类型的操作数：

```assembly
BT r/m16,r16
BT r/m32,r32
BT r/m16,imm8
BT r/m32,imm8
```

在下例中，进位标志值等于变量semaphore第7位的值：

```assembly
.data
semaphore 	WORD 10001000b
BT 			semaphore,7		;CF=1
```

在Intel指令集引入BT指令之前，我们不得不把变量复制到寄存器中，然后再通过移位把第7位送到进位标志中：

```assembly
mov ax,semaphore
shr ax,8			;CF=1
```

(这里的SHR指令把AX中所有的数据位都向右移动8个数据位，这导致位7被送入进位标志中。SHR指令将在7.2.3节中讲述。）

### BTC 指令

BTC(位测试并取反，bit test and complement)指令选择第一个操作数的位n并把它复制到进位标志中，同时对位n取反：

```assembly
3TC bitBase,n
```

BTC指令允许的操作数类型与BT指令相同。在下例中，进位标志等于sempahore变量的位6的值，同时变量中的位6被取反：

```assembly
.data
semaphore WORD 10001000b
BTC 	semaphore,6			;CF=0,sempahore=11001000b
```

### BTR指令

BTR(位测试并复位，bit test and reset)指令选择第一个操作数的位n并把它复制到进位标志中，同时位n清零：

```assembly
BTR bitBase,n
```

BTR指令允许的操作数类型与BT和BTC指令相同。在下例中，进位标志等于semaphore位7的值，同时位7清零：

```assembly
.data
semaphore WORD 10001000b
.code
BTR semaphore,7	; CF = 1, semaphore=00001000b
```

### BTS指令

BTS(位测试并置位，bit test and set)指令选择第一个操作数的位n并把它复制到进位标志中，同时位n置位：

```assembly
BTS bitBase,n
.data
semaphore 	WORD 10001000b
.code
BTS 		semaphore,6		;CF=0,semaphore=11001000b
```

BTR指令允许的操作数类型与BT指令相同。在下例中，进位标志等于semaphore位6的值，同时Semaphore中的对应位置位：

```assembly
.data
semaphore WORD 10001000b
.code
BTS 		semaphore,6			;CF=0,semaphore=11001000b
```



# 6.4条件循环指令

## 6.4.1 LOOPZ和LOOPE指令

LOOPZ指令（为零则循环，loop if zero)允许在零标志置位并且ECX中的无符号值大于0时循环，目标标号距LOOPZ后下一条指令的距离应该在`-128~+127`字节范围内，指令格式是：

```assembly
LOOPZ 目的地址
```

LOOPE指令（相等则循环，loop if equal)与LOOPZ指令是等价的，因为二者的操作码是相同的。LOOPZ和LOOPE指令的执行逻辑如下：

```assembly
ECX = ECX - 1
```

如果ECX>0手且ZF=1,跳转到目的地址否则将不发生跳转，控制权转移到下一条指令。LOOPZ和LOOPE指令不影响任何状态标志。
运行于实地址模式下的程序使用CX作为LOOPZ指令的默认循环计数器，如果想强制使用ECX作为循环计数器，应使用LOOPZD指令。

## 6.4.2 LOOPNZ和LOOPNE指令

LOOPNZ指令（不为零则循环，loop if not zero)与LOOPZ指令是对应的，它在ECX中的无符号值大于0并且零标志复位的状态下进行循环，指令格式是：

```assembly
LOOPNZ 目的地址
```

LOOPNE指令（不相等则循环，loop if not equal)与LOOPNZ指令是等价的，因为二者的机器码相同。LOOPNZ和LOOPNE指令的执行逻辑如下：

```assembly
ECX = ECX - 1
```

如果ECX>0并且ZF=1,跳转到目的地址
否则将不发生跳转，控制权转移到下一条指令。
例子：下面的代码片断（摘自Loopnz.asm)扫描数组中的每个数值直到发现非负数（正数或零）为止（也就是检测到符号位清零时）:

```assembly
array SWORD 	-3,-6,-1,-10,10,30,40,4
sentinel SWORD
.code
	mov esi,OFFSET array
	mov ecx,LENGTHOF array
L1:
	test WORD PTR [esi],8000		;测试符号位
	add esi,TYPE array
	popfd							;标志出栈
	loopnz L1						;继续循环
	jnz quit						;未发现
	sub esi,TYPE array				;ESI指向某个值
quit:
```

程序执行完毕后，如果找到了一个非负值，则ESI指向该值；如果没有找到非负值，则循环在ECX等于0时停止，此时执行JNZ指令跳转到标号quit处，ESI指向数组之后的哨兵值（0)。

# 6.5 条件结构

本节讲述几个高级语言中常见的条件结构，读者将看到所有结构都是非常易于转换成汇编言的。条件结构（conditional structure)可认为是在不同的逻辑分支之间引发选择的一个或多个条件表达式，每个分支都会执行不同的指令序列
计算机科学系的学生通常都会学习一门编译器建构的课程，在该课程中他们要实现一个程序设计语言的编译器。本节讨论的代码优化技术对这样的课程大有神益。

## 6.5.1 IF块结构语句

在大多数高级语言中，IF语句通常表示布尔表达式后有两个语句序列：当表达式为真时执行
其中的一个语句序列，表达式为假时执行另外一个：

```c
if(表达式)
	语句序列1
else
	语句序列2
```

语句的else部分是可选的，图6.1的流程图显示了IF条件结构的两条分支路径，其中分别以true(真）和false(假）标识。
例子1:下面使用的是Java/C++的语法格式，如果op1等于op2则执行两条赋值语句：

```assembly
if(op1 == op2)
{
	X = 1;
	Y = 2;
}
```

把IF语句翻译成汇编语言的唯一方法是使用CMP语句，其后跟一个或多个条件跳转语句。由于op1和op2都是内存操作数，在执行CMP之前其中的一个必须被送人寄存器中。下面的代码实现了IF语句的功能，实现反转了恒等测试条件并使用JNE指令以尽可能地高效：

```assembly
	mov eax,opl		
	cmp eax,op2		;op1==op2?
	jne L1			;否：跳过下面的指令
	mov X,1			;是：对X和Y赋值
	mov Y , 2
L1:
```

如果实现时使用JE指令实现了=运算符，得到的代码就没那么紧凑了（6条指令而非5条）:

```assembly
	mov eax,op1
	cmp eax,op2		;op1==op2?
	je  L1			;是，跳转到L1
	jmp L2			;否：跳过赋值语句
L1: 
	mov X,1			;对X和Y赋值
 	mov Y,2
L2:
```

同样的高级语言代码可用以多种方式翻译成汇编语言代码，本章例子中编译后生成的代码只是一个假想的编译器或许会生成的代码。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.1mzorutnxhnk.webp)

### 例子2:

MS-Windows使用的FAT32文件系统中，磁盘簇的大小决定于磁盘的总容量，在下面的伪码中，如果磁盘的容量小于8GB,则把簇的大小设为4096字节（在名为gigabytes的变量中）,否则把簇的大小设为8192字节：

```assembly
clustersize = 8192;
if(gigabytes<8)
	clustersize=4096;
;下面是该语句的汇编语言实现：
	mov clustersize,8192	;假设使用大的磁盘簇
	cmp gigabytes,8			;是否大于8GB?
	jae next
	mov clustersize,4096	;换成小的磁盘簇
next:
```

(有关磁盘簇的内容在14.2节中讲述。）

### 例子3:

如下IF-ELSE伪代码语句具有另一个分支：

```c
if opl > op2 then
	call Routine
else
	call Routine2
end if
```

在以下汇编语言实现中，假设op1和op2为有符号双字变量，大于操作符通过JNG实现，它是JG的补充：

```assembly
	mov eax,op1
	cmp eax,op2		;Op1>op2?
	jng A1			;否：调用Routine2
	call Routine1	;是：调用Routine1
	jmp A2
A1:
	call Routine2
A2:
```

### 应用白盒测试

汇编语言中的复杂的条件语句有多条执行路径，这使得通过（调试器）查看代码进行调试相当困难。优秀的程序员通常使用一种称为白盒测试（white box testing)的技术验证过程的输入和相应的输出。进行白盒测试必须要有一份程序的源代码，要为输入变量赋各种可能值，对于每种输入组合，手动跟踪源代码并验证程序的执行路径以及过程的输入输出。让我们看看如何做白盒测试，假设想把下面的嵌套IF语句翻译成汇编语言：

```c
if op1 == op2 then
	if X > Y then
		call Routine1
	else
		call Routine2
   	end if
else
	call Routine3
end if
```

下面是到汇编语言的一种可能的翻译，添加了行号以便于引用。翻译时反转了（使用JNE指令）初始条件（op1==op2)并立即跳转到外层的ELSE部分。剩下的事情就是翻译内层的IF-ELSE语句了：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.7hxbwhum4x40.webp)

表6.6列出了例子代码的白盒测试的结果。测试输入值赋给了op1,op2,X,Y,验证了执行路径。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.5w8q3bc4j2c0.webp)

## 6.5.2 复合表达式

### 逻辑AND运算符

用汇编语言实现包含AND运算符的布尔表达式是非常容易的。考虑下面的伪代码，其中假设值是无符号整数：

```c
if (al > bl) AND (bl > c1)
{
	X=1
}
```

#### 短路求值

下面的汇编代码是一种使用短路求值的直接实现，如果第一个表达式为假，则第二个表达式根本就无须计算：

```assembly
	cmp al,bl		;第一个表达式
	ja 	L1
	jmp next
L1: 
	cmp bl,cl		;第二个表达式
	ja L2
	jmp next
L2: 
	mov X,1			;全为真：X设为1
next:
```

可以通过使用JBE代替JA指令来优化代码，使得最后只包含5条指令：

```assembly
	cmp al,bl		;第一个表达式
	jbe next		;如果为假则退出
	cmp bl,cl		;第二个表达式
	jbe next		;如果为假则退出
	mov X,1			;全为真
next:
```

优化后的代码数量减少了29%(7条指令减少为5条）,如果不选取第一个JBE分支的话，将直接执行其后的第二条CMP指令。高级语言编译器（如Java/C/C++)使用短路求值法的目的大概在于效率方面的考虑。

### 逻辑OR运算符

当复合表达式中的多个表达式使用逻辑OR运算符连接的时候，只要任何一个表达式为真则复合表达式为真。以下面的伪码为例：

```c
if (al > bl) OR (bl > c1)
	X=1
```

在下面的实现中，如果第一个表达式为真，则代码分支转移到L1,否则直接执行第二条CMP指令。第二条表达式反转了“大于”运算符并使用JBE代替：

```assembly
	cmp al,bl		;1:比较AL和BL
	ja L1			;如果为真，跳过第二个表达式
	cmp bl,cl		;2:比较BL和CL
	jbe next		;为假则跳过下面一条语句
L1:
	mov X,1		;为真则X设为1
	Next
```

对于给定的复合表达式，在汇编语言中通常有多种方法可以实现。

## 6.5.3 WHILE 循环

WHILE结构在执行一块指令之前首先测试条件，只要条件为真，就重复执行语句块。下面的循环是用C++写的：

```c
while(val1<val2)
	va11++;
	val2--;
}
```

在用汇编语言编写该结构时，将循环条件反转并在条件为假时跳转到endwhile处更加方便。假设val1和val2是变量，那么必须在开始的时候把其中一个送寄存器并在结束时保存：

```assembly
		mov eax,vall		;复制变量至EAX
@@while:
		cmp eax,va12		; if not (vall < val2)
		jnl endwhile		;退出循环
		inc eax				;val1++;
		dec val2			;va12--;
		jmp @awhile			;重复循环
endwhile:
		nov val1,eax		;保存va11的新值
```

在循环中，EAX是val1的代理（替代物）,任何对val1的引用必须通过EAX进行。另外，请注意程序使用了JNL指令，这意味着val1和val2是有符号整数。

### 例子：嵌套在循环中的IF语句

高级语言特别擅长于表达嵌套的控制结构。下面的C++例子中，IF语句被嵌套在一个WHILE循环中。代码的功能是计算数组中大于变量sample所有数组元素之和：

```c
int array[] = {10,60,20,33,72,89,45,65,72,18};
int sample = 50;
int ArraySize = sizeof array / sizeof sample;
int index = 0;
while(index<Arraysize)
{
	if(array[index]>sample)
    {
		sum+=array[index];
    }
    index++;
}
```

在开始以汇编语言编写循环代码之前，首先用图6.2所示的流程图来描述程序的逻辑结构为简化到汇编语言的转换并尽量减少内存访问以提高执行速度，使用了寄存器来代替变量（EDX=sample,EAX=sum,ESI=index,ECX=ArraySize),其中标号的名字也被加到了图形上。

#### 汇编代码：

根据流程图生成汇编代码的最简单的方法就是分别为每个图形实现代码。注意流程图中的标号和下面代码中标号的直接对应关系（参见Flowchart.asm):

```assembly
.data
sum 	DWORD 0
sample 	DWORD 50
array 	DWORD 10,60,20,33,72,89,45,65,72,18
ArraySize = ($-Array)/TYPE array
.code
main PROC
	mov eax,0		; sum
	mov edx,sample
	mov esi,0		; index
	mov ecx,ArraySize
L1:
	cmp esi,ecx
	j1 L2
	jmp L5
L2: 
	cmp array[esi*4], edx
	jg L3
	jmp L4
L3:
	add eax,array[esi*4]
L4: 
	inc esi
	jmp L1
L5:
	mov sum,eax
```

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.5n48yiwmc0s0.webp)

### 6.5.4以表格驱动的分支选择

表格驱动的分支选择是使用表格查找法来替代多路选择结构的一种方法。要使用该方法，必须创建一个包含查找值和过程偏移的表格，程序使用循环来搜索该表格，当需要大量的比较时，这种方法是工作得最好的。
例如，下面就是一个表格的部分，包含了待查找的单字符值和过程地址：

```assembly
.data
CaseTable BYTE 'A'		;查找值
	DWORD Process_A		;过程的地址
	BYTE 'B'
	DWORD Process_B
	(etc.)
```

假设Process_A,Process_B,Process_C和Proces-D分别位于地址120h,130h,140h和150h处，在内存中表格的布局如图6.3所示。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.1iolv4pl4la8.webp)

### 例子程序：

在下面的例子程序（ProcTable.asm)中，用户从键盘输入一个字符，程序使用一个循环将该字符同表中的每个项相比较，对于找到的第一个匹配项，紧跟在待查找值其后存储的过程将被调用。每个过程使用EDX来装入不同字符串的偏移，然后在循环中显示该字符串：

```assembly
TITLE Table of Procedure Offsets	(ProcTble.asm)
; This program contains a table with offsets of procedures.
; It uses the table to execute indirect procedure calls .
INCLUDE Irvine32.inc
CaseTable 	BYTE 'A'			;查找值
			DWORD Process_A		;过程的地址
EntrySize = ($-CaseTable)
			BYTE 'B'
			DWORD Process_B
			BYTE 'C'
			DWORD Process_C
			BYTE ID
			DWORD Process D
NumberofEntries = ($-CaseTable)/EntrySize
prompt BYTE "Press capital A,B,C,or D:",0
;为每个过程定义一个消息字符串。
msgA BYTE "Process_A",0
msgB BYTE "Process_B",0
msgC BYTE "Process_C",0
msgD BYTE "Process_D",0
.code
main PROC
	mov 	edx,OFFSET prompt		;要求用户输入
	call 	writestring
	call 	ReadChar				;读取字符至AL
	mov 	ebx,OFFSET CaseTable	;EBX指向表格
	mov 	ecx,NumberofEntries		;循环计数器
L1:
	cmp 	al,[ebx]				;找到了匹配项？
	jne 	L2						;否：继续
	call 	NEAR PTR [ebx + 1] 		;是：调用该过程
;CALL指令调用存储在EBX+1内存地址处的过程地址，这种间接调用格式要求使用NEARPTR运算符。
	call 	writestring				;显示消息
	call 	Crlf
	jmp 	L3						;退出搜索
L2:
	add 	ebx,EntrySize			;指向下一个表项
	loop 	L1						;重复直到ECX=0
L3:
	exit
main ENDP
;下面的每个过程都把一个不同的字符串偏移送EDX。
Process_A PROC
		mov edx, OFFSET msgA
		ret
Process_A ENDP
Process_B PROC
		mov edx, OFFSET msgB
		ret
Process_B ENDP
Process_C PROC
		mov edx,OFFSET msgC
		ret
Process_C ENDP
Process_D PROC
		mov edx, OFFSET msgD
		ret
Process D ENDP
END main
```

表格驱动的分支选择有一些初始的开销，但可以减少编写代码的数量。表格可以处理大量的比较，而且这种方法比一系列很长的比较、跳转和调用指令的组合更加容易修改，表格甚至可以在运行时动态配置。

# 6.6 应用：有限状态机

有限状态机（FSM,Finite-State Machine)是依据输入改变状态的机器或程序。使用图来表示一个有限状态机是非常简单的，图中包含的方块（或圆）称为节点，节点之间带箭头的线称为边（edge)或弧（arc)。
图6.4是一个简单的例子，图中的节点代表程序的状态，边代表从一种状态到另一种状态的转换。有一个节点是起始状态（start state),在图中以正向的箭头标示；其余状态可以用数字或字母来标识。终结状态（terminal state)可以有一个或多个，以加粗的方框标记，终结状态是代表程序可能无错误终止的某种状态。有限状态机是一种称为有向图（directed graph或diagraph)的更加œ通用的结构的一个特殊实例，后者是由有特定方向的边连接的一系列节点构成的集合。

在计算机科学中，有向图在动态数据结构和高级搜索技术等方面有许多应用。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.2seflen83hk0.webp)

## 6.6.1 输入字符串的验证

读取输入流的程序通常必须执行一定的错误检查步骤以验证输入。例如，程序设计语言的编译器可使用有限状态机扫描源程序，并把单词和符号转换成关键字、算术运算符和标识符等记号(token)。
使用有限状态机检查输入字符串的有效性时，通常逐个读取字符，每个字符都用图中的一条边（转换）来标识。有限状态机使用下面的方法检测非法输入，如果出现了下面任意一种情况，就可以认为是检测到了非法输入：

- 下一个输入字符与从当前状态出发的任何一种转换都不能匹配。
- 输入已经结束，而当前状态不是终结状态。

### 字符串的例子

我们依据下面两条规则检查输入字符串的有效性：

- 字符串必须以字母x开始，以字母z结束。
- 在第一个和最后一个字符之间，可以有0个或多个字符，但字符必须在范围（a...y)之内

图6.5所示的FSM状态图描述了该字符串的格式，每种转换都由一种特定类型的输入来标识例如从状态A到状态B的转换只能在从输入流中读人字母x之后才能完成；输入任何除了z之外的字母则完成状态B到自身的转换；从状态B到状态C的转换只在从输入流中读入字母z时才会发生
如果输入已经结束，但程序仍然处于状态A或状态B,则表明发生了错误，因为只有状态C被标记为终结状态。下面的输入字符串是可以由上述FSM识别的有效字符串：

```
xaabcdefgz
xz
xyyqqrrstuvz
```

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.52ufm0z4bqw0.webp)

### 6.6.2有符号整数的验证

一个分析有符号整数的有限状态机如图6.6所示，输入由最前面的一个可选的符号和其后的一系列数字组成。该图并没有规定最多应该有多少数字（digit)。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.5mdkce3cujc0.webp)

有限状态机很容易翻译成汇编语言代码。图中的每个状态（A,B,C,…）都由程序中的一个标号标识，各标号处执行以下动作：

- 调用输入过程，从输入中读取下一个字符。
- 如果是终结状态，则检查用户是否输入了回车键结束输入。
- 使用一条或多条比较指令检查从当前状态到其他状态的可能转换，每条比较指令后面都紧跟一条跳转指令。

例如，在状态A,下面的代码读取下一个输入字符并检查到状态B的可能转换：

```assembly
StateA:
	call Getnext	;读下一个字符送AL
	cmp al ,'+'		;开头是“+”号吗？
	je StateB		;转状态B
	cmp al,'-'		;开头是“-”号吗？
	je StateB		;转状态B
	call IsDigit	;如果AL中包含一个数字，2F=1
	jz statec		;转状态C
	call DisplayErrorMsg	;发现了无效输入
jmp Quit
```

在状态A的代码中，调用了链接库中的IsDigit过程，该过程在输入是数字时设置零标志这使得查找到状态C的转换成为可能。如果状态转换失败，程序显示一条错误消息并退出。在图6.7中给出了标号StateA处的代码对应的流程图。

### FSM的实现：

下面的程序实现了图6.6描述的分析有符号整数的有限状态机：

```assembly
TITLE Finite State Machine	(Finite.asm)
INCLUDE Irvine32.inc
ENTER_KEY = 13
.data
InvalidInputMsg BYTE "Invalid input",13,10,0
.code
main PROC
		call Clrscr
StateA:
		call Getnext		;读下一个字符送AL
		cmp  al,'+'			;开头是“+”号吗？
		je StateB			;转状态B
		cmp al,'-'			;开头是“-”号吗
		je StateB			;转状态B
		call IsDigit		;如果AL中包含一个数字，ZF=1
		jz Statec			;转状态C
		call DisplayErrorMsg;发现了无效输入
		jmp Quit
StateB:
		call Getnext		;读下一个字符送AL
		call IsDigit		;如果AL中包含一个数字则ZF=1
		jz Statec
		call DisplayErrorMsg;发现了无效输入
		jmp Quit
Statec:
		call Getnext		;如果AL中包含一个数字则ZF=1
		call IsDigit		;发现了无效输入
		jz Statec
		cmp al,ENTER_KEY	;是否按下了回车键？
		je Quit				;是：退出
		call DisplayErrorMsg;否：发现了无效输入
		jmp Quit
Quit:
		call Crlf
		exit
main ENDP
Getnext PROC
; Reads a character from standard input .
; Receives : nothing
; Returns : AL contains the character
		call ReadChar		;读取键盘输入
		call writeChar		;在屏幕上回显
		ret
Getnext ENDP
DisplayErrorMsg PROC
; Displays an error message indicating that
; the input stream contains illegal input.
; Receives : nothing .
; Returns : nothing
		push edx
		mov edx,OFFSET InvalidInputMsg
		call writestring
		pop edx
		ret
DisplayErrorMsg ENDP
END main
```

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.7buqnqlni900.webp)

如果AL中的字符是有效的十进制数字，本书链接库中的IsDigit过程设置零标志，否则清除零标志：

```assembly
;-----------------------------------
Isdigit PROC
; Determines whether the character in AL is a
;valid decimal digit.
; Receives : AL = character
; Returns : ZF=1 if AL contains a valid decimal
; digit ; otherwise , ZF=0 .
	cmp al,'0'
	jb ID1		;发生跳转时ZF=0
	cmp al,'9'
	ja ID1		;发生跳转时ZF=0
	test ax,0	;设置ZF=1
ID1:
	ret
Isdigit ENDP
```

# 6.7 决策伪指令

==MASM的决策伪指令（.IF,.ELSE,.ELSEIF,.ENDIF)使得在编写涉及到多路分支逻辑的代码时更加容易。==汇编器在幕后为这些伪指令自动生成CMP和条件跳转指令，可在输出的列表文件（.lst)中查看，其格式如下：

```assembly
.IF condition1
	statements
[.ELSEIF condition2
	statements ]
[.ELSE
	statements ]
.ENDIF
```

方括号中的部分.ELSEIF和.ELSE是可选的，但是.IF和.ENDIF是必需的。其中条件（condition)是一个布尔表达式，使用的运算符与C++/Java中的布尔运算符（如<,>,=,1=)相同。表达式是在运行时求值的，下面是一些有效条件的例子，寄存器和变量都是32位的：

```assembly
eax > 10000h
val1 <= 100
val2 == eax
val3 != ebx
```

下面是一些复合条件的例子：

```assembly
(eax>0) && (eax>10000h)
(val1 <=100) || (val2<=100)
(val2 != ebx) && !CARRY?
```

关系运算符和逻辑运算符的完整列表如表6.7所示。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.4hj3qd7hysc0.webp)

对决策伪指令的讲述是有争议的，因为初学者容易被其表面的简单性所欺骗。在使用决策伪指令之前，一定要彻底理解条件分支指令，除此之外，在汇编包含决策伪指令的程序时，最好检查一下输出的列表文件以确保汇编器生成的指令正是你想要的。

### 生成汇编代码：

当使用.IF和.ELSE等高级伪指令时，编译器承担起了代码编写的任务。例如，
我们使用.IF伪指令比较EAX和变量vall:

```assembly
mov eax,6
.IF eax > val1
	mov result,1
.ENDIF
```

假设val1和result都是32位的无符号整数，编译器遇到上面的代码行时，会将其扩展成下面的汇编语言指令：

```assembly
	mov eax,6
	cmp eax,vall		;基于无符号比较的跳转
	jbe @C0001
	mov result,1
@C0001:
```

标号名@C0001是由汇编器创建的，汇编器保证同一过程之内的所有标号都是唯一的。

## 6.7.1 有符号比较和无符号比较

使用.IF伪指令对数值进行比较时，必须清楚编译器是如何生成条件跳转指令的。如果比较的值中包含无符号变量，生成代码中就会插入无符号条件跳转指令。下面的例子比较EAX和无符号双字val1,这个例子在前面已经出现过了：

```assembly
.data
vall DWORD 5
result DWORD ?
.code
	mov eax,6
	.IF eax > val1
		mov result,1
	.ENDIF
```

汇编器使用JBE(无符号跳转）指令对其进行扩展：

```assembly
	the eax,val			;基于无符号比较的跳转
	moy result,1		
@C0001:
```

### 有符号整数的比较：

下面以val2试一下类似的比较，变量val2是一个有符号双字：

```assembly
.data
val2 SDWORD -1
.code
mov eax,6
	.IF eax val2
		mov result,1
	.ENDIF
```

现在编译器生成了使用JLE指令的代码，跳转基于有符号数的比较：

```assembly
		mov eax,6
		cmp eax,val2
		jle @C0001		;基于有符号比较的跳转
		mov result,1
@C0001:
```

### 寄存器的比较

随后我们可能要问的问题就是：如果两个寄存器进行比较会是什么样的情形
呢？很明显，编译器不能确定两个值是有符号的还是无符号的：

```assembly
	mov eax,6
	mov ebx,val2
.IF eax ebx
	mov result,1
.ENDIF
```

结果证明，汇编器默认使用的是无符号比较，因此比较两个寄存器的.IF伪指令是使用JBE指令实现的。

## 6.7.2 复合表达式

许多复合布尔表达式都使用逻辑“或”和逻辑“与”运算符，在使用.IF伪指令时，逻辑“或”使用“||”符号：

```assembly
.IF expressionl || expression2
	statements
.ENDIF
```

与之类似，逻辑“与”使用“&&”符号：

```assembly
.IF expressionl && expression2
	statements
.ENDIF
```

下面的例子将使用逻辑或操作符。

### 例子：设置光标位置

下面的SetCursorPosition过程对两个输入参数DH和DL进行范围检查（参见SetCur.asm)其中Y坐标（DH)必须在0~24之间，X坐标（DL)必须在0~79之间。如果其中的任何一个值超出了范围，屏幕上将显示一条错误信息：

```assembly
SetCursorPosition PROC
; Sets the cursor position .
; Receives : DL = X-coordinate, DH = Y-coordinate
; Checks the ranges of DL and DH
; Returns : nothing
.data
BadXCoordMsg BYTE "X-Coordinate out of range!", ODh, OAh,0
BadYCoordMsg BYTE "Y-Coordinate out of range!", ODh, OAh,0
.code
	.IF (DL>0) || (DL > 79)
		mov 	edx, OFFSET BadXCoordMsg
		call 	writestring
		jmp 	quit
	.ENDIF
	.IF (DH < 0) || (DH > 24)
		mov 	edx,OFFSET BadYCoordMag
		call 	writestring
		jmp 	quit
	.ENDIF
	call Gotoxy
quit:
	ret
SetCursorPosition ENDP
```

### 例子：大学课程注册

假设某大学生想注册一门课程，我们使用两个标准来决定学生是否可以注册：首先是此人的平均成绩（在0~400的范围之内，400是可能的最高分）;其次是此人要注册的课程的学分。程序中可使用包含.IF,.ELSEIF,.ENDIF等的多路分支结构（参见Regist.asm):

```assembly
.data
TRUE = 1
FALSE = 0
gradeAverage 	WORD 275		;测试值
credits			WORD 12			;测试值
OkToRegister 	BYTE ?
.code
	mov OkToRegister,FALSE
	.IF gradeAverage > 350
		mov OkToRegister,TRUE
	.ELSEIF (gradeAverage > 250) && (credits <= 16)
		mov OkToRegister,TRUE
	.ELSEIF(credits<=12)
		mov OkToRegister,TRUE
	.ENDIF
```

表6.8列出了汇编器生成的相应代码，读者可以在Microsoft Visual Studio调试器的反汇编窗口看到这些代码（这里已经对其进行了整理以便更加可读）。如果在汇编程序时使用了/Sg命令行选项，汇编器生成的代码就会出现在列表文件中。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230223/image.1j23hm0tu1uo.webp)

## 6.7.3 .REPEAT和.WHILE伪指令

除使用CMP和条件跳转指令编写自定义循环外，使用.REPEAT和.WHILE伪指令也可以编写循环。.REPEAT和.WHILE伪指令允许使用前面表6.7中列出的条件表达式。.REPEAT伪指令首先执行.REPEAT到.UNTIL之间的循环体，然后再测试.UNTIL之后的条件（条件为假执行循环体，条件为真则终止循环）:

```assembly
.REPEAT
	statements
.UNTIL condition
;WHILE伪指令首先测试条件（条件为真执行循环，条件为假则终止循环）,然后才执行循环体：
.WHILE condition
	statements
.ENDW
```

### 例子：

下面的语句使用.WHILE伪指令显示1~10之间的值：

```assembly
mov eax,0
.WHILE eax < 10
	inc eax
	call writeDec
	call crlf
.ENDW
```

下面的语句使用.REPEAT伪指令显示1~10之间的值：

```assembly
mov eax,0
.REPEAT
	inc eax
	call writeDec
	call Crlf
.UNTIL eax == 10
```

读者可以比较一下.REPEAT和.WHILE伪指令对条件处理方式的不同。

### 例子：

包含IF语句的循环
在本章前面的6.5.3节中，我们讲述了如何为内嵌的WHILE循环中的IF语句编写汇编代码
其伪码如下所示：

```c
while(op1<op2)
{
	op1++;
	if(op2==op3)
		X=2
	else
		X=3;
}
```

下面使用.WHILE和.IF伪指令实现这些伪码。由于op1,op2和op3都是变量，因此把它们
送入寄存器以避免在一条指令中同时出现两个内存操作数：

```assembly
.data
X 	DWORD 0
op1 DWORD 2		;测试数据
op2 DWORD 4		;测试数据
op3 DWORD 5		;测试数据
.code
	mov eax,op1
	mov ebx,op2
	mov ecx,op3
	.WHILE eax < ebx
	inc eax
	.IF ebx == ecx
		mov X,2
	.ELSE
		mov X,3
	.ENDIF
.ENDW
```

# 6.8 本章小结

由于AND,OR,XOR,NOT和TEST指令在数据位的层次上进行操作，因此它们称为位操
作指令（bitwise instruction)。这些指令中，源操作数的每个数据位与目的操作数对应位置的数据匹配进行操作：

- AND指令在两个数据位都是1的时候产生结果1。
- OR指令在两个数据位至少有一个是1的时候产生结果1。
- XOR指令仅在两个数据位不相同的时候产生结果1。
- TEST指令对目的操作数执行隐含的AND操作，并相应设置符号标志位，该指令不会改变目的操作数
- NOT指令对目的操作数的所有位取反。

CMP指令比较源操作数和目的操作数，它对源操作数和目的操作数执行隐含的减法操作，并相应设置CPU的状态标志。CMP后面通常跟一条条件跳转指令，根据输入条件的不同条件跳转指令有可能把控制权转移到跳转指令中的代码标号处。
本章中讲述了4种类型的条件跳转指令：

- 表6.2包含了基于特定状态标志值的跳转指令，如JC(设置进位标志则跳转）,JZ(为零则跳转）和JO(设置溢出标志则跳转）等。
- 表6.3包含了基于操作数是否相等的跳转指令，如JE(相等则跳转）,JNE(不相等则跳转）,JECXZ(ECX=0则跳转）。
- 表6.4包含了基于无符号整数比较结果的跳转指令，例如JA(大于则跳转）,JB(小于则跳转）和JAE(大于等于则跳转）等。
- 表6.5包含的是基于有符号数的比较结果的跳转指令，如JL(小于则跳转）,JG(大于则跳转）等。

LOOPZ(LOOPE)指令在零标志置位并且ECX大于0时重复执行循环。LOOPNZ(LOOPNE)指令在零标志置位并且ECX大于0时重复执行循环（在实地址模式下，LOOPZ和LOOPNZ指令使用CX寄存器）。
加密是对数据进行编码的过程，解密是对数据进行解码的过程。XOR指令可用来以每次一个字节的方式执行简单的加密和解密操作。
流程图是以可视化的方式表示程序逻辑的有效工具。以流程图为模型，可以非常容易地编写汇编语言代码。把标号附加到每个流程图符号上，并在汇编语言代码中使用同样的标号，这种做法是非常有好处的。
有限状态机（FSM)是验证包含可识别字符的字符串的有效工具，如有符号整数字符串。如果每个状态都以一个标号来表示，那么以汇编语言来实现有限状态机是非常容易的。
.IF,.ELSE,.ELSEIF和.ENDIF伪指令在运行时对表达式求值，极大地简化了汇编代码的编写，它们在编写复杂的复合布尔表达式时非常有用。读者还可以使用.WHILE和.REPEAT伪指令创建条件循环。