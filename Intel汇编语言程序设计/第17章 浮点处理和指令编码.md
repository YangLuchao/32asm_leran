[toc]

本章要点

- 浮点二进制表示
- 浮点单元
- Intel指令编码

# 17.1 浮点二进制表示

==浮点数由三部分构成：符号、尾数和指数。以数字$-1.23154×10^5$为例，其中`-`是符号，表示该浮点数是负数，1.23154是尾数，5是指数==。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.4z3ca03xfjs0.webp)

## 17.1.1 EEE二进制浮点数的表示

Intel 微处理器使用IEEE发布的 Standard 754-1985 for Binary Floating-Point Arithmetic中规定的三种浮点二进制存储格式，见表17.1①。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.6gc1hlkp6zs0.webp)

三种格式非常相似，因此这里重点讲述单精度格式（参见图17.1)。图中最高有效位（MSB)在左边：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.1bfdrj32y6ps.webp)

### 符号

==二进制浮点数的符号由一个符号位表示，如果该位为1表示负数，为0表示正数。浮点数0是正数。==

### 尾数

在形如$m×b$的表达式表示的浮点数中，m称为尾数，b是基数，e是指数。尾数部分m是一个十进制小数。==在第1章中讲述二进制、十进制和十六进制数制系统的时候介绍了加权进位表示法的概念，现在可以把同样的概念扩展到数字的小数部分。==例如十进制数123.154可表示如下表达式的和：
$$
123.154=(1\times10^2)+(2\times10^1)+(3\times^{-0})+(1\times10^{-1})+(5\times10^{-2})+(4\times10^{-3})
$$
==二进制浮点数的表示方法是类似的，只要用2为基数计算位权值即可==。例如浮点二进制值11.1011可表示为：
$$
11.1011=(1\times2^1)+(1\times2^0)+(1\times2^{-1})+(0\times2^{-2})+(1\times2^{-3})+(1\times2^{-4})
$$
==另一种表示小数点后面的值的方法是用以2的署为分母的分数之和表示，下例中和是11/16(或0.6875):==
$$
.1011=\frac{1}{2}+\frac{0}{4}+\frac{1}{8}+\frac{1}{16}=\frac{11}{16}
$$
得到分数表示的方法是相当直观的。二进制数1011实际上是十进制数11,可用1011(十进制数11)做分数的分子。假设e是二进制小数点后的有效数字的位数，那么分母就是2。在该例中，e=4,因此分母2=16。表17.2给出了另外一些二进制浮点表示转换成十进制分数的例子。表中的最后一项是23位规格化尾数所能存储的最小的分数。表17.3列出了另外一些例子，例子中分别给出了二进制浮点数及与其等价的分数和十进制数。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.723ip6xwzkc0.webp)

### 尾数的精度

连续实数的整体不可能以有限个数据位的浮点格式表示。例如，假设使用一种简化的格式表示浮点数，其中尾数占5个数据位，那么就没有办法表示二进制数1.1111和二进制数10.0000之间的实数了。因此表示二进制数1.11111就需要更高精度的尾数。类似的结论可以推广到IEEE双精度格式，==53位的尾数不能表示需要54或更多个数据位的二进制数字。==

## 17.1.2 指数

==单精度浮点数的指数是以8位无符号整数的格式存储的，实际存储的是指数值与127相加的和。==以数字1.101×$2^5$为例，指数(5)与127相加的和(十进制132,二进制10100010)存储在指数部分。表17.4给出了一些指数的例子，其中的第一列是有符号十进制整数，第二列是调整后的值，最后一列是调整后的整数对应的二进制值。调整后的指数部分总是正数(在1~254之间）,==实际指数范围是-126~+127。之所以选择这个范围，是为了避免最小可能的指数的倒数发生溢出(这是由于如果最小的指数选择了-127,那么-127+127=0)==。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.6ljws8kij8w0.webp)

## 17.1.3 二进制浮点数的正规化

==大多数浮点二进制数都是以正规化的格式存储的，以便能够使得尾数的精度最大化。对于任何给定的浮点二进制数，可通过移动小数点，使小数点前仅有一个数字`1`从而使其正规化，指数表示小数点向左（正数）或向右移动（负数）的位数==。下面是一些例子。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.1b2s1j7bwu80.webp)

### 非正规化值

二进制浮点数正规化操作的逆过程称为逆正规化，可通过移动二进制小数点直到指数为零。如果指数部分是正数n,那么向右移动小数点n位；如果指数部分是负数n,那么向左移动小数点n位。如果需要，开头的空位应以0填充。

## 17.1.4 IEEE 表示法

### 实数编码

==一旦符号位、指数和尾数域进行了正规化和编码，得到一个完整的二进制IEEE短实数就非常容易了。==参照图17.1,可以把符号位放在最前边，接下来是指数，最后是尾数中的小数部分。例如，二进制值1.101×$2^0$可表示如下：

- 符号位：0
- 指数：01111111
- 尾数：10100000000000000000000

调整后的指数01111111是十进制数127,所有正规化后的二进制尾数在小数点前面都有一个1，因此对该位就无需显式编码了。更多的例子如表17.5所示。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.67titp02dbk0.webp)

IEEE规范中还规定了几类实数和非实数的编码：

- 正数0和负数0
- 反向正规化的有限数
- 正规化的有限数
- 正无穷和负无穷
- 非数字值(NaN)
- 不确定数

Intel浮点单元使用不确定数响应一些无效的浮点操作。

### 正规化和反向正规化

==正规化的有限数是0到无穷大之间的所有可以编码为正规化实数的非0有限值。==尽管乍看起来所有的非零浮点数都可以正规化，不过事实上在值非常接近于0时这是不可能的。由于指数范围的限制，FPU有可能不能把二进制的小数点移动到正规化的位置。假设FPU的计算结果是1.0101111×$2^{129}$，其指数太小无法在单精度实数中存储，这时将产生一个下溢异常，可通过向左移动小数点（每次一位）使其正规化，直到指数落在有效的范围内为止：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.52c4aow5qxo0.webp)

在这个例子中，由于移动了小数点，精度会有一些损失。

### 正无穷和负无穷

==正无穷(+00)表示最大的正实数，负无穷(-00)表示最小的负实数。可以比较无穷值和其他值：-00小于+00，-00小于任何有限数，+00大于任何有限数。正无穷和负无穷都能用来表示溢出。==

### 非数字(NaN)

==NaN是不表示任何有效实数的位序列。==IA-32体系结构包含两种类型的`NaN，quiet NaN`可通过大多数算术运算而不会导致任何异常；`signalling NaN`可用于产生一个无效操作异常。编译器可以用`Signalling NaN`值填充未初始化的数组，对该数组进行任何计算的企图都会引发一个异常。`quiet NaN`可用于存放调试会话产生的诊断信息。浮点单元不会试图对`NaN`执行操作。IA-32手册详细描述了一套规则，用于确定以这两种类型的NaN组合作为操作数时指令执行的结果。

### 特殊值的编码

表17.6列出了浮点操作中经常遇到的几个特殊值的编码。标记位的位置既可以是0，也可以是1。==QNaN表示quietNaN，SNaN表示signallingNaN==。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.2ayq3t2i764g.webp)

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.6lv4vpc8qao0.webp)

## 17.1.5 把十进制分数转换为二进制实数

如果十进制分数可以很容易地表示为$\frac{1}{2}+\frac{1}{4}+\frac{1}{8}+...$的格式，那么就很容易得到其对应的二进制实数。表17.7中的左边一列中的大部分分数一眼看去都不容易转换成二进制数，不过如果写成第二列的格式就容易得多了。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.3tjrrle9is80.webp)

很多实数，如1/10(0.1)或1/100(.01),不能用有限个二进制数字位表示，这样的分数只能近似表示为若干以2的寡为分母的分数之和。想想货币值如$39.95会受什么影响吧！

### 另一种方法：使用二进制长除法。

把十进制分数转换成二进制数时如果涉及到的十进制值比较小，可以使用一种很方便的方法：首先把分子和分母都转换成二进制值，然后再进行除法操作。例如，十进制值0.5可表示成分数5/10,十进制5是二进制值0101,十进制值10是二进制值1010,执行下面的除法，我们发现商是二进制值0.1:

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.2sxqcuc4bnk0.webp)

在被除数减去1010后余数为0时，除法终止。因此十进制分数5/10等于二进制值0.1,我们称上面这种方法为二进制长除法。
**例子：以二进制数表示0.2**。下面把十进制的0.2(2/10)转换成二进制值，使用长除法。首先，把二进制值10除以1010(十进制值10):

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.6y7p7wzybw40.webp)

第一个足够大的余数是10000,在除以1010之后，余数是110,在末尾添加一个0,新的被除数是1100,除以1010后，余数是10,再添加3个0,新的被除数是10000,这等于进行第一次除法操作时被除数的值。从这点开始，商中重复出现0011,因此我们得知准确的商是无法得到的，0.2不能用有限个二进制位表示。0.2的单精度编码的尾数部分是00110011001100110011001。

### ==把单精度值转换成十进制数==

下面是在把IEEE单精度值转换成十进制数时的推荐步骤：

1. ==如果MSB(最高有效位)是1，该值为负数，否则为正数==
2. ==接下来的8位表示指数，减去二进制值0111111(十进制值127)，得到未调整的值数值，把未调整的指数值转换成十进制数==。
3. ==接下来的23位表示尾数，注意尾数小数点前面有一个被省略的`1.`，尾数尾部的0可以忽略==。这时就可以使用第1步中得到的符号位、第二步中得到的指数以及本步骤中得到的尾数创建一个浮点二进制数了。
4. ==对第三步得到的浮点数进行逆规格化操作（根据指数相应地移动小数点，指数为正则右移，指数为负则左移）==
5. ==从左到右，使用位权表示方法得到浮点数的2的次幕之和的表示，进而得到相应的进制数。==

#### 例子

==把IEEE浮点数(0 10000010 01011000000000000000000)转换成十进制数==

1. ==该数是正数。==
2. ==未调整的指数值是00000011,也就是十进制数3。==
3. ==组合符号位、指数和尾数得到二进制数+1.01011×2$^3$。==
4. ==逆正规化后的二进制数是+1010.11。==
5. ==对应的十进制值是$+10 \frac{3}{4}$或+10.75==

# 17.2 浮点单元

Intel 8086处理器是为处理整数运算而设计的，不过，事实证明这对于大量使用浮点运算的图形处理程序和运算密集型的程序是个严重的限制。尽管可以采用纯软件仿真的方法模拟浮点运算，但运算性能会严重下降，例如像AutoCAD(Autodesk公司出品）这样的软件就需要更加强大的浮点运算支持。==Intel曾经销售过一种名为8087的浮点协处理器芯片，并随处理器的升级换代而升级。从Intel486处理器开始，浮点处理硬件集成进了主CPU,称为浮点单元(FPU,Floating Point Unit)==。

## 17.2.1 浮点寄存器栈

==FPU不使用通用寄存器(EAX,EBX等）,FPU有自己的一套寄存器，称为寄存器栈。FPU从内存中把值加载到寄存器栈，执行计算，然后再把栈上的值存储到内存中。==FPU指令以后缀格式计算数学表达式，这和Hewlett-Packard计算器差不多。例如，中缀格式的表达式：`(5*6)+4`的后缀格式是：

```c
5 6 * 4 +
```

中缀表达式`(A+B)\*C`使用圆括号覆盖了默认的优先级规则（默认是先乘法后加法）,但其等价的后缀格式不需要圆括号：

```
A B + C *
```

### 表达式栈

在对后缀表达式求值的时候可使用堆栈存放中间值。图17.2显示了对表达式`56*4-`求值所需的步骤，堆栈项以ST(0)和ST(1)标识，ST(0)通常表示堆栈指针指向的位置（栈顶）。
把中缀表达式转换成后缀表达式的常用方法在计算机科学的入门教科书以及在因特网上都有很多很好的介绍，这里就不再重复了。表17.8给出了一些中缀表达式和后缀表达式的例子。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.5s29s8d4ryo0.webp)

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.6umt7a0kaeo0.webp)

### 浮点数据寄存器

==FPU有8个可独立寻址的80位寄存器，分别名为R0,R1,…，R7(如图17.3所示）,它们以堆栈形式组织在一起，统称为寄存器栈。栈顶由FPU状态字中的一个名为TOP的域（占三个二进制位）来标识，对寄存器的引用都是相对于栈顶而言的。==例如在图17.3中，TOP等于二进制值011,说明R3是栈顶。在编写浮点指令时栈顶也写为ST(0)(或ST),最后一个寄存器（栈底）写为ST(7)。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.13i0rcvu8bo.webp)

==跟预期的一致，压栈(push)操作(也称为加载操作)把指示栈顶的TOP域值减1并把结果复制到由ST(0)标识的寄存器中，如果在压栈操作之前TOP等于0，压栈操作使TOP回滚指示寄存器R7。出栈(pop)操作(也称为存储操作)把ST(0)中的数据复制到一个操作数中并把TOP域的值增1。如果在出栈之前TOP等于7,出栈操作使TOP回滚指示寄存器R0。加载一个值至浮点栈中时如果会覆盖已存在的数据，就会产生一个浮点异常。==图17.4显示了同一浮点栈在1.0和2.0压栈后的情况。
==浮点寄存器中的值以10字节的IEEE扩展实数格式(也称为临时实数格式)存储，FPU在内存中保存算术运算的结果时，自动把结果转换为以下格式之一：整数、长整数、单精度（短实数）、双精度（长实数）或压缩的二进制编码的十进制整数。==

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.15ddsfhxjg74.webp)

### 特殊用途寄存器

FPU有6个特殊用途的寄存器（如图17.5所示）。

- ==一个10位的操作码寄存器：存储最后一条执行的非控制指令==。
- ==一个16位的控制寄存器：在执行计算时控制FPU的精度和使用的近似方法==。
- ==一个16位的状态寄存器：存放着栈顶指针、条件码以及关于异常的警告==。
- ==一个16位的标记字寄存器：指示FPU数据寄存器栈中每个寄存器的内容的状态，每个寄存器使用两位指示寄存器是否包含一个有效的数字、零、特殊值（NaN、无穷数、反规格化或不支持的格式）或是否为空==。
- ==一个48位的最后指令指针寄存器：存放最后执行的非控制指令的指针==。
- ==一个48位的最后数据（操作数）指针寄存器：存放最后执行的指令使用的数据操作数（如果有的话）==。

操作系统在任务切换时使用这些特殊用途的寄存器保存浮点单元的状态信息。我们在第2章讲述CPU如何执行多任务的时候提到过任务状态的保存。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.2xj1wxurn600.webp)

## 17.2.2 近似

==FPU在进行浮点计算时试图产生准确的结果，不过在许多情况下这是不可能的，因为目的操作数根本就不能准确表示计算的结果==。例如，假设某种存储格式只允许3个小数位，这种格式就只能存储1.011或1.101,但不能存储1.0101。如果计算产生的精确结果是+1.0111(十进制数1.4375),我们就必须通过加.0001或减去.0001向上或向下近似：

```
(a) 1.0111 --> 1.100
(b) 1.0111 --> 1.011
```

如果精确结果是负数，那么加-0001会使近似值趋向-00,减去-0001会使近似值趋向0和+00FPU允许选择下面4种近似方法。

- ==近似到最接近的偶数==：近似结果最接近准确结果，如果有两个值与精确结果近似度相同，则选取最接近的偶数（最低有效位是0)。
- ==向下近似趋向于-00==:近似结果小于或等于精确结果。
- ==向上近似趋向于+00==:近似结果大于或等于精确结果。
- ==近似趋向于0==:也称为剪裁，近似结果的绝对值小于或等于精确结果。

### FPU控制字

==FPU的状态字中包含了一个名为RC的域，包含两个数据位，该域指定使用何种近似方法==。该域取值如下：

- 二进制值00:近似到最近的偶数（默认）。
- 二进制值01:向下近似趋向于-00。
- 二进制值10:向上近似趋向于+00。
- 二进制值11:近似趋向于0(剪裁）。

==近似到最接近的偶数是默认的，被认为是最准确的，适合大多数应用程序==。表17.9给出了二进制值+1.0111如何应用4种近似方法的例子。类似地，表17.10给出了二进制值-1.0111可能的近似值

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.4gwgg7wzl940.webp)

## 17.2.3 浮点异常

每个程序都有可能出错，FPU必须能够处理错误。FPU能够识别和检查6种类型的异常：无效操作（#I)、除零（#乙）、反规格化操作数（#D)、数值溢出（#O)、数值下溢（#U)、无效精度(#P)前三种异常（#I,#Z,#D)是在算术操作发生之前检查的，后三种异常（#O,#U,#P)是在算术操作发生之后检查的。
每种异常类型都有相应的标志位和掩码，检测到浮点异常时，处理器设置相应的标志位。对于标识出来的每种异常，根据其掩码位的不同，处理器可采取两种不同的动作：

- 如果相应的掩码位置位，处理器自动处理异常并允许程序继续。
- 如果相应的掩码位清零，处理器调用软件异常处理程序。

处理器的掩码响应方式对大多数程序而言都是可接受的。定制的异常处理程序可在应用程序要响应特定异常的情况下使用。单条指令可引发多个异常，因此处理器保留自上次异常清除后发生的异常记录，因此在一系列计算完成之后，可以检查是否发生了异常。

## 17.2.4 浮点指令集

浮点指令集有点复杂，因此本节尽量给读者一个关于其功能的大致印象，可能会给出一些例子用以说明编译器通常会生成的代码是个什么样子。除此之外，本节还会讲述如何修改近似模式控制FPU。浮点指令集包含下面几类指令：

- 数据传送指令
- 基本的算术运算指令
- 比较指令
- 超越指令
- 常量加载指令（特殊的预定义常量）
- X87FPU控制指令
- x87FPU和SIMD状态管理指令

==浮点指令总是以字母F开头，以便与CPU指令区别开。指令的第二个字母（通常是B或I)说明了内存操作数应如何解释：B表示二/十进制（BCD)操作数，I表示二进制整数操作数。如果未指定B或I,就表示操作数是实数格式的==。例如FBLD对BCD数进行操作，FILD对整数进行操作，FLD对实数进行操作。
附录B中包含了IA-32浮点指令的参考。

#### 操作数

==浮点指令最多可以有两个操作数，也可以无操作数或只有一个操作数。如果有两个操作数，其中的一个必须是浮点寄存器==。没有立即数操作数，但可加载一些预定义的值（如0.0,开以及1b10等）。通用寄存器EAX,EBX,ECX,EDX等不能作为操作数，不允许内存到内存的操作。
整数操作数必须从内存加载至FPU(绝不能从CPU寄存器中加载）,FPU把整数自动转换成浮点数的格式。类似地，在内存中存储浮点值的时候，浮点值自动剪裁或近似取整。

### 初始化(FINIT)

FINIT指令初始化浮点单元，把FPU的控制字设为037Fh，掩盖所有的浮点异常，把近似方法设置为最接近的偶数，并把计算精度设为64位。强烈建议在程序的开始调用FINIT,以使FPU处于一个固定的初始状态。

### 浮点数据类型

下面快速回顾一下MASM支持的浮点数据类型（QWORD,TBYTE,REAL4,REAL8,
REAL10),如表17.11所示。在定义FPU指令使用的数据类型时，会用到这些数据类型。例如，在加载一个浮点变量至FPU堆栈时，变量可定义为REALA,REAL8,REAL10:

```assembly
.data
	bigVal REAL10 1.212342342234234243E+864
.code
	fld bigval	;加载变量至浮点栈
```

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.8ar86dfotp0.webp)

### 加载浮点值(FLD)

==FLD(加载浮点值）指令复制一个浮点数至FPU的栈顶[即ST(O)]，操作数可以是32位、64位或80位的内存操作数(REAL4,REAL8,REAL10)或另外一个浮点寄存==器：

```assembly
FLD m32fp
FLD m64fp
FLD m80fp
FLD ST(i)
```

内存操作数的类型：FLD支持的内存操作数的类型和MOV是一样的。下面是一些例子：

```assembly
.data
	array REAL8 10 DUP(?)
.code
	f1d 	array			; direct
	f1d 	[array+16]		; direct-offset
	fld 	REAL8 PTR[esi]	;indrect
	f1d 	array[esi]		;indexed
	fld 	array[esi*8]	; indexed , scaled
	fld 	array[esi*TYPE array]	; indexed , scaled
	f1d 	REAL8 PTR[ebx+esi]		; base-index
	fld 	array[ebx+esi]		; base-index-displacement
	fld 	array[ebx+esi*TYPE array] ; base-index-displacement, scaled
```

例子：下面的例子加载两个直接操作数至FPU堆栈：

```assembly
.data
	dblOne REAL8 234.56
	dblTwo REAL8 10.1
.code
	f1d dblOne	;ST(0)=db10ne
	f1d dbLTwo	;ST(0)=db]Two,ST(1)=db10ne
```

下图显示了在每条指令执行后堆栈的内容：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.2j4fyhw8hq60.webp)

在第二条FLD执行的时候，TOP域减1,导致原来标记为ST(0)的元素变成了ST(1)。

### FILD

==FILD指令把16位、32位、64位的整数源操作数转换成双精度浮点数并把其加载到ST(0),源操作数的符号位保留==，在17.2.10节（混合模式算术运算）将详细解释该指令。==FILD支持的内存操作数类型（间接操作数、变址操作数、基址变址操作数等）同MOV指令。==

#### 加载常量

下面的指令在堆栈上加载特定的常量，这些指令无操作数：

- ==FLD1指令在寄存器堆栈上压入1.0。==
- ==FLDL2T指令在寄存器堆栈上压入1b10。==
- ==FLDL2E指令在寄存器堆栈上压入1be。==
- ==FLDPI指令在寄存器堆栈上压人TT。==
- ==FLDLG2指令在寄存器堆栈上压入1g2。==
- ==FLDLN2指令在寄存器堆栈上压入1n2。==
- ==FLDZ指令在寄存器堆栈上压入0.0。==

### 存储浮点值(FST,FSTP)

==FST指令（存储浮点值）复制FPU的栈顶的操作数至内存中，操作数可以是32位、64位或80位的内存操作数（REAL4,REAL8,REAL10)或另外一个浮点寄存器==：

```assembly
FST m32fp
FST m64fp
FST ST(i)
```

FST不会弹出栈顶元素，下面的指令把ST(0)存储到内存中，假设ST(O)等于10.1并且ST(1)等于234.56:

```assembly
fst dblThree	;10.1
fst db1Four		;10.1
```

单凭直观感觉，我们可能会期望dblFour等于234.56,不过由于第一条FST指令把10.1留在ST(0)中，因此结果正好相反。如果想把ST(1)复制到dblFour中，第一条指令就必须用FSTP。

#### FSTP

==FSTP(存储浮点值并出栈）复制ST(0)至内存中并弹出ST(0)==，假设在执行下面的指令之前ST(0)等于10.1并且ST(1)等于234.56

```assembly
fstp dblThree	;10.1
fstp dblFour	;234.56
```

在执行之后，从逻辑上来讲这两个值已经从堆栈上移除了。从物理上来讲，每次FSTP指令执行后，TOP指针增1,改变了ST(0)的位置。
==FIST(存储整数）指令把ST(0)中的值转换成有符号整数并把结果存储到目的操作数中，值可以存储在字或双字中。==17.2.10节（混合模式算术运算）将讲述其详细用法。FIST支持的内存操作数格式同FST。

## 17.2.5 算术运算指令

基本的算术运算指令如表17.12所示，==算术运算指令支持的内存操作数类型同FLD(加载）和FST(存储）,因此操作数类型可以是间接操作数、变址操作数、基址变址操作数等。==

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.67mq8ndvwt00.webp)

### FCHS和FABS

==FCHS(改变符号）指令把ST(0)中的值的符号变反，FABS(绝对值）指令取ST(0)中值的绝对值，这两条指令都不需要操作数==：

```assembly
FCHS
FABS
```

### FADD,FADDP,FIADD

FADD(加法）指令的格式如下，其中m32fp是一个REAL4类型的内存操作数，m64fp是一个REAL8类型的操作数，i是寄存器号：

```assembly
FADD
FADD m32fp
FADD m64fp
FADD ST(0), ST(i)
FADD ST(i), ST(0)
```

#### 无操作数

==如果FADD不带操作数，那么ST(0)和ST(1)相加，结果临时存储在ST(1)中，然后ST(0)弹出堆栈，最终结果存储在栈顶。==下图解释说明了无操作数的FADD指令执行的情况，假设堆栈中已经包含了两个变量。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.xovt3v5xwj.webp)

### 寄存器操作数

假设浮点栈的内容和上例相同，下图解释了ST(0)和ST(1)相加的过程。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.81lkia19l24.webp)

### 内存操作数

FADD在使用内存操作数时，把操作数和ST(0)相加，下面是一些例子：

```assembly
fadd mySingle		;ST(0)+=mySingle
fadd REAL8 PTR[esi]	;ST(0)+=[esi]
```

### FADDP

==FADDP指令（相加并出栈）指令在执行完加法后从堆栈上弹出ST(0)==,其格式如下：

```assembly
FADDP ST(i),ST(0)
```

下图解释了FADDP是如何工作的。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.780lftpj5sg0.webp)

### FIADD

==FIADD(与整数相加）指令把源操作数转换成扩展精度浮点数格式，然后再和ST(0)相加。==其格式如下：

```assembly
FIADD m16int
FIADD m32int
```

例子：

```assembly
.data
	myInteger DWORD 1
.code
	fiadd myInteger		;ST(0)+=myInteger
```

### FSUB,FSUBP,FISUB

==FSUB指令从目的操作数中减去源操作数，把差存储在目的操作数中。目的应总是一个FPU寄存器，源可以是FPU寄存器或内存操作数，其操作数格式同FADD:==

```assembly
FSUB m32fp
FSUB ST(0), ST(i)
FSUB ST(i),ST(0)
```

==除了执行的是减法操作而不是加法操作之外，FSUB的操作和FADD很相似。==例如，无操作数的FSUB从ST(1)中减去ST(0),结果临时存储在ST(1)中，然后从堆栈中弹出ST(0),这样最后的结果就保存在栈顶了。FSUB在使用内存操作数时从ST(0)中减去内存操作数，不会弹出栈顶元素：

```assembly
fsub mySingle		;ST(0)-=mySingle
fsub array[edi*8]	;ST(0)-=array[edi*8]
```

### FSUBP

==FSUBP(相减并出栈）指令在执行完减法后从堆栈中弹出ST(O)。==MASM支持下面的格式：

```assembly
FSUBP ST(i),ST(0)
```

### FISUB

==FISUB(减整数）指令把源操作数转换成扩展精度的浮点数格式，然后再从ST(0)中减去源操作数==：

```assembly
FISUB m16int
FISUB m32int
```

### FMUL,FMULP,FIMUL

==FMUL指令把源操作数和目的操作数相乘，结果存储在目的操作数中。目的应总是FPU寄存器，源可以是寄存器或内存操作数==。其格式同FADD和FSUB是一样的：

```assembly
FMUL
FMUL m32fp
FMUL m64fp
FMUL ST(0),ST(i)
FMUL ST(i),ST(0)
```

除执行的操作是乘法而不是加法之外，FMUL的操作和FADD非常相似。例如，无操作数的FMUL把ST(1)和ST(0)相乘，积临时存储在ST(1)中，然后从堆栈中弹出ST(0),这样最后的结果就保存在栈顶了。FMUL在使用内存操作数时把ST(0)和内存操作数相乘，不会弹出栈顶元素：

```assembly
fmul mySingle	;ST(0)*=mySingle
```

### FMULP

==FMULP(相乘并出栈)指令在执行完乘法后从堆栈中弹出ST(0)。==MASM支持下面的格式：

```assembly
FMULP ST(i),ST(0)
```

FIMUL与FIADD的格式基本相同，不过执行的操作是乘法而非加法：

```assembly
FIMUL m16int
FIMUL m32int
```

### FDIV,FDIVP,FIDIV

FDIV指令把源操作数和目的操作数相除，结果存储在目的操作数中。目的应总是FPU寄存器，源可以是寄存器或内存操作数。其格式同FADD和FSUB是一样的：

```assembly
FDIV
FDIV m32fp
FDIV m64fp
FDIV ST(0),ST(i)
FDIV ST(i),ST(0)
```

除执行的操作是除法而不是加法之外，FDIV的操作和FADD非常相似。例如，无操作数的FDIV把ST(1)和ST(0)相除，商临时存储在ST(1)中，然后从堆栈中弹出ST(0),这样最后的结就保存在栈顶了。FDIV在使用内存操作数时把ST(0)和内存操作数相除，不会弹出栈顶元素。下面的代码中dblOne除以dblTwo,商存储在dblQuot中：

```assembly
.data
	dblOne REAL8 1234.56
	dbLTWO REAL8 10.0
	dbLQuot REAL8 ?
.code
	f1d 	dblOne		;加载至ST(0)
	fdiv 	dbLTwo		;ST(0)除以ab1Two
	fstp 	dbLQuot		;把ST(0)存储到db1Quot中
```

如果源操作数是0,就会产生一个除零异常。有很多特殊情况，如正无穷、负无穷、正数0、负数0、NaN作为被除数时，其细节请参阅1A-32指令集参考手册。

### FIDIV

FIDIV指令把整数源操作数转换成扩展精度的浮点数格式，然后再把ST(0)和源操作数相除，格式如下：

```assembly
FIDIV m16int
FIDIV m32int
```

## 17.2.6 浮点值的比较

浮点值的比较不能使用CMP指令（执行比较时使用整数减法操作）,应该使用FCOM指令。

在执行完FCOM指令之后、使用条件跳转指令（JA,JB,JE等）之前还要执行一些必需的指令。

### FCOM,FCOMP,FCOMPP

==FCOM指令（比较浮点值）比较ST(0)和源操作数，源操作数可以是内存操作数或FPU寄存器，==其格式如下所示。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.67s575wbnmg0.webp)

==FCOMP的操作数格式和FCOM相同，对于每种类型的操作数，FCOMP执行的动作和FCOM基本相同，不过最后还要从堆栈上弹出ST(0)。FCOMPP和FCOMP基本相同，最后还要再一次从堆栈上弹出ST(0)。==

#### 条件码

==C3,C2,C0这三个FPU条件码标志说明了浮点值比较的结果==（如表17.13所示）,
表格的标题栏中列出了各个浮点标志对应的CPU状态标志，这是因为C3,C2,C0分别与零标志、奇偶标志和进位标志在功能上类似。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.2bhts0rvgcu8.webp)

在比较了两个值并设置了FPU条件码之后，主要的挑战在于找到一种方法以根据条件码分支跳转到目的标号处，这涉及到两个步骤：

- 使用FNSTSW指令把FPU状态字送AX。
- 使用SAHF指令把AH复制到EFLAGS寄存器中。

==一旦条件码复制到ELFAGS寄存器之后，就可以使用基于零标志、奇偶标志和进位标志的跳转指令了==。表17.13列出了对于每组条件码的组合可使用的合适的条件跳转指令。对于其他的条件码的组合，还可以使用其他的条件跳转指令，如JAE指令在CF=0时跳转；JBE在CF=1或ZF=1时跳转；JNE在ZF=0时跳转。
例子：假设有下面的C++代码：

```c++
double X=1.2;
double Y=3.0;
int N=0;
if(X<Y)
	N=1;
```

下面是等价的汇编语言代码：

```assembly
.data
X REAL8 1.2
Y REAL8 3.0
N DWORD 0
.code
;if(X<Y)
;N=1
	fld 	x		;ST(0)=X
	fcomp 	Y		; compare ST(0) to Y
	fnstsw 	ax		; move status word into AX
	sahf			; copy AH into EFLACS
	jnb 	L1		; X not < Y? skip
	mov 	N,1		;N=1
L1:
```

#### P6的改进

对于前面的例子，有一点值得注意：==浮点数比较比整数比较运行时开销更大，因此Intel的P6系列处理器引入了FCOMI指令，该指令比较两个浮点值并直接设置零标志、奇偶标志和进位标志（P6系列处理器始于奔腾Pro和奔腾Ⅱ处理器）==。FCOMI的格式如下：

```assembly
FCOMI ST(0),ST(i)
```

下面使用FCOMI指令重写前面的例子代码（比较X和Y):

```assembly
.code
;if(X<Y)
;N=1
	fld 	Y		;ST(0)=Y
	f1d 	X		;ST(0)=X,ST(1)=Y
	fcomi 	ST(0),ST(1)	; compare ST(0) to ST(1)
	jnb 	L1		;ST(0)not<ST(1)?skip
	mov 	N,1		;N=1
L1:
```

FCOMI指令替代了前面例子中的三条指令，不过需要一条额外的FLD指令。FCOMI指令不接受内存操作数。

### 比较是否相等

几乎所有的程序入门设计教材都会警告读者不要去比较浮点值是否相等，这是由于计算过程中的近似可能导致错误。这个问题可通过计算下面的表达式说明：`(sqrt(2.0)*sqrt(2.0))-2.0`。从数学上讲，这个表达式的结果应该是0,但实际的结果却不是（大约为4.4408921E-016)。表17.14使用下面的数据定义展示了每步之后的FPU堆栈：

```assembly
vall REAL8 2.0
```

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.7h7mqghouts0.webp)

比较浮点值x和y是否相等的正确做法是取其差值的绝对值(Lx-yl)并和用户自定义的一个小的正数相比较。下面是实现类似功能的汇编语言代码，其中使用了一个小正数作为在认为这两个值相等时其差值的临界值：

```assembly
.data
	epsilon REAL8 1.0E-12
	val2 	REAL8 0.0		; value to compare
	va13	 REAL8 1.001E-13	; considered equal to val2
.code
;if(va12==va13),display"Values are equal".
	fld 	epsilon
	fld 	val2
	fsub 	val3
	fabs
	fcomi 	ST(0),ST(1)
	ja 		skip
	mwrite 	<"Values are equal", Odh, Oah>
skip:
```

表17.15跟踪了程序指令的执行过程，显示了每条指令执行后浮点栈的情况。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.1fk5t99lfk74.webp)

如果重新定义val3,使其大于临界值，则val3和val2将不再相等：

```assembly
va13 REAL8 1.001E-12		; not equal
```

## 17.2.7 读写浮点值

本书附带的链接库中包含了两个处理浮点数输入输出的过程，它们是由San Jose State University 的 William Barrett 编写的

- ReadFloat:从键盘读入一个浮点值并把它压入浮点栈。
- WriteFloat:在控制台窗口上以指数格式显示ST(0)的值。

ReadFloat接收多种格式的浮点数。下面是一些例子：

```c
35
+35.
-3.5
35
3.5E5
3.5E005
-3.5E+5
3.5E-4
+3.5E-4
```

### ShowFPUStack

显示浮点堆栈的内容，这是由Pacific Lutheran University的James Brink编写的。调用该过程时无需参数：

```assembly
call ShowFPUStack
```

#### 例子程序

下面的例子程序在FPU堆栈上压人两个浮点值，然后显示，接下来读人两个用户输入的值，相乘并显示其乘积：

```assembly
TITLE 32-bit Floating-Point I/0 Test (floatTest32. asm)
INCLUDE Irvine32.inc
INCLUDE macros.inc
.data
	first 	REAL8 123.456
	second 	REAL8 10.0
	third 	REAL8 ?
.code
main PROC
	finit		;初始化FPU
; Push two floats and display the FPU stack.
	fld first
	fld second
	call ShowFPUStack
;输入两个浮点数并显示其乘积
	mwrite 	"Please enter a real number:"
	call 	ReadFloat
	mwrite	"Please enter a real number:"
	call 	ReadFloat
	fmul 	ST(0),ST(1)		;相乘
	mwrite	"Their product is:"
	call 	WriteFloat
	call 	Crlf
	exit
	main ENDP
END main
```

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.1di470yi4wzk.webp)

## 17.2.8 异常的同步

==CPU和FPU分别是独立的单元，因此浮点指令可以和整数及系统指令同时执行，这称为并行（concurrency)。并行执行浮点指令在发生未屏蔽的异常时可能会导致问题，屏蔽的异常不会导致问题，因为FPU总是会执行完当前的操作并存储结果。==
未屏蔽的异常发生时，当前执行的浮点指令被中断，FPU产生异常事件信号。下一条浮点指令或FWAIT(WAIT)指令要执行时，FPU检查是否有未决异常，如果有，则调用浮点异常处理程序（一个子过程）。
如果产生异常的浮点指令后跟的是一条整数指令或系统指令又会怎样呢？遗憾的是，这些指令不会检查未决异常——它们将立即执行。假设第一条浮点指令要把其输出存储到一个内存操作数中，第二条整数指令修改该内存操作数，如果第一条指令发生了异常，那么异常处理程序就不能适时执行了，这通常会导致错误的结果。下面是一个例子：

```assembly
.data
	intVal DWORD 25
.code
	fild intVal		;存储ST(0)至intVa1
	inc intVal		;整数值增1
```

==WAIT和FWAIT指令正是用来强制处理器在执行下一条指令之前检查未决的未屏蔽浮点异常的，这两条指令都能解决这里潜在的同步问题。==在下例中，直到异常处理程序执行完后INC令才能执行：

```assembly
fild intVal		;存储ST(0)至intVal
fwait			;等待未决异常
inc intVal		;整数值增1
```

## 17.2.9 代码示例

本节看几个演示浮点算术运算指令的例子。一种好的学习方法是使用C++编写表达式，然后编译，再查看编译器生成的代码。

### 表达式

下面编码实现表达式`valD=-valA+(valB*valC)`。按部就班的方法是：加载valA至浮点栈并求反，加载valB至ST(0),这时-valA保存在ST(1)中，valC和ST(0)相乘，乘积保存在ST(0)中，ST(0)和ST(1)相加并把和存储在valD中。实现代码如下：

```assembly
.data
	vala REAL8 1.5
	valB REAL8 2.5
	valc REAL8 3.0
	valD REAL8 ?; +6.0
.code
	fld 	vala		;ST(0)=va1A
	fchs				;改变ST(0)中值的符号
	f1d 	valB		;加载va1B至ST(0)
	fmul 	valc		;ST(0)*=valc
	fadd				;ST(0)+=ST(1)
	fstp 	valD		;存储ST(0)至valD中
```

### 数组之和

下面的代码计算一个双精度实数数组之和并显示：

```assembly
ARRAY_SIZE = 20
.data
	sngArray REAL8 ARRAY_SIZE DUP(?)
.code
	mov esi,0		;数组的索引
	f1dz			;在浮点栈上压入0.0
	mov ecx, ARRAY_SIZE
L1:
	fld sngArray[esi]	;加载内存操作数至ST(0)
	fadd				;ST(0)和ST(1)相加后ST(0)出栈
	add esi,TYPE REAL8	;下一个数组元素
	loop L1
	call WriteFloat		;显示ST(0)中的和
```

### 平方根之和

FSQRT指令计算ST(0)的平方根并把结果存储在ST(0)中，下面的代码计算了两个平方根之和：

```assembly
.data
	vala REAL8 25.0
	valB REAL8 36.0
.code
	f1d valA		; push vala
	fsqrt			;ST(0)=sqrt(va1A)
	f1d valB		; push valB
	fsqrt			;ST(0)=sqrt(va1B)
	fadd			; add ST(0), ST(1)
```

### 数组的点积

下面的代码计算表达式`(array[0]*array[11])+(array[2]*array[3])`,这种计算有时也称为点积(dot product)。表17.16显示了每条指令执行后FPU栈的内容。下面是输入数据：

```assembly
.data
	array REAL4 6.0,2.0,4.5,3.2
```

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.7dg57zr9q8g0.webp)

## 17.2.10 混合模式算术运算

到现在为止，涉及到算术运算只包含实数，应用程序经常涉及到混合算术运算：同时包括整数和实数的运算。ADD和MUL等整数算术运算指令不能处理实数，因此惟一的选择是使用浮点指令。Intel指令集中提供了提升整数至实数的指令以及把值加载至浮点栈的指令。

#### 例子

下面的C++代码把一个整数和一个双精度数相加，其和存储在一个双精度数中。在执行加法之前C++自动把整数提升到实数：

```c++
int N = 20;
double X = 3.5;
double Z = N + X;
```

下面是等价的汇编语言代码：

```assembly
.data
	N SDWORD 20
	X REAL8 3.5
	Z REAL8 ?
.code
	fild N		;加载整数至ST(0)中
	fadd X		;内存操作数和ST(0)相加
	fstp Z		;存储ST(0)至内存操作数中
```

#### 例子

下面的C++程序把N提升成双精度数，然后计算实数表达式的值，最后再把结果存储到一个整数变量中：

```assembly
int 	N = 20;
double 	X = 3.5;
int 	Z = (int)(N+X);
```

Visual C++生成的代码在Z中存储剪裁的结果之前调用了一个转换函数ftol。如果以汇编语言编写实现该表达式的代码，就可以使用FIST替代函数ftol,乙向上近似（默认）到24:

```assembly
fild N		;加载整数至ST(0)中
fadd X		;内存操作数和ST(0)相加
fist Z		;存储ST(0)至内存整数操作数中
```

### 改变近似模式

FPU控制字的RC域允许指定近似的类型。可使用FSTCW把控制字存储到一个变量中，修改RC域（位10和位11),然后再使用FLDCW指令把变量加载回控制字中：

```assembly
fstcw 	ctrlword				;存储控制字
or 		ctrlword,110000000000b	;设置RC=剪裁方式
fldcw 	ctrlword				;加载控制字
```

对于前面的例子，如果使用剪裁的近似方法执行计算，得到的结果是Z=23:

```assembly
fild N		;加载整数至ST(0)中
fadd X		;内存操作数和ST(0)相加
fist Z		;存储ST(0)至内存整数操作数中
```

此外，还可以重置近似模式至默认模式（近似到最近的偶数）:

```assembly
fstcw 	ctrlword		;存储控制字
and 	ctrlword,001111111111b	;重置近似模式至默认
fldcw 	ctr7Word		;加载控制字
```

## 17.2.11 屏蔽和未屏蔽的异常

浮点异常默认是屏蔽的（参见17.2.3节）,因此在浮点异常发生时，处理器给结果赋一个默认值并继续安静地执行。例如，浮点数除0的结果是无穷大而不会终止程序：

```assembly
.data
	val1	DWORD	1
	val2 	REAL8 	0.0
.code
	fild vall		;加载整数至ST(0)中
	fdiv val2		;ST(0)=正无穷大
```

如果在FPU控制字中未屏蔽异常，处理器将进入执行合适的异常处理程序。关闭异常屏蔽是通过清除FPU控制字中向合适的位完成的（参见表17.17)。假如想要关闭对除零异常的屏蔽，下面是所需的步骤：

1. 存储FPU控制字至一个16位变量中。
2. 清除位2(除零标志）。
3. 加载变量至控制字中。

下面的代码关闭对除零异常的屏蔽：

```assembly
.data
	ctrlword WORD ?
.code
	fstcw 	ctrlword		;获取控制字
	and 	ctrlword,111111111111011b;关闭对除零异常的屏蔽
	fldcw 	ctrlword		;加载回FPU中
```

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.1bv1df1qxeo0.webp)

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.777948nacfk0.webp)

现在，如果执行下面的除零的代码，就会产生一个未屏蔽的异常：

```assembly
fild 	vall
fdiv 	val2	;除零
fst 	val2
```

FST指令一开始执行，MS-Windows就会显示下面的对话框：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.5pdz2afotoo0.webp)

### 屏蔽异常：

要屏蔽某种异常，应设置FPU控制字中的相应位，下面的代码屏蔽了除零异常：

```assembly
.data
	ctrlword WORD ?
.code
	fstcw 	ctrlword		;获取控制字
	or 		ctrlword,100b	;屏蔽除零异常
	fldcw 	ctrlword		;加载回FPU中
```

# 17.3 Intel 指令编码

如果想要彻底理解汇编语言，读者需要花些时间研究汇编语言是如何翻译成机器语言的。由于Intel指令集中有大量的指令和寻址方式，因此这个主题相当复杂。本节首先以运行于实地址模式下的8086/8088微处理器作为示例，后面再介绍Intel引入32位处理器后发生的一些变化。
正如在第2章中所说的，Intel8086处理器是使用复杂指令集计算机（CISC)设计的第一个实现，该指令集中包含了多种内存寻址方式以及大量的移位指令、算术运算指令、数据传送指令、逻辑操作指令。与精简指令集计算机（RISC)的指令相比，Intel指令的编码和解码需要一点技巧。编码一条指令的含义就是把汇编语言指令及其操作数转换成机器码，解码一条指令的含义是把指令的机器码转换成汇编语言。对于Intel指令编码和解码的学习，如果起不到别的作用的话，至少也会使你对MASM的作者多少有所感激！

## 17.3.1 IA-32指令的格式

通用的IA-32机器指令格式（如图17.6所示）包含指令前缀，操作码，Mod R/M字节，SIB(Scale Index Byte),地址偏移，立即数等部分。指令是以小尾顺序（little endian)存储的，因此前缀字节在指令的起始地址处，每条指令都有一个操作码，但其余的域就是可选的了，几乎没有指令包含所有这些域。平均而言，大多数指令都是2到3个字节长的。下面是各个域的简要描述：

- ==指令前缀(instruction prefix)覆盖操作数的默认尺寸==。
- ==操作码(opcode)指明是哪条指令的哪个变量==。例如，ADD指令根据使用的参数类型的不同，有9种不同的操作码。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.59hi5j5r8uk0.webp)

- ==Mod R/M字节指明寻址模式和操作数==。“R/M”代表寄存器（Register)和模式（Mode)。表17.18描述了Mod域不同取值的含义。表17.19描述了当Mod=10(二进制）时R/M域不同取值的含义。
- ==SIB字节(Scale Index Byte)用于计算数组索引的偏移地址==。
- ==地址偏移(Address Displacement)域存放着操作数的偏移地址，或者是可以在基址偏移寻址模式和基址变址寻址模式下加到基址寄存器或变址寄存器上的值==。
- ==立即数(ImmediateData)存放常量操作数==。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.uwuua292sds.webp)

## 17.3.2 单字节指令

==最简单的指令是无操作数或使用隐含操作数的指令，这类指令仅需要操作码域，操作码是由微处理器指令集预先定义好的。==表17.20列出了一些常见的单字节指令。乍看起来，好像INCDX指令是由于疏忽而被误放在表中了，但事实上是Intel指令集的设计者决定为一些常用指令提供惟一的操作码，因此它们对寄存器加一指令的代码大小和执行速度都进行了优化。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.4psux1ldmku0.webp)

## 17.3.3 立即操作数送寄存器

==立即数（常量）是以小尾顺序（最低的字节最先存储）存储在指令后面的。我们首先看看立即数送寄存器的指令，先避开复杂的内存寻址模式。==把立即数送寄存器的MOV指令的编码格式为B8+rwdw,其中操作码字节是B8+rw,+rw表示B8要和一个寄存器号（0~7)相加，dw表示后面跟一个立即字操作数（先低字节后高字节）,操作码字节中可使用的寄存器编号如表17.21所示。下面例子中的数值都是十六进制的。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.3mbmnozy4ve0.webp)

例子：PUSHCX。对应的机器指令是51,编码步骤如下：

1. 使用16位寄存器操作数的PUSH指令的操作码是50。
2. CX的寄存器号是1,因此1加50,得到机器码51。

例子：MOVAX,1。对应的机器指令是B80100(十六进制）。编码步骤如下：

1. 立即数送16位寄存器的MOV指令的操作码是B8。
2. AX的寄存器编号是0,因此0和B8相加（参见表17.21)。
3. 立即数以小尾顺序（0100)追加到指令的末尾。

例子：MOV BX,1234h。对应的机器指令是BB3412。编码步骤如下：

1. 立即数送16位寄存器的MOV指令的操作码是B8。
2. BX的寄存器编号是3,因此3和B8相加得到的操作码是BB。
3. 立即操作数是字节3412。

在实践中，建议读者手动汇编几条立即数送寄存器的MOV指令以掌握其方法和要领，然后查看列表文件检查MASM生成的代码。

### 17.3.4 寄存器模式指令

在只使用寄存器操作数的指令中，ModRM字节中分别使用三个数据位指定每个寄存器操作数。表17.22给出了寄存器的位编码。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.1vplagt5ewg0.webp)

例如，MOV AX,BX的机器码是89D8。把寄存器数据送任何操作数的16位MOV指令的Intel编码是89/r，/r表示操作码后跟一个ModR/M字节。ModR/M字节由三个域组成（mod,reg,R/M)。以D8为例，包含下面三个域：

![image-20230304225628079](../../../../Library/Application%20Support/typora-user-images/image-20230304225628079.png)

- 位6~7是m*od域，表示寻址方式，mod域的值为11,指示R/M域存放着寄存器号。
- 位3~5是reg域，表示源操作数，在上例中，BX的寄存器号是011。
- 位0~2是R/M域，表示目的操作数，在上例中，AX的寄存器号是000。

表17.23给出了一些使用8位和16位寄存器操作数的例子。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.47t2b4u1aso0.webp)

## 17.3.5 IA-32 的操作数尺寸前缀

下面我们把注意力转移到32位Intel处理器指令的编码上来。有些IA-32机器指令是以操作数尺寸前缀（66h)开头的，该前缀修改了其后指令的段属性。问题是，为什么要用操作数尺寸指令前缀呢？在实现8088/8086指令集时，几乎所有的256个可能的操作码都被用于处理8位和16位的操作数了，在Intel引入32位处理器时，就必须使用一些新的操作码处理32位操作数，同时与老处理器兼容。因此，对于使用16位处理器的程序，指令使用16位的操作数是默认的，任何使用32位操作数的指令都要增加一个前缀字节。对于使用32位处理器的程序，指令使用32位的操作数是默认的，任何使用16位操作数的指令都要增加一个前缀字节。程序无论使用32位还是16位处理器，对于使用8位操作数的指令，无需使用任何前缀。
例子：16位操作数。下面通过汇编表17.23中给出的MOV指令来看看前缀字节在16位模式下是如何工作的。下例使用.286伪指令指示编译后代码的目标处理器，以确保不会使用32位的寄存器。下面例子中每条MOV指令后都给出了相应的机器码：

```assembly
.model small
.286
.stack 100h
.code
	main PROC
	mov ax,dx		;8BC2
	mov al,dl		;8A
```

(这里没有使用Irvine16.inc,因为其面向的目标处理器是386。）
接下来，使用.386伪指令为32位处理器汇编同样的指令，这时默认的操作数是32位的，下面的例子中既使用了16位操作数也使用了32位操作数。第一条MOV指令无须前缀，因为它使用的是32位操作数。第二条MOV指令由于使用的是16位的操作数，因此需要使用操作数尺寸前缀：

```assembly
.model small
.386
.stack 100h
.code
main PROC
	mov	eax,edx
	mov	ax,dx		;668BC2
	mov	al,dl
```

## 17.3.6 内存模式指令

如果Mod R/M字节只用于指示寄存器操作数的话，那么Intel指令的编码将比事实上简单得多事实上，Intel汇编语言有大量的寻址方式，导致ModRM字节的编码相当复杂（IA-32指令集的复杂性是许多精简指令集计算机设计的支持者对其批评的主要来源之一）。
该字节可指定256种不同的操作数组合，表17.24列出了Mod域等于00时ModR/M字节的编码（十六进制）(完整的列表可在IA-32 Intel Architecture Software Developer's Manual卷II中找到）。表格中的Mod列包含两位，用于指示寻址方式的分类，例如在Mod等于00的情况下，有8种可能的R/M值（二进制000到111),R/M域用于指示有效地址列中的操作数类型。
例如，要对指令MOV[SI],AX进行编码，通过查表我们知道：Mod域是00,R/M域是二进制值100,通过表17.19我们知道AX的寄存器号是二进制值000,因此完整的Mod R/M字节是二进制值00000100,也就是十六进制值04。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.37ezki9vp9k.webp)

十六进制值04在AX标记的列的第5行上。
MOV[SI],AL的Mod R/M字节也是04,这时由于AL的寄存器号也是000,从8位寄存器移出数据的MOV指令的操作码是88,ModR/M字节是04,因此机器指令是8804。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.4evw04pblhk0.webp)

### MOV指令的例子

8位和16位MOV指令的操作码和格式如表17.25所示。表17.26和表17.27解释了表17.25
中用的缩略语。手动汇编MOV指令时可参考这些表格（如果想了解更多这方面的细节，请参考

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.kmfnwnd1oe8.webp)

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.2vwnwlic1f40.webp)

# 17.4 本章小结

==浮点数由三部分构成：符号、尾数和指数==。Intel微处理器使用IEEE发布的Standard754-1985 for Binary Floating-Point Arithmetic中规定的三种浮点二进制存储格式：

- 32位的单精度值符号占1位、指数占8位、尾数中的小数部分占23位。
- 64位的双精度值符号占1位、指数占11位、尾数中的小数部分占52位。
- 80位的扩展精度值符号占1位、指数占16位、尾数中的小数部分占63位。

符号位为1表示是负数，为0表示是正数。
浮点数的尾数部分由小数点及其前后数字构成。
在计算机中，并非所有的0到1之间的实数都能用浮点数表示，这是因为计算机只有有限个数据位。
正规化的有限数是所有0到1之间的可以用正规实数编码的0到无穷大之间的非零有限值。正无穷(+00)代表最大的正实数，负无穷(-00)代表最小的负实数。NaN是不能用有效浮点数表示的数。
Intel 8086处理器是为处理整数运算而设计的，因此Intel又引入了一种独立浮点协处理器芯片，可以同8086一起插在主板上。从Intel486处理器开始，浮点处理硬件集成进了主CPU,称为浮点单元(FPU,Floating Point Unit)。
FPU有8个可独立寻址的8位寄存器：R0~R7,是以寄存器堆栈的形式组织的。在计算时浮点操作数是以扩展精度实数格式存放在FPU栈中的，内存操作数也可用于浮点运算。FPU在内存中保存算术运算的结果时，自动把结果转换为以下格式之一：整数、长整数、单精度(短实数)、双精度(长实数)或压缩的二进制编码的十进制整数。
浮点指令总是以字母F开头，以便与CPU指令区别开。指令的第二个字母(通常是B或1)说明了内存操作数应如何解释：B表示二/十进制(BCD)操作数，I表示二进制整数操作数，如果未指定B或I,就表示操作数是实数格式的。
Intel8086处理器是使用复杂指令集计算机(CISC)设计的第一个实现，该指令集很大，其中包含了多种内存寻址方式以及大量的移位指令、算术运算指令、数据传送指令、逻辑操作指令。
编码一条指令的含义就是把汇编语言指令及其操作数转换成机器码，解码一条指令的含义是把指令的机器码转换成汇编语言。
IA-32机器指令的格式包含指令前缀，操作码，Mod R/M字节，SIB(Scale Index Byte),可选的立即数，可选的地址偏移等部分。很少有指令包含所有这些域。前缀覆盖了目标处理器的默认操作数尺寸。操作码字节包含了指令唯一的操作代码。Mod R/M字节指定了操作数及寻址方式。在仅使用寄存器操作数的指令中，Mod R/M字节中分别使用三个数据位指定每个寄存器操作数。