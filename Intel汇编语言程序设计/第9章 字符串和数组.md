[toc]

本章要点

- 简介
- 基本字符串操作指令
- 精选的字符串过程
- 二维数组
- 整数数组的查找和排序

# 9.1 简介

如果读者学会了如何高效地处理字符串和数组，就称得上是掌握了代码优化的一般技术了。研究表明，大多数程序90%的时间花在了运行其10%的代码上。毫无疑问，这10%的代码通常是循环，而在处理数组和字符串时需要使用循环。本章介绍处理字符串和数组的技术，目标是编写高效的代码。
本章从Intel指令集中优化过的基本的字符串指令开始讲述，这些指令是设计用来移动、比较装载和存储数据块的。接下来，本章介绍Irvine32(或Irvine16)库中的几个字符串处理过程，其实现与标准C字符串库的实现非常相似。本章的第3部分讲述如何使用高级间接寻址模式——基址变址和相对基址变址模式——操纵二维数组。简单的间接寻址已在4.4节介绍过。
本章最后一节的“整数数组的查找和排序”是最有趣的，读者将看到计算机科学中两种最用的数组处理算法——冒泡排序和二分查找——实现起来是多么容易。以Java、C++以及汇编语言研究这些算法是个不错的主意。

# 9.2 基本字符串操作指令

IA-32指令集中有5组处理字节、字和双字数组的指令，尽管这些指令称为基本字符串指令，但它们的用法并不限于字符数组。表9.1中的每条指令都隐含使用了ESI或EDI寄存器，或者同时使用二者以寻址内存。表中的指令在引用累加器时，根据指令数据大小的不同，隐含使用AL,AX或EAX基本的字符串操作指令可以高效地执行，因为它们自动重复并自动增加数组的索引。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.2ijynr127dc0.webp)

==在保护模式程序中，ESI自动作为由DS寻址的段内的偏移，EDI自动作为由ES寻址的段内的偏移，DS和ES总是具有同样的值并且无法改变它们（相反，在实地址模式下ES和DS经常由汇编程序员操纵）。==
在实地址模式下，字符串操作指令使用SI和DI寄存器来寻址内存。SI是DS段中的偏移，DI是ES段中的偏移。我们通常在main过程的开始把ES和DS设为同样的段值：

```assembly
main PROC
mov ax,@data			; get addr of data seg
mov ds,ax				; initialize DS
mov es,ax				; initialize ES
```

### 使用重复前缀

==字符串操作指令本身每次只处理一个或一对值，但如果增加一个重复前缀，指令就会使用ECX作为计数器进行重复。重复前缀使得可以仅用一条指令处理整个数组==。可以使用的重复前缀如下所示：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.652rhik95rw0.webp)

#### 例子：复制字符串

在下面的例子中，MOVSB指令从string1中移动10个字节到string2,在执行MOVSB指令之前，重复前缀首先测试ECX是否大于0。如果ECX等于0,则执行程序中的下一条指令。如果ECX>0,那么ECX减1并重复执行该指令：

```assembly
cld							;方向标志
mov esi,OFFSET stringl		;ESI指向源
mov edi,OFFSET string2		;EDI指向目的
mov ecx,10					;计数器设为10
rep movsb					;移动10个字节
```

==MOVSB指令每次重复时ESI和EDI会自动增1，这可由CPU的方向标志控制。==

#### 方向标志

==字符串指令根据方向标志的状态使ESI和EDI增加或减少（见表9.2),方向标志可以通过CLD和STD指令显式地修改：==

```assembly
CLD		;清除方向标志
STD		;设置方向标志
```

在执行字符串操作指令之前忘记设置方向标志可能导致非常令人头疼的问题，在执行时由于方向标志值的不确定性，多次执行的结果可能会非常地不一致。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.7iuex8kl7tw0.webp)

## 9.2.1 MOVSB,MOVSW和MOVSD指令

==MOVSB,MOVSW和MOVSD指令从ESI指向的内存位置复制数据到EDI指向的内存位置，同时ESI和EDI寄存器自动增加或减少（依据方向标志的值而定）。==

==MOVSB,MOVSW和MOVSD指令可以和重复前缀联合使用，方向标志决定ESI和EDI的值是增加还是减少，增减的数值如下表所示。==

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.469j3gi95460.webp)

#### 例子

复制双字数组。假设要从source复制20个整数到target中，在复制完数据之后，ESI和EDI将指向每个数组末尾的后一个位置（4字节）:

```assembly
.data
source DWORD 20 DUP (OFFFFFFFFh)
target DWORD 20 DUP (?)
.code
cld						;方向=正向
mov ecx,LENGTHOF source	;设置REP的计数器
mov esi,OFFSET source	;ESI指向源
mov edi,OFFSET target	;EDI指向目的
rep movsd				;复制双字
```

### 9.2.2 CMPSB,CMPSW和CMPSD指令

CMPSB,CMPSW和CMPSD指令将ESI指向的内存操作数同EDI指向的内存操作数相比较：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.4jam2edgth20.webp)

CMPSB,CMPSW和CMPSD指令可以同重复前缀联合使用，方向标志决定了ESI和EDI的值是增加还是减少。

### CMPS指令的显式格式

==字符串比较指令还有另外一种格式，称为显式格式。在显式格式中应提供两个间接操作数，使用PTR运算符指明操作数的大小==。例如：

```assembly
cmps DWORD PTR [esi],[edi]
```

但是CMPS指令很棘手的地方在于汇编器允许提供容易令人误解的操作数，如：

```assembly
cmps DWORD PTR [eax],[ebx]
```

==不管使用什么操作数，CMPS都只比较ESI指向的内存值和EDI指向的内存值==。CMPS的操作数顺序也与我们熟悉的CMP指令的操作数顺序相反：

```assembly
CMP		目的,源
CMPS	源,目的
```

==有一种方法可用于记住二者之间的区别：CMP表示从目的（target)中减掉源（source)，而CMPS暗示了从源（source)中减掉目的（target)。最好避免使用CMPS而使用其特定的版本（CMPSB,CMPSW和CMPSD)。==

#### 例子：比较双字

假设想用CMPSD指令比较一对双字，在下面的例子中，可以看到source的值比target的值小，所以当执行JA的时候，不会发生跳转。相反，程序会执行JMP指令：

```assembly
.data
source DWORD 1234h
target DWORD 5678h
.code
mov 	esi, OFFSET source
mov 	edi,OFFSET target
cmpsd		;比较双字
ja 		L1	;如果source>target则跳转
jmp 	L2	;如果source<=target则跳转
```

==如果想要比较多个双字，就需要清除方向标志（向前）,并把ECX初始化为计数器，然后在CMPSD指令前使用重复前缀：==

```assembly
mov esi,OFFSET source
mov edi,OFFSET target
cld					;方向=向前
mov ecx,count		;重复计数器
repe cmpsd			;相等则重复
```

REPE前缀重复进行比较动作，并自动增加ESI和EDI,直到ECX等于0或发现任何一对双字不相等为止。

#### 例子：比较两个字符串

一对字符串的比较通常是从字符串头部开始的，然后按顺序逐个字符进行比较。例如AABC和AABB的前三个字符是相等的，但在第四个位置，C的ASCII码（在第一个字符串中）大于B的ASCII码（在第二个字符串中）,那么第一个字符串大于第二个字符串。与之类似，如果比较字符串AAB和AABB,那么第二个字符串的值较大，因为它们的前三个字符是相等的，而第二个字符串还有一个额外的字符。
下面的代码使用CMPSB指令比较两个等长度的字符串，REPE前缀使CMPSB指令逐个比较字符并增加ESI和EDI的值，直到在两个字符串中发现了差异为止：

```assembly
TITLE Comparing Strings	(Cmpsb.asm)
; This program uses CMPSB to compare two strings
; of equal length .
INCLUDE Irvine32.inc.
.data
source BYTE 	"MARTIN "
dest 	BYTE 	"MARTINEZ"
strl 	BYTE 	"Source is smaller", 0dh, 0ah,
str2 	BYTE 	"Source is not smaller", 0dh, 0ah, 0
.code
main PROC
	cld		;方向=正向
	mov 	esi,OFFSET source
	mov 	edi,OFFSET dest
	mov 	cx,LENGTHOF source
	repe 	cmpsb
	jb 		source_smaller
	mov 	edx,OFFSET str2
	jmp 	done
source_smaller:
	mov 	edx,OFFSET str1
done:
	call 	writestring
	exit
	main 	ENDP
END main
```

使用给出的测试数据，控制台上会显示信息“Source is smaller”。在图9.1中，ESI和EDI最终指向两个字符串不同之处后面的一个位置。如果两个字符串是相同的，ESI和EDI将指向字符串结尾的后一个位置。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.5sxdws9vr640.webp)

==应该强调一下，只有在两个字符串长度相等的条件下，使用CMPSB指令比较两个字符串才是可行的，这也就是为什么在前一个例子中要在字符串MARTIN的结尾处填充两个空格使其同MARTINEZ长度相等的原因，这是很重要的==。无须多说，这暴露了使用CMPS比较字符串的一个很严重的限制，该限制将由9.3.1节的Str_compare过程解决。

## 9.2.3 SCASB,SCASW和SCASD指令

==SCASB,SCASW和SCASD指令把AL/AX/EAX中的值同由EDI寻址的目标内存中的字节、字或双字相比较。这些指令在一个长字符串或数组中查找一个值的时候特别有用。如果使用REPE(或REP)前缀，当ECX>0并且AL/AX/EAX匹配内存中的值时，指令继续扫描字符串或数组。REPNE前缀使得指令扫描字符串，直到AL/AX/EAX匹配内存中的一个值或ECX=0时停止。==

### 扫描一个匹配字符

在下例中，我们在字符串变量alpha中查找字母“F”，如果找到了该母，EDI指向匹配字符串后面的一个字符（如果没有找到匹配字符，就会执行JNZ指令退出）:

```assembly
.data
	alpha BYTE "ABCDEFGH", 0
.code
	mov edi,OFFSET alpha		;EDI指向字符串
	mov al,'F'					;查找字母F
	mov ecx,LENGTHOF alpha		;设置查找计数器
	cld							;方向=向前
	repne scash					;不相等则重复
	jnz	quit					;如果字符未找到则退出
	dec edi						;找到了：EDI回退
```

在这个例子中，增加了一条JNZ指令，检测扫描是否是因为ECX=0(字母未找到）而终止的。

## 9.2.4 STOSB,STOSW和STOSD指令

==STOSB,STOSW和STOSD指令把AL/AX/EAX的内容存储在EDI指向的内存单元中，同时EDI的值根据方向标志的值增加或减少。同REP前缀联合使用的时候，这组指令在需要以指定的字符填充整个字符串或数组时非常有用。==例如下面的代码把string1的每个字节初始化为0FFh:

```assembly
.data
	Count = 100
string1 BYTE Count DUP(?)
.code
	mov al,0FFh				;要存储的值
	mov edi,OFFSET string1	;EDI指向目标
	mov ecx,Count			;字符计数
	cld						;方向=向前
	rep stosb				;以AL中的值填充
```

## 9.2.5 LODSB,LODSW和LODSD指令

==LODSB,LODSW和LODSD指令从ESI指向的内存位置向AL/AX/EAX中装入一个值，同时ESI的值根据方向标志值增加或减少。我们很少把REP前缀同LODS指令联用，因为装入到累加器中的每个新值都会覆盖掉以前的值，相反，一般仅用LODS指令来装入一个值。==例如LODSB指令可以替代下面的两条指令（假设方向标志已清零）:

```assembly
mov al,[esi]			;传送一个字节至AL
inc esi					;指向下一个字节
```

#### 数组乘法的例子

下面的程序把双字数组的每个元素都乘以一个常量，程序中同时使用了LODSD和STOSD指令：

```assembly
TITLE Multiply an Array	(Mult.asm)
; This program multiplies each element of an array
; of 32-bit integers by a constant value .
INCLUDE Irvine32.inc
.data
	array DWORD 1,2,3,4,5,6,7,8,9,10	;测试数据
	multiplier DWORD 10					;测试数据
.code
main PROC
	cld							;方向=向前
	mov 	esi,OFFSET array	;源指针
	mov 	edi,esi				;目的指针
	mov 	ecx,LENGTHOF array	;循环计数器
L1:
	lodsd						;加载[ESI]至EAX
	mul 	multiplier			;乘以一个值
	stosd						;EAX存储至[EDI]
	loop 	L1
	exit
	main 	ENDP
END main
```

# 9.3 精选的字符串过程

在本节中，我们介绍Irvine32和Irvine16库中几个操作以NULL字符（空字符）结尾的字符串的简单过程。这些过程与标准的C语言库函数极其相似：

```assembly
;复制源字符串至目的串中
str_copy PROTO,
	source:PTR BYTE
	target:PTR BYTE
;在EAX中返回字符串的长度（不包含结尾的空字节）
str_length PROTO,
	pString:PTR BYTE
;比较string1和string2,设置零标志和进位标志的方式同CMP指令
str_compare PROTO,
	string1:PTR BYTE,
	string2:PTR BYTE
;从字符串中删除指定的字符，第二个参数是要删除的字符
Str_trim PROTO,
	pString:PTR BYTE,
	char:BYTE
;把字符串转换成大写
Str_ucase PROTO,
	pString:PTR BYTE
```

## 9.3.1 Str_compare过程

Str_compare过程比较两个字符串，它的调用格式如下：

```assembly
INVOKE Str_compare,ADDR string1,ADDR string2
```

Str_compare从第一个字节开始向前比较两个字符串。比较是大小写敏感的，因为大写字母和小写字母的ASCII码不同。该过程没有返回值，假设参数是string1和string2,进位标志和零标志可按表9.3解释。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.5vnr2ix1q2c0.webp)

请参见6.2.7节对CMP指令如何设置进位标志和零标志的解释。下面是Str_compare过程的清单，关于该过程的解释说明请参见Compare.asm

```assembly
Str_compare PROC USES eax edx esi edi,
		string1:PTR BYTE,
		string2:PTR BYTE
; Compare two strings .
; Returns nothing , but the Zero and Carry flags are affected
; exactly as they would be by the CMP instruction.
	mov 	esi,string1
	mov 	edi,string2
L1:
	mov 	al,[esi]
	mov 	dl,[edi]
	cmp 	al,0		;string的结尾？
	jne 	L2			;否
	cmp 	d1,0		;是：string2的结尾？
	jne 	L2			;否
	jmp 	L3			;是：退出设置ZF=1
L2:
	inc		esi			;指向下一个字符
	inc		edi
	cmp 	al,dl		;字符相等？
	je 		L1			;是：继续循环
						;否：退出并设置标志
L3:
	ret
Str_compare ENDP
```

在实现Str_compare过程时本可以用CMPSB指令，不过如果要用CMPSB指令，就必须知道较长的字符串的长度，因此需要对Str_length过程（在下节中讲述）进行两次调用。在这种特殊的情况下，在同一个循环中检查两个字符串结尾的空字符更加简单。CMPSB指令在处理已知长度的长字符串或大数组时效率更高。

## 9.3.2 Str_length 过程

Str_length过程在EAX寄存器中返回字符串的长度，调用该过程时需要传递字符串的偏移，例如：

```assembly
INVOKE str_length,ADDR mystring
```

下面是该过程的实现：

```assembly
Str_length PROC USES edi,
	pString:PTR BYTE			;指向string
	mov 	edi , pString
	mov 	eax,0				;字符计数器
L1:
	cmp 	byte ptr [edi],0 	;string的结尾
	je 		L2					;是：退出
	inc 	edi					;否：指向下一个字符
	inc 	eax					;count加1
	jmp 	L1
L2:
	ret
Str_length ENDP
```

该过程的解释说明参见Length.asm文件。

## 9.3.3 Str_copy过程

Str_copy过程把以空字符结尾的字符串从源位置复制到目的位置。在调用该过程之前，必须确保目的操作数足够大以便容纳被复制的字符串。调用Str_copy过程的格式如下：

```assembly
INVOKE Str_copy,ADDR source,ADDR target
```

该过程不返回任何值，下面是它的具体实现：

```assembly
Str_copy PROC USES eax ecx esi edi,
	source:PTR BYTE,	;源字符串
	target:PTR BYTE		;目的字符串
; Copy a string from source to target .
; Requires : the target string must contain enough
; space to hold a copy of the source string .
INVOKE Str_length,source	;EAX=源的长度
	mov ecx,eax				;重复计数
	inc ecx					;由于结尾的有空字节因此加1
	mov esi,source
	mov edi,target
	cld						;方向=向前
	rep movsb				;复制字符串
	ret
Str_copy ENDP
```

过程的解释说明参见CopyStr.asm文件。

## 9.3.4 Str_trim 过程

Str_trim过程删除以空字符结尾的字符串结尾的所有指定字符。调用格式如下：

```assembly
INVOKE Str_trim, ADDR string, char_to_trim
```

该过程的逻辑非常有趣，因为程序必须处理可能出现的各种情况（这里字符串结尾的特定字符以#表示）:

1. 字符串为空。
2. 字符串在末尾包含一个或多个特定字符，如“Hello##”。
3. 字符串仅包含一个字符，如“#”。
4. 字符串中不包含特定字符，如“Hello”或“H”。
5. 字符串中包含一个或多个特定字符，其后又有一些其他字符，如“#H”或“##Hello”。

可以使用Str_trim删除字符串结尾的所有空格（或其他任何重复字符）。剪裁字符串的最简单方法是在字符串中要保留的字符后面插入NULL字节，NULL字节后的任何字符都无关紧要了。下面是该过程的源代码，Trim.asm测试了Str_trim过程：

```assembly
Str_trim PROC USES eax ecx edi,
		pString:PTR BYTE,		;指向string
		char:BYTE				;要删除的字符
; Remove all occurrences of a given character from
; the end of a string
; Returns : nothing
;--------
	mov 	edi,pString
	INVOKE 	Str_length,edi		;在EAX中返回长度
	cmp 	eax,0				;字符串长度为0?
	je 		L2					;是：退出
	mov 	ecx,eax				;否：计数器=字符串长度
	dec 	eax
	add 	edi,eax				;EDI指向最后一个字符
	mov 	al,char				;要删除的字符
	std							;方向=向后
	repe 	scasb				;跳过要删除的字符
	jne 	L1					;要删除的是否是第一个字符？
	dec 	edi					;调整EDI:ZF=1&&ECX=0
L1:	
	mov 	BYTE PTR [edi+2],0	;插入空字节
L2:
	ret
Str_trim ENDP
```

除一种情况之外，EDI在要以NULL替换的字符的前两个字节的位置停下来。表9.4给出了对多个非空字符串测试的情况。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.134u7zbqs8b4.webp)

图9.2显示了上表中第一个字符串在SCASB停止时EDI的位置：

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.1r8lp6elmjog.webp)

在SCASB指令结束时，程序又对字符串是否只包含一个恰好要剪裁掉的字符这种情况进行了特别测试。在这种情况下，EDI指向要替换为空字节的位置之前一个字节处（因为这时SCASB指令是在ECX=0而不是在ZF=1的情况下停止的）。为了补偿这一点，在将空字符存储到[EDI+2]位置之前首先把EDI减1,如图9.3所示。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.68ug3764j740.webp)

## 9.3.5 Str_ucase过程

Str_ucase过程把字符串中的所有字符转换成大写形式，该过程没有任何返回值。调用时，传递的参数是字符串的偏移地址：

```assembly
INVOKE Str_ucase,ADDR myString
```

下面是过程的具体实现：

```assembly
Str_ucase PROC USES eax esi,
		pstring:PTRBYTE
; Convert a null-terminated string to uppercase .
;Returns:nothing
	mov 	esi,pstring
L1:
	mov 	al,[esi]		;取一个字符
	cmp 	al,0			;字符串的结尾？
	je 		L3				;是：退出
	cmp 	al,'a'			;小于“a”？
	jb 		L2
	cmp 	al,'z'			;大于“z”？
	ja 		L2
	and 	BYTE PTR [esi],11011111b ;转换字符
L2:
	inc		esi				;下一个字符
	jmp 	L1
L3:
	ret
Str ucase ENDP
```

该过程的解释说明参见Ucase.asm。

## 9.3.6字符串库演示程序

下面的程序（StringDemo.asm)演示了如何调用本书链接库中的Str_trim,Str_ucase,Str_compare和Str_length过程：

```assembly
TITLE String Library Demo (StringDemo. asm)
; This program demonstrates the string-handling procedures in
; the book's link library.
INCLUDE Irvine32.inc
.data
	string_1 BYTE	"abcde////",0
	string_2 BYTE	"ABCDE",0
	msgo 	BYTE	"string_1 in upper case:",0
	msg1	BYTE	"stringl and string2 are equal",0
	msg2 	BYTE	"string_1 is less than string_2",0
	msg3 	BYTE	"string_2 is less than string_1",0
	msg4 	BYTE 	"Length of string_2 is",0
	msg5 	BYTE	"string_1 after trimming:",0
.code
main PROC
	call trim_string
	call upper_case
	call compare_strings
	call print_length
exit
main ENDP
trim_string PROC
;Remove trailing characters from string 1.
	INVOKE 	Str_trim,ADDR string.1,'/'
	mov 	edx, OFFSET msg5
	call 	WriteString
	mov 	edx,OFFSET string_1
	call 	writestring
	call 	Crlf
	ret
trim_string ENDP
upper_case PROC
; Convert string_1 to upper case.
	mov 	edx,0FFSET msg0
	call 	WriteString
	INVOKE 	Str_ucase,ADDR string_1
	mov 	edx,OFFSET string_1
	call 	WriteString
	call 	Crlf
upper_case ENDP
compare_strings PROC
;Compare string . 1 to string 2 .
INVOKE Str_compare,ADDR string_1,ADDR string_2
	.IF ZERO?
		mov edx,OFFSET msg1
	.ELSEIF CARRY ?
		mov edx,OFFSET msg2
; string 1 is less than ...
	.ELSE
		mov edx,OFFSET msg3
; string 2 is less than...
	.ENDIF
	call WriteString
	call Crlf
ret
compare_strings ENDP
print_length PROC
; Display the length of string _ 2 .
	mov 	edx, OFFSET msg4
	call 	WriteString
	INVOKE 	Str_length,ADDR string_2
	call 	WriteDec
	call 	Crlf
ret
print_length ENDP
END main
```

示例程序调用Str_trim删除string_1结尾的字符，然后再调用Str_ucase过程把字符串转换成大写形式。
程序的输出：下面是演示程序的输出。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.5up3c18pnh80.webp)

# 9.4 二维数组

## 9.4.1 行和列的顺序

从汇编语言程序员的视角来看，二维数组是一维数组的高阶抽象。对于二维数组在内存中行列的存储，高级语言一般采用下面的两种方法：行主序（row-major order)和列主序（column-major order)。使用行主序存储（最常使用）时，第一行放在内存块的开始，第一行的最后一个元素后接第二行的第一个元素。使用列主序存储时，第一列的元素放在内存块的开始，第一列的最后一个元素后接第二列的第一个元素。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.6d5tmr3d5740.webp)

## 9.4.2 基址变址操作数

==基址变址（base-index)操作数把两个寄存器的值相加，得到一个偏移地址。==两个寄存器分别称为基址（base)和变址（index)。格式如下：

```assembly
[base + index]
```

格式中的方括号是必需的。==在32位模式下，基址和变址部分均可使用任意32位通用寄存器==；在16位模式下，基址寄存器必须是BX或BP(除非是寻址堆栈上的数据，否则应尽量避免使用BP或EBP作为基址寄存器）,变址寄存器必须是SI或DI。下面是32位模式下各种组合的例子：

```assembly
.data
	array WORD 1000h,2000h,3000h
.code
	mov 	ebx,OFFSET array
	mov 	esi,2
	mov 	ax,[lebx+esi]	;AX=2000h
	mov 	edi,OFFSET array
	mov 	ecx,4
	mov 	ax,[edi+ecx]	;AX=3000h
	mov 	ebp,OFFSET array
	mov 	esi,0
	moy 	ax,[ebp+esi]	;AX=1000h
```

### 二维数组：

以行主序访问二维数组时，通常使用基址寄存器存放行偏移，变址寄存器存放列偏移。例如，下表包含了3行5列：

```assembly
tableB	BYTE 10h,20h,30h,40h,50h
Rowsize = ($-tableB)
		BYTE 60h,70h,80h,90h,0A0h
		BYTE 0B0h,0C0h,0D0h,0E0h,0F0h
```

表是以行主序的方式存储的，常量Rowsize是数组每行的字节数，这是通过汇编器自动计算的。我们想要通过行列坐标定位表中特定的项，假设坐标是从0开始的，那么行1列2的位置处包含的是80h。我们把EBX设为表的起始偏移地址，然后再加上Rowsizexrow_index的值，以计算行的偏移地址，再把ESI设为列的索引：

```assembly
row_index = 1
column_index = 2
mov 	ebx,OFFSET tableB		;表的偏移地址
add 	ebx,RowSize * row_index	;行的偏移地址
mov 	esi,column_index
mov 	al,[ebx + esi]			;AL=80h
```

如果数组的起始偏移地址是0150h,那么EBX+ESI代表的有效地址就是0157h。图9.5显示了EBX和ESI相加是如何得到tableB[1,2]表项的偏移地址的。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.3zcezsykbo40.webp)

像其他间接寻址方式一样，如果有效地址超出了程序的数据区域，就会产生通用保护异常。

#### 计算一行之和

基址变址寻址简化了许多与二维数组相关的任务，例如我们可以计算整数矩阵一行元素的和。下面的过程calc_row_sum(参见RowSum.asm)计算一个8位整数的矩阵中选定行的和：

```assembly
calc_row_sum PROC uses ebx ecx edx esi
; Calculates the sum of a row in a byte matrix.
; Receives : EBX = table offset , EAX = row index ,
; ECX=row size,in bytes.
; Returns : EAX holds the sum .
	mul 	ecx			; row index x row size
	add 	ebx,eax		;行的偏移地址
	mov 	eax,0		;累加器
	mov 	esi,0		;列的索引
L1:
	movzx 	edx,BYTE PTR[ebx+esi]	;取一个字节
	add 	eax,edx					;累加
	inc		esi						;行的下一个字节
	loop 	L1
	ret
calc_row_sum ENDP
```

在MOVZX指令中需要使用BYTEPTR来明确操作数的尺寸。

### 比例因子

如果编写字数组的处理代码，就要把变址部分乘以一个比例因子2。下面的例子定位行1列2处的值：

```assembly
tablew 	WORD 10h,20h,30h,40h,50h
Rowsizew = ($-tablew)
		WORD 60h,70h,80h,90h,0A0h
		WORD OBOh,0C0h,0D0h,0E0h,0F0h
.code
	row_index = 1
	column_index = 2
	mov 	ebx,OFFSET tablew		;表的偏移地址
	add 	ebx,RowSizew * row_index	;行的偏移地址
	mov 	esi,column_index
	mov 	ax,[ebx+esi*TYPE tableW]	;AX=0080h
```

本例中使用的比例因子（TYPE tableW)等于2,比例因子只能是2、4或8。在处理双字数组时就必须使用比例因子4:

```assembly
tableD DWORD 10h, 20h,....etc.
.code
mov eax,[ebx+esi*TYPE tableD]
```

### 9.4.3 相对基址变址操作数

相对基址变址操作数把偏移、基址寄存器、变址寄存器以及可选的比例因子组合起来产生一个有效地址，下面是该操作数最常见的两种格式：

```assembly
[base + index + displacement]
displacement[base + index]
```

偏移（displacement)可以是变量的名字或常量表达式。在32位模式下，基址和变址部分可以使用任意的32位寄存器。在16位模式下，基址部分必须是BX或BP,变址部分必须是SI或DI。相对基址变址操作数也非常适合处理二维数组，偏移可以是数组的名字，基址部分可以存放行偏移，变址部分可以存放列偏移。

#### 双字数组的例子

下面的二维双字数组共3行5列：

```assembly
tableD 	DWORD 10h,20h,30h,40h,50h
Rowsize=($-tableD)
		DWORD 60h,70h,80h,90h,0A0h
		DWORD 0B0h,0C0h,0D0h,0E0h,0F0h
```

Rowsize等于20(14h)。假设行列坐标都是从0开始的，那么行1列2的项包含了80h。为访问该项，我们把EBX设为行索引，ESI设为列索引：

```assembly
mov 	ebx,Rowsize		;行索引
mov 	esi,2			;列索引
mov 	eax,tableD[ebx+esi*TYPE tableD]
```

假设tableD开始于偏移地址0150h,图9.6显示了EBX和ESI相对于数组的偏移，偏移地址是十六进制的。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.35iv32ffkuu0.webp)

# 9.5 整数数组的查找和排序

计算机科学家们已经花费了大量时间和精力去寻找更有效的海量数据搜索和排序的方法，显然，为特定的应用程序选择最好的算法远比购买一台更加快速的计算机有价值得多。大多数学生使用C++和Java等高级语言研究搜索和排序算法，但由于使用汇编语言能够清晰地看到底层的实现细节，使得汇编语言为研究算法提供了另外一个视角。非常有趣的是，20世纪最著名的算法学家之一DonaldKnuth在其出版的书籍中也使用汇编语言来编写例子。
查找和排序也给了我们一个尝试本章中介绍的几种寻址方式的绝好机会，特别是，基址变址寻址方式被证明是非常有用的，因为我们可以使用一个寄存器（如EBX)指向数组的基址，而使用另外一个寄存器（如ESI)作为指向数组任意位置的指针。

## 9.5.1 冒泡排序法

冒泡排序法（bubble sort)从位置0和1开始比较每对数组值，如果两个值的顺序不正确，就进行交换。图9.7显示了对一系列整数进行的一次完整的比较过程。
在第一遍处理完毕之后，数组并未被排好序，因此外层循环又开始另一轮处理，在完成n-1遍处理后，就能保证数组是按顺序排列好了。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.3pmh46d1fni0.webp)

冒泡排序对小数组工作得很好，但对大数组就非常低效了。冒泡排序法是一个时间复杂度为O($n^2$)的算法，也就是说排序时间与数组元素的数目（n)之间是二次方的关系。假设排序1000个元素需要花费0.1s,那么当数组元素数目增加10倍的时候，排序数组需要的时间增加到原来的$10^2$倍。下表显示了不同大小的数组排序所需要的时间，其中假设1000个数组元素可以在0.1s内排序完毕。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.325un6dnfo00.webp)

对于包含百万个以上整数的数组来说，冒泡排序法并不是一个好的排序算法，因为要完成排序需要花费27h!但对于只有几百个整数的数组来说，这种排序方法已经足够好了。

#### 伪码

使用类似于汇编语言的伪码来创建冒泡排序的简化版本是非常有用的。下面用N代表数组的大小，cx1代表外层的循环计数，cx2代表内层的循环计数：

```c
cx1 = N - 1
while ( cx1 > 0 )
{
	esi = addr(array)
	cx2 = cx1
	while( cx2 > 0 )
	{
		if(array[esi]<array[esi+4])
			exchange(array[esi],array[esi+4])
		add esi,4
		dec cx2
	}
	dec cx1
}        
```

一些细节性的代码（如保存和恢复外层循环计数等技术方面的考虑）在这里被忽略掉了。很明显，内层循环的计数值（cx2)依据的是外层循环计数的当前值（cx1),cx1在每遍数组处理开始时递减。

#### 汇编语言

一旦理解了伪码，距离用汇编语言来实现它就只有一步之遥了，只需把代码写成一个过程并且加上参数和局部变量就可以了。

```assembly
BubbleSort PROC USES eax ecx esi,
		pArray:PTR DWORD 	;pointer to array
		Count:DWORD			; array size
; Sort an array of 32-bit signed integers in ascending
; order , using the bubble sort algorithm .
; Receives : pointer to array , array size
; Returns : nothing
;-----------
	mov 	ecx,Count
	dec 	ecx			; decrement count by 1
L1:
	push 	ecx			; save outer loop count
	mov 	esi,pArray	; point to first value
L2:
	mov 	eax,[esi]	; get array value
	cmp 	[esi+4],eax	; compare a pair of values
	jg 		L3			;if [esi] <= [esi+4] don't exch
	xchg 	eax,[esi+4]	; exchange the pair
	mov 	[esi] ,eax
L3:
	add 	esi,4		; move both pointers forward
	loop 	L2			; inner loop
	pop 	ecx			; retrieve outer loop count
	loop 	L1			; else repeat outer loop
L4:
	ret
BubbleSort ENDP
```

## 9.5.2 二分查找

在日常编写应用程序的过程中，数组查找是最常用的操作之一。对于小数组（1000个元素或更小）,可以使用顺序查找（sequential search),也就是从数组的开始位置按顺序检查每个元素直到发现匹配项为止。对于有n个元素的数组，顺序查找平均需要n/2次比较。如果数组很小，花费在顺序查找上的时间几乎是意识不到的。另一方面，如果用顺序查找来处理一个有100万个元素的数组将花费大量的时间。
二分查找算法（binary search)在大数组中查找一个项时被证明是非常高效的，但该算法有一个重要的前提条件：数组元素必须已经按升序或降序排列。下面是该算法的一个非正式描述，在进行查找之前，首先要求用户输入一个整数，我们称之为searchVal:

1. 查找范围由名为first和last的下标表示，如果first>last则退出查找，表明没有匹配项。
2. 计算由下标first和last标识的数组的中点。
3. 把searchVal同数组中点处的值进行比较：
	1. 如果两个值相等则从过程中返回，EAX中包含中点值，这个返回值表明在数组中发现了匹配项。
	2. 如果searchVal大于中点处的数值，把first重设为中点之后的下一个位置。
	3. 如果searchVal小于中点处的数值，把last重设为中点之前的一个位置。
4. 重复步骤1。

二分查找非常高效，因为它使用了分治策略（divide and conquer),每次循环后查找范围都会减半，通常该算法被称为是时间复杂度为O(logn)的算法，也就是说如果数组元素增加到n倍，那么平均查找时间只增加到log n倍。由于查找时间非常短，下表简单地记录了各种不同大小的数组最多需要的比较次数
![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.7jr3rjivk140.webp)

最多比较次数是按1b(n+1)计算的，向上取整。下面以C++实现的二分查找函数适用于有符号整数数组的查找：

```c
int Binsearch(int values[],const int searchVal,int count){
	int first = 0;
	int last = count - 1;
	while( first <= last)
	{
		int mid = (last + first) / 2;
		if(values[mid] < searchVal)
			first=mid+1;
		else if(values[mid]>searchVal)
			last=mid-1;
		else
			return mid;		//成功
	}
	return - 1;				//未找到
}
```

下面是上述C++代码的汇编语言实现：

```assembly
BinarySearch PROC uses ebx edx esi edi,
			pArray:PTR DWORD,		; pointer to array
			Count:DWORD,			; array size
			searchVal:DWORD			; search value
		LOCAL 	first:DWORD,		; first position
				last:DWORD,			; last position
				mid:DWORD			; midpoint
; Search an array of signed integers for a single value.
; Receives : Pointer to array , array size , search value .
; Returns : If a match is found , EAX = the array position of the
; matching element; otherwise, EAX =-1.
	mov 	first,0	;first=0
	mov 	eax,Count; last = (count - 1)
	dec 	eax
	mov 	last,eax
	mov 	edi,searchVa1	;EDI=searchVal
	mov 	ebx,pArray		; EBX points to the array
L1:							;while first <= last
	mov 	eax,first
	cmp 	eax,last
	jg 		L5				; exit search
; mid = (1ast + first) / 2
	mov 	eax,last
	add 	eax,first
	shr 	eax,1
	mov 	mid,eax
; EDX = values [ mid ]
	mov 	esi,mid
	shl 	esi,2
; scale mid value by 4
	mov 	edx,[ebx+esi]
;EDX=values[mid]
; if ( EDX < searchval ( EDI ) )
	cmp 	edx,edi
	jge 	L2
; first = mid + 1
	mov 	eax,mid
	inc		eax
	mov 	first,eax
	jmp 	L4
; else if ( EDX > searchVal (EDI) )
L2:
	cmp	 	edx,edi
	jle 	L3
; last = mid - 1 ;
	mov 	eax,mid
	dec 	eax
	mov 	last,eax
	jmp 	L4
; else return mid
L3:
	mov 	eax,mid
; value found
	jmp 	L9
;return(mid)
L4:
	jmp 	L1
; continue the loop
L5:
	mov 	eax,-1
; search failed
L9:
	ret
BinarySearch ENDP
```

#### 测试程序

为了解释说明本章中给出的冒泡排序函数和二分查找函数，我们来写一个顺序执行以下步骤的小测试程序：

- 用随机整数填充数组
- 显示数组
- 使用冒泡排序算法对数组进行排序
- 重新显示数组
- 要求用户输入一个整数
- 使用二分查找算法在数组中查找用户输入的整数
- 显示二分查找的结果

不同的过程放在单独的源代码文件中以便定位和编辑。表9.5中列出了每个模块和它包含的内容，大多数的专业程序都是以模块化的方式组织的。

![image](https://cdn.staticaly.com/gh/YangLuchao/img_host@master/20230301/image.46dv29q2w140.webp)

除了B_main之外的所有模块都以黑厘子的方式编写，这样它们不做任何修改就可以在别的程序中使用，这一点是非常重要的，因为在将来通过重用已有代码时能够节省大量的时间，Irvine32和Irvine16链接库中也正是采用了同样的方法。下面的包含文件（Bsearch.inc)包含了主模块中调用的过程的原型：

```assembly
; Bsearch . inc - prototypes for procedures used in
;the BubbleSort/BinarySearch program.
; Searches for an integer in an array of 32-bit signed
;integers.
BinarySearch PROTO,
		pArray:PTR DWORD,		; pointer to array
		Count:DWORD,			; array size
		searchVal:DWORD			; search value
; Fills an array with 32-bit signed random integers
FillArray PROTO,
		pArray:PTR DWORD,		; pointer to array
		Count:DWORD,			; number of elements
		LowerRange:SDWORD,		; lower range
		UpperRange:SDWORD		; upper range
; Writes a 32-bit signed integer array to standard output
PrintArray PROTO,
		pArray:PTR DWORD
		Count:DWORD
; Sorts the array in ascending order
BubbleSort PROTO,
		pArray:PTR DWORD,
		Count:DWORD
```

下面是主模块B_main.asm的清单：

```assembly
TITLE Bubble Sort and Binary Search	(B_main.asm)
; Bubble sort an array of signed integers, and perform
; a binary search .
; Main module, calls Bsearch. asm, Bsort. asm, Fi11Arry. asm,
; and PrtArry . asm
INCLUDE Irvine32.inc
INCLUDE Bsearch.inc
; procedure prototypes
LOWVAL = -5000		; minimum value
HIGHVAL = +5000		; maximum value
ARRAY_SIZE = 50
; size of the array
.data
	array DWORD ARRAY_SIZE DUP(?)
,code
main PROC
	call Randomize
; Fill an array with random signed integers
	INVOKE FillArray, ADDR array, ARRAY_SIZE, LOWVAL, HIGHVAL
; Display the array
	INVOKE PrintArray, ADDR array, ARRAY_SIZE
	call WaitMsg
; Perform a bubble sort and redisplay the array
	INVOKE BubbleSort, ADDR array, ARRAY_SIZE
	INVOKE PrintArray, ADDR array, ARRAY_SIZE
;Demonstrate a binary search
	call AskForSearchVal
; returned in EAX
	INVOKE BinarySearch, ADDR array, ARRAY_SIZE, eax
	call ShowResults
	exit
main ENDP
AskForSearchVal PROC
; Prompts the user for a signed integer .
; Receives : nothing
; Returns : EAX = value input by user
.data
	prompt 	BYTE "Enter a signed decimal integer "
			BYTE "in the range of -5000 to +5000"
			BYTE "to find in the array:",0
.code
	call Crlf
	mov edx,OFFSET prompt
	call writestring
	call ReadInt
	ret
AskForSearchVal ENDP
;--
ShowResults PROC
; Displays the resulting value from the binary search.
;Receives:EAX=position number to be displayed
; Returns : nothing
.data
	msg1 BYTE "The value was not found.",0
	msg2 BYTE "The value was found at position ",0
.code
	.IF eax == -1
		mov edx,OFFSET msg1
		call writestring
	.ELSE
		mov edx,OFFSET msg2
		call writestring
		call WriteDec
	.ENDIF
	call Crlf
	call Crlf
	ret
ShowResults ENDP
END main
```

PrintArray:下面是包含PrintArray过程的模块的清单。

```assembly
TITLE PrintArray Procedure (PrtArry.asm)
INCLUDE Irvine32.inc
.code
PrintArray PROC USES eax ecx edx esi,
	pArray:PTR DWORD , ; pointer to array
	Count:DWORD; number of elements
; Writes an array of 32-bit signed decimal integers to
; standard output, separated by commas
; Receives : pointer to array , array size
; Returns : nothing
.data
	comma BYTE ",",0
.code
	mov esi,pArray
	mov ecx,Count
	cld					; direction = forward
L1: 
	lodsd				; load [ESI] into EAX
	call WriteInt		; send to output
	mov edx,OFFSET comma
	call Writestring	; display comma
	loop L1
	call Crlf
	ret
PrintArray ENDP
END
```

FillArray:下面是包含FillArray过程的模块的清单。

```assembly
TITLE FillArray Procedure	(Fi11Arry.asm)
INCLUDE Irvine32.inc
.code
;--
FillArray PROC USES eax edi ecx edx,
	pArray:PTR DWORD,		; pointer to array
	Count:DWORD ,			; number of elements
	LowerRange:SDWORD,		; lower range
	UpperRange:SDWORD
; upper range
; Fills an array with a random sequence of 32-bit signed
; integers between LowerRange and (UpperRange - 1) .
; Returns : nothing
	mov edi,pArray		; EDI points to the array
	mov ecx,Count		; loop counter
	mov edx,UpperRange
	sub edx,LowerRange	; EDX = absolute range ( 0 . . n )
L1: 
	mov eax,edx			; get absolute range
	call RandomRange
	add eax,LowerRange	; bias the result
	stosd				; store EAX into [edi]
	loop L1
	ret
FillArray ENDP
END
```

# 9.6本章小结

字符串操作指令的不同寻常之处在于不需要寄存器操作数，并且它们针对内存访问速度进行了特别的优化，这些指令包括：

- MOVS,移动字符串数据
- CMPS,比较字符串
- SCAS,扫描字符串
- STOS,存储字符串数据
- LODS,把字符串数据装入累加器

在处理字节、字或双字时，这些指令可以带后缀B,W或D。
REP指令对简单字符串操作指令进行重复，并自动增减指针寄存器。例如REPNE和SCASB指令联合使用时可以逐字节扫描内存，直到EDI指向的值和AL寄存器中的值相等为止，方向标志决定了在每次字符串操作指令重复时指针寄存器的值是增加还是减少。
字符串和数组实际上是相同的，在传统意义上，字符串是包含单字节ASCH值的数组，但现在字符串也可以是包含16位Unicode字符的数组。字符串和数组之间唯一的重要区别在于字符串通常是以空字节（值为0)结尾的。

数组操作通常是计算密集型的，这是由于数组操作几乎总是涉及到循环算法。大多数程序花费80%~90%的时间来执行一小部分代码。由此得出的结论是：可以通过减少循环内指令的数目和复杂性来加快软件的执行速度。汇编语言是代码优化的利器，因为通过汇编语言可以控制一切细节，例如，可以通过使用寄存器替换内存变量，或者通过使用本章中介绍的字符串操作指令而不是用MOV和CMP指令来优化一块代码。
本章中还介绍了一些极为有用的字符串处理过程：Str_copy过程把一个字符串复制到另外一个字符串中；Str_length过程返回字符串的长度；Str_compare过程比较两个字符串；Str_trim过程删除字符串末尾的所有特定字符；Str_ucase过程把字符串中的字符转换成大写形式。
基址变址操作数使处理二维数组（表）更简单，可以把基址寄存器设置为表的行地址，而把变址寄存器设置为某一行内列的偏移。在32位模式下，任何32位通用寄存器都可用做基址和变址寄存器。在16位模式下，基址寄存器必须是BX和BP,变址寄存器必须是SI和DI。相对基址变址操作数与基址变址操作数类似，它们之间的区别在于前者还可以包含数组的名字：

```assembly
[ebx+esi]			;基址变址
array [ebx + esi]	;相对基址变址
```

本章还给出了冒泡排序算法和二分查找算法的汇编语言实现。冒泡排序算法按升序或降序新排列数组元素，它对于不超过几百个元素的数组来说效率已经足够高了，但是对于大数组来说则效率很低。二分查找算法允许在已经排好序的数组内快速查找一个值，该算法很容易用汇编语言实现。